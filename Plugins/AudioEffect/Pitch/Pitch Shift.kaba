# Image = hui:properties
# Title[Deutsch] = Pitch Shift

use "../../api.kaba"

const string AutoConfigFactor = "0:2:0.1:100:%"


class PitchShift extends AudioEffect
	class Config extends Module.Config
		float factor

		override void reset()
			factor = 1

	Config config

	complex[] buf1
	complex[] buf2
		
	void shift_buf(float[] b, float factor)
		fft_r2c(b, buf1)
		int N = b.num
		int NN = buf1.num
		buf2.resize(NN)
		for i in 0:NN
			buf2[i] = complex(0, 0)
			
		int i0 = 10.0 / (session.sample_rate() / 2) * NN
		print("{{i0}} / {{NN}}")
		
		for i in 0:i0
			buf2[i] += buf1[i]

		int D = max(100, sqrt(N)*2)
		for i in i0:NN
			float kk = factor * i
			float x0 = pi * kk
			int j0 = max(kk - D, 0)
			int j1 = min(kk + D, NN)
			complex M0 = complex(cos(x0), sin(x0)) * sin(x0)
			for j in j0:j1
				float dk = kk - j
				float x = pi * dk
				if x == 0
					buf2[j] += buf1[i]
				else
					complex M = M0 * (1/x)
					buf2[j] += M * buf1[i]

		fft_c2r_inv(buf2, b)
		b /= N

	override void process(AudioBuffer buf)
		shift_buf(buf.l, config.factor)
		if buf.channels > 1
			shift_buf(buf.r, config.factor)
		buf1.clear()
		buf2.clear()

