# Image = hui:properties
# Title[Deutsch] = Equalizer 15
use hui
use tsunami




const int NUM_BANDS = 15
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0


class Equalizer15 extends AudioEffect
	class Config extends Module.Config
		float[NUM_BANDS] factor

		override void reset()
			for f in factor
				f = 1


	Config config

	float[NUM_BANDS] FreqMin, FreqMax

	complex[] f

	override void __init__()

		for i in 0:NUM_BANDS
			FreqMin[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i - 0.5))
			FreqMax[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i + 0.5))
		FreqMin[0] = 0
		FreqMax[NUM_BANDS - 1] = 1000000

	void equalize(out float[] b)
		int _STACK_ALIGNMENT_
		float sample_rate = session.sample_rate()
	
		# transform
		int len2 = len(b) / 2 + 1
		f.resize(len2)
		fft_r2c(b, f)

		# equalize
		float w = float(len(b)) / sample_rate
		for i in 0:NUM_BANDS
			int j0 = clamp(FreqMin[i] * w, 0, len2)
			int j1 = clamp(FreqMax[i] * w, 0, len2)
			float ff = config.factor[i] / len(b)
			for j in j0:j1
				f[j] = f[j] * ff
		
		# transform back
		fft_c2r_inv(f, b)

	override void process(out AudioBuffer buf)
		for c in buf.c
			equalize(c)
		f.clear()
	
	override ConfigPanel *create_panel()
		return new Equalizer15Panel(self)

class Equalizer15Panel extends ConfigPanel
	override Equalizer15 *c

	override void __init__(Module _eq)
		add_grid("", 0, 1, "grid")
		set_target("grid")
		for i in 0:NUM_BANDS
			add_slider("!expandy,vertical,height=200", i, 0, "slider_{{i}}")
			#add_string("slider_" + i, "0.5:0")
		add_label("20", 0, 1, "")
		add_label("54", 2, 1, "")
		add_label("140", 4, 1, "")
		add_label("390", 6, 1, "")
		add_label("1k", 8, 1, "")
		add_label("2k8", 10, 1, "")
		add_label("7k5", 12, 1, "")
		add_label("20k", 14, 1, "")
	
		event("*", on_event)
	
		update()

	void on_event()
		Event *e = hui.get_event()
		for i in 0:NUM_BANDS
			if e.id == ("slider_{{i}}")
				c.config.factor[i] = get_float("") * 2
				changed()
		
	override void update()
		for i in 0:NUM_BANDS
			set_float("slider_{{i}}", c.config.factor[i] / 2)
	