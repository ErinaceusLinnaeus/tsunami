// Image = hui:properties
// Title[Deutsch] = Tiefpass Filter
use "../../api.kaba"

const string AUTO_CONFIG_FREQUENCY = "0:5000:0.1:1:Hz"

class Config extends PluginData
	float frequency
	override void reset()
		frequency = 1000

class LowPassFilter extends AudioEffect
	Config config
	
	
	float[2] temp
	bool[2] used
	override void reset_state()
		used[0] = false
		used[1] = false

	// y[i] := α * x[i] + (1-α) * y[i-1]
	void do(float[] buf, int c)
		float RC = 1 / (2 * pi * config.frequency)
		float dt = 1.0 / session.sample_rate()
		float a = dt / (RC + dt)
		float b = 1 - a
		
		if used[c]
			buf[0] = a * buf[0] + b * temp[c]
	
		for i in 1:buf.num
			buf[i] = a * buf[i] + b * buf[i-1]
		
		used[c] = true
		temp[c] = buf[buf.num-1]
			
	override void process(AudioBuffer buf)
		do(buf.r, 0)
		if buf.channels > 1
			do(buf.l, 1)
