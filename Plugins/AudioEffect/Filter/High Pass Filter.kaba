// Image = hui:properties
// Title[Deutsch] = Hochpass Filter
use "../../api.kaba"

const string AUTO_CONFIG_FREQUENCY = "0:5000:0.1:1:Hz"

class Config extends PluginData
	float frequency
	override void reset()
		frequency = 1000

class LowPassFilter extends AudioEffect
	Config config
	
	
	float[2] _in, _out
	bool[2] used
	override void reset_state()
		used[0] = false
		used[1] = false

	// y[i] := Î± * (y[i-1] + x[i] - x[i-1])
	void do(float[] buf, int c)
		float RC = 1 / (2 * pi * config.frequency)
		float dt = 1.0 / session.sample_rate()
		float a = RC / (RC + dt)
		float prev_in = 0
		if used[c]
			float t = buf[0]
			buf[0] = a * (buf[0] + _out[c] - _in[c])
			prev_in = t

	
		for i in 1:buf.num
			float t = buf[i]
			buf[i] = a * (buf[i] + buf[i-1] - prev_in)
			prev_in = t
		
		used[c] = true
		_in[c] = prev_in
		_out[c] = buf[buf.num - 1]
		
	override void process(AudioBuffer buf)
		do(buf.l, 0)
		if buf.channels > 1
			do(buf.r, 1)
