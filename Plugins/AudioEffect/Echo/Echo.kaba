# Image = hui:copy
# Title[Deutsch] = Echo

use "../../api.kaba"

const string AUTO_CONFIG_DELAY = "0:1:1:1000:ms"
const string AUTO_CONFIG_VOLUME = "0:2:0.1:100:%"
const string AUTO_CONFIG_FEEDBACK = "0:1:0.1:100:%"


class Config extends PluginData
	float delay
	float volume
	float feedback

	override void reset()
		volume = 0.2
		feedback = 0.5
		delay = 0.15


class Echo extends AudioEffect
	Config config
	float[] buf_r, buf_l
	int offset


	void do_delay(float[] b, float[] d)
		int p = offset % d.num
		for bb in b
	
			# old value
			float b0 = bb
		
			# add echo
			bb = bb + d[p] * config.volume
			#clampf(b[i], -1, 1)
		
			# add to echo
			d[p] = b0 + d[p] * config.feedback
		
			p ++
			if p >= d.num
				p = 0


	override void process(AudioBuffer buf)

		int delay_samples = session.sample_rate() * config.delay
		buf_r.resize(delay_samples)
		buf_l.resize(delay_samples)
	
		do_delay(buf.l, buf_l)
		if buf.channels > 1
			do_delay(buf.r, buf_r)
	
		offset += len(buf)
	
	override void reset_state()
		buf_r.clear()
		buf_l.clear()
		offset = 0
		on_config()
