# Image = hui:copy
# Title[Deutsch] = Echo

use tsunami
use helper.grid

const AUTO_CONFIG_DELAY = "0:1:1:1000:ms"
const AUTO_CONFIG_VOLUME = "0:2:0.1:100:%"
const AUTO_CONFIG_FEEDBACK = "0:1:0.1:100:%"




class Echo extends AudioEffect
	class Config extends Module.Config
		var delay: float
		var volume: float
		var feedback: float

		func override reset()
			volume = 0.2
			feedback = 0.5
			delay = 0.15
	var config: Config
	var buf_temp: AudioBuffer
	var offset: int


	func do_delay(out b: float[], out d: float[])
		int p = offset % len(d)
		for bb in b
	
			# old value
			float b0 = bb
		
			# add echo
			bb = bb + d[p] * config.volume
			#clampf(b[i], -1, 1)
		
			# add to echo
			d[p] = b0 + d[p] * config.feedback
		
			p ++
			if p >= len(d)
				p = 0


	func override process(out buf: AudioBuffer)

		int delay_samples = session.sample_rate() * config.delay
		buf_temp.set_channels(buf.channels)
		buf_temp.resize(delay_samples)
	
		for c,i in buf.c
			do_delay(c, buf_temp.c[i])
	
		offset += len(buf)
	
	func override reset_state()
		buf_temp.clear()
		offset = 0
		on_config()
