# Image = hui:zoom-in
# Title[Deutsch] = LautstÃ¤rke

use tsunami


func abs_max(buf: float[]) -> float
	float m = 0.0
	for b in buf
		m = max(m, abs(b))
	return m


class Volume extends AudioEffect
	class Config extends Module.Config
		var volume: float
		var maximize: bool
	
		func override reset()
			volume = 1
			maximize = false

	var config: Config

	func override create_panel() -> ConfigPanel*
		return new Volume.Panel(self)

	func override process(out buf: AudioBuffer)
		float factor = config.volume
		if config.maximize
			float _max = 0
			for cc in buf.c
				_max = max(_max, abs_max(cc))
			factor = 1 / _max
		for cc in buf.c
			for b in cc
				b = clamp(b * factor, -1, 1)
	
	class Panel extends ConfigPanel
		var override c: Volume*
		var slider: Slider*
		func override __init__(_v: Module)
			add_grid("!width=400", 0, 0, "table1")
			set_target("table1")
			add_check_box("Maximize", 0, 1, "max")
			add_grid("", 0, 0, "table2")
			set_target("table2")
			add_slider("!expandx", 0, 0, "volume_slider")
			add_spin_button("\\0\\1000\\0.1", 1, 0, "volume")
			add_label("%", 2, 0, "")
		
			slider = new Slider(self, "volume_slider", "volume", 0, 2, 100, on_volume, c.config.volume)
	
			event("max", on_max)
	
		func override __delete__()
			del slider

		func override update()
			slider.set(c.config.volume)
			check("max", c.config.maximize)
			enable("volume", !c.config.maximize)
			enable("volume_slider", !c.config.maximize)

		func mut on_max()
			c.config.maximize = is_checked("max")
			enable("volume", !c.config.maximize)
			enable("volume_slider", !c.config.maximize)
			changed()

		func mut on_volume()
			c.config.volume = slider.get()
			changed()

