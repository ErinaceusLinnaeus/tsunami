// Image = hui:properties
// Title[Deutsch] = Equalizer

use "../../api.kaba"
use "../../grid.kaba"



const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0
const int MIN_VOL = 0-45
const int MAX_VOL = 45

enum
	POINT_PEAK
	POINT_LOW_PASS
	POINT_HIGH_PASS
	//POINT_SHELF

class Peak
	bool enabled
	int mode
	float freq
	float gain
	float q
	complex z(float g0)
		complex r
		r.x = freq
		r.y = gain + g0
		return r
	vector v(float g0)
		return vector(freq, gain + g0, q)
	float dgain(float _pitch)
		if !enabled
			return 0
		float pitch = log(freq)
		float d = (_pitch-pitch) / (pitch * q)
		//float df = p.freq * p.q
		if mode == POINT_PEAK
			//float d = (p.freq - f) / df
			return gain * exp( - d*d)
		if mode == POINT_LOW_PASS
			if d < 0
				return 0
			return gain * d
		if mode == POINT_HIGH_PASS
			if d > 0
				return 0
			return - gain * d //- exp(min(-d, 5))
Peak _Peak(int mode, float f, float gain)
	Peak p
	p.enabled = true
	p.mode = mode
	p.freq = f
	p.gain = gain
	p.q = 0.1
	return p
				

class EqCurve extends Curve
	float gain
	Peak[] peaks
	void set(Config c)
		gain = c.gain
		peaks = c.peaks
	override float get(float f)
		float pitch = log(f)
		float r = gain
		for p in peaks
			r += p.dgain(pitch)
		return r

class Config extends PluginData
	float gain
	Peak[] peaks

	override void reset()
		gain = 0
		peaks.clear()
		peaks.add(_Peak(POINT_HIGH_PASS, 100, -10))
		peaks.add(_Peak(POINT_LOW_PASS, 10000, -10))
		//for p in peaks
		//	p.enabled = false

class Equalizer extends AudioEffect

	Config config

	void equalize(float[] b)
		float sample_rate = session.sample_rate()
		complex[] f
	
	
		// transform
		int len2 = b.num / 2 + 1
		f.resize(len2)
		fft_r2c(b, f)
	
		EqCurve curve
		curve.set(config)
	
		// equalize
		f *= (1.0 / b.num)
		float w = i2f(b.num) / sample_rate
		for i in 1:f.num
			float freq = i2f(i) / w
			float param = lin2log(freq, MIN_FREQ, MAX_FREQ)
			float db = curve.get(param)
			f[i] = f[i] * pow(10, db / 20)
		
		// transform back
		fft_c2r_inv(f, b)

	override void process(AudioBuffer buf)
		equalize(buf.r)
		if buf.channels > 1
			equalize(buf.l)

	override ConfigPanel *create_panel()
		return new EqualizerPanel(self)


class EqualizerGrid extends Grid
	Config *c
	EqualizerPanel *cp
	int hover, selected
	override void on_draw(Painter *p)
		// curve + area below
		EqCurve curve
		curve.set(*c)
		draw_curve_c(p, curve, true)
	
		// points
		for pk,i in c.peaks
			complex pp = project2(pk.z(c.gain))
			
			p.set_line_width(2)
			color col = point_color(i == selected, i == hover)
			col.a = 0.6
			p.set_color(col)
			p.draw_line(pp.x, 0, pp.x, p.height)
			
			draw_point2(p, pk.z(c.gain), i == selected, i == hover)
			if i == hover
				p.set_color(Red)
				p.draw_str(pp.x - 20, pp.y - 50, pk.freq.str2(1) + " Hz")
				p.draw_str(pp.x - 20, pp.y - 35, pk.gain.str2(1) + " dB")
				p.draw_str(pp.x - 20, pp.y - 20, "q " + pk.q.str2(3))
			

	// left button down -> select
	override void on_left_button_down()
		selected = hover
		redraw()

	override void on_mouse_wheel()
		if hover >= 0
			if HuiGetEvent().scroll_y > 0
				c.peaks[hover].q *= 1.04
			else
				c.peaks[hover].q /= 1.04
			cp.changed()
			redraw()

	// right button down -> new point
	override void on_right_button_down()
		vector m = get_mouse3(0)
		c.peaks.add(_Peak(POINT_PEAK, m.x, m.y - c.gain))
		selected = c.peaks.num - 1
		hover = selected
		cp.changed()
		redraw()

	override void on_mouse_move()
		// move
		if HuiGetEvent().button_l and (selected >= 0)
			c.peaks[selected].freq = get_mouse2().x
			c.peaks[selected].gain = get_mouse2().y - c.gain
			/*float xmin = MIN_FREQ
			float xmax = MAX_FREQ
			if selected > 0
				xmin = c.curve[selected - 1].x
			if selected < c.curve.num - 1
				xmax = c.curve[selected + 1].x
			c.curve[selected].x = clamp(c.curve[selected].x, xmin, xmax)
			c.curve[selected].y = clamp(c.curve[selected].y, MIN_VOL, MAX_VOL)
			if selected == c.curve.num - 1
				c.curve[selected].x = MAX_FREQ
			if selected == 0
				c.curve[selected].x = MIN_FREQ*/
			cp.changed()
		else
		// mouse over?
			hover = -1
			for i in 0:c.peaks.num
				if mouse_over_point2(c.peaks[i].z(c.gain))
					hover = i
		redraw()

	// [Delete] -> remove point
	override void on_key_down()
		if HuiGetEvent().key_code == KEY_DELETE
			if selected >= 0
				c.peaks.remove(selected)
				cp.changed()
				selected = -1
				hover = -1
				redraw()

class EqualizerPanel extends ConfigPanel
	EqualizerGrid grid
	int num_peaks
	override void __init__(Equalizer *e)
		super.__init__(e)
		add_grid("", 0, 0, "grid")
		set_target("grid")
		add_drawing_area("!grabfocus,height=250", 0, 0, "drawing")
		add_grid("", 0, 1, "stuff")
		add_grid("", 0, 2, "peaks")
		set_target("stuff")
		add_label("!width=80\\Gain", 0, 0, "")
		add_spin_button("!range=-40:40:0.1,expandx", 1, 0, "gain")
		add_label("dB", 2, 0, "")
	
		grid.init(self, "drawing")
		grid.x_axis.set(MIN_FREQ, MAX_FREQ, true, "Hz")
		grid.y_axis.set(MIN_VOL, MAX_VOL, false, "db")
		
		grid.cp = self
		grid.c = &e.config
		num_peaks = 0
		
		event("gain", &on_gain)
	
		grid.redraw()
		
	void on_gain()
		grid.c.gain = get_float("")
		grid.redraw()
		changed()
		
	override void update()
		grid.redraw()
		set_float("gain", grid.c.gain)
		
		for i in num_peaks:grid.c.peaks.num
			set_target("peaks")
			add_grid("", i, 0, "peak-" + i)
			set_target("peak-" + i)
			add_check_box("" + (i+1), 0, 0, "enabled-" + i)
			add_combo_box("Peak\\Low pass\\High pass\\Shelf...", 0, 1, "mode-" + i)
			/*add_spin_button("!range=::0.1", 0, 2, "freq-" + i)
			add_spin_button("!range=::0.1", 0, 2, "gain-" + i)
			add_spin_button("!range=::0.001", 0, 2, "q-" + i)*/
			/*add_label("", 0, 2, "freq-" + i)
			add_label("", 0, 3, "gain-" + i)
			add_label("", 0, 4, "q-" + i)*/
		for i in grid.c.peaks.num:num_peaks
			delete_control("peaks-" + i)
		num_peaks = grid.c.peaks.num
		
		for pk,i in grid.c.peaks
			check("enabled-" + i, pk.enabled)
			set_int("mode-" + i, pk.mode)
			/*set_float("freq-" + i, pk.freq)
			set_float("gain-" + i, pk.gain)
			set_float("q-" + i, pk.q)*/
			/*set_string("freq-" + i, pk.freq.str2(1) + " Hz")
			set_string("gain-" + i, pk.gain.str2(1) + " dB")
			set_string("q-" + i, "q: " + pk.q.str2(3))*/

	override void set_large(bool large)
		if large
			set_options("drawing", "height=380")
		else
			set_options("drawing", "height=250")

