// Image = hui:properties
// Title[Deutsch] = Tiefpass Filter
use "../../api.kaba"

const string AUTO_CONFIG_FREQUENCY = "0:5000:0.1:1:Hz"

class Config extends PluginData
	float frequency
	override void reset()
		frequency = 1000

class LowPassFilter extends AudioEffect
	Config config
	
	
	float l, r
	bool used
	override void reset_state()
		used = false

	// y[i] := α * x[i] + (1-α) * y[i-1]
	override void process(AudioBuffer buf)
		float RC = 1 / (2 * pi * config.frequency)
		float dt = 1.0 / sample_rate
		float a = dt / (RC + dt)
		float b = 1 - a
		
		if used
			buf.r[0] = a * buf.r[0] + b * r
			buf.l[0] = a * buf.l[0] + b * l
	
		for i in 1:buf.length
			buf.r[i] = a * buf.r[i] + b * buf.r[i-1]
			buf.l[i] = a * buf.l[i] + b * buf.l[i-1]
		
		used = true
		r = buf.r[buf.length-1]
		l = buf.l[buf.length-1]
