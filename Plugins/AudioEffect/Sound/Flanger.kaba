// Image = hui:copy
// Title[Deutsch] = Flanger

use "../../api.kaba"

const string AUTO_CONFIG_DELAY = "0:0.02:0.1:1000:ms"
const string AUTO_CONFIG_SPEED = "0:2:0.01:1:Hz"
const string AUTO_CONFIG_DEPTH = "0:1:0.1:100:%"


class PluginConfig extends PluginData
	float delay
	float speed
	float depth
	override void reset()
		depth = 0.5
		speed = 0.3
		delay = 0.01


class Flanger extends AudioEffect
	PluginConfig config
	
	float[] temp_r
	float[] temp_l
	int offset
	override void reset_state()
		temp_r.clear()
		temp_l.clear()
		offset = 0

	void do_flanger(float[] b, float[] temp)
		int p = offset % temp.num
		float sample_rate = session.sample_rate()
	
		float f_a = (1 - 0.5 * config.depth) * config.delay
		float f_b = 0.5 * config.depth * config.delay
		float f_c = 1.0 / sample_rate * 2 * pi * config.speed
	
		for bb,i in b
	
			// old value
			temp[p] = bb
		
			// shift
			float dt = f_a - cos((i + offset) * f_c) * f_b
			int dp = dt * sample_rate
			int p2 = p - dp
			if p2 < 0
				p2 += temp.num
		
			// add echo
			bb += temp[p2]
		
			p ++
			if p >= temp.num
				p = 0

	
	override void process(AudioBuffer buf)
		int delay_samples = session.sample_rate() * config.delay + 1
		temp_r.resize(delay_samples)
		temp_l.resize(delay_samples)
	
		do_flanger(buf.r, temp_r)
		do_flanger(buf.l, temp_l)
	
		offset += buf.length
