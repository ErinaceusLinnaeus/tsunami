// Image = hui:back
// Title[Deutsch] = Monofizieren
use "../../api.kaba"

const string AUTO_CONFIG_PANNING = "-1:1:1:100:%"
const string AUTO_CONFIG_DISTANCE = "-2:2:0.1:100:cm"
const string AUTO_CONFIG_INVERT = ""

class Config extends PluginData
	float panning
	bool invert
	float distance
	
	override void reset()
		panning = 0
		distance = 0
		invert = false
	

class Monofier extends AudioEffect
	Config config
	float[] temp
	int offset
	
	override void reset_state()
		offset = 0
	
	override void process(AudioBuffer buf)
		if buf.channels == 1
			return
		
		int n = (session.sample_rate() * abs(config.distance) / 320.0)
		temp.resize(n)
			
		float a = (config.panning / 2) + 0.5
		float b = (-config.panning / 2) + 0.5
		if config.invert
			b = -b
		
		if config.distance == 0
			for i in 0:buf.length
				float x = a * buf.r[i] + b * buf.l[i]
				buf.l[i] = x
				buf.r[i] = x
		else if config.distance > 0
			for i in 0:buf.length
				float x = a * buf.r[i] + b * temp[offset]
				temp[offset] = buf.l[i]
				offset ++
				if offset >= n
					offset = 0
				buf.l[i] = x
				buf.r[i] = x
		else if config.distance < 0
			for i in 0:buf.length
				float x = a * temp[offset] + b * buf.l[i]
				temp[offset] = buf.r[i]
				offset ++
				if offset >= n
					offset = 0
				buf.l[i] = x
				buf.r[i] = x
