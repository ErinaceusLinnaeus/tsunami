# Image = hui:back
# Title[Deutsch] = Monofizieren
use tsunami

const string AUTO_CONFIG_PANNING = "-1:1:1:100:%"
const string AUTO_CONFIG_DISTANCE = "-2:2:0.1:100:cm"
const string AUTO_CONFIG_INVERT = ""

const float SPEED_OF_SOUND = 320.0 # m/s


class Monofier extends AudioEffect
	class Config extends Module.Config
		float panning
		bool invert
		float distance
		override void reset()
			panning = 0 # INVERTED.... for historic reasons...
			distance = 0
			invert = false
	

	Config config
	float[] temp
	int offset

	override void reset_state()
		offset = 0

	override void process(out AudioBuffer buf)
		if buf.channels == 1
			return

		int n = (session.sample_rate() * abs(config.distance) / SPEED_OF_SOUND)
		temp.resize(n)

		float sl = (1 + config.panning) / 2
		float sr = (1 - config.panning) / 2
		if config.invert
			sl = -sl
		
		if config.distance == 0
			# pure mix
			for i in 0:len(buf)
				float x = sl * buf.c[0][i] + sr * buf.c[1][i]
				buf.c[0][i] = x
				buf.c[1][i] = x
		else if config.distance > 0
			# delay right channel
			for i in 0:len(buf)
				float x = sl * buf.c[0][i] + sr * temp[offset]
				temp[offset] = buf.c[1][i]
				offset ++
				if offset >= n
					offset = 0
				buf.c[0][i] = x
				buf.c[1][i] = x
		else if config.distance < 0
			# delay left channel
			for i in 0:len(buf)
				float x = sl * temp[offset] + sr * buf.c[1][i]
				temp[offset] = buf.c[0][i]
				offset ++
				if offset >= n
					offset = 0
				buf.c[0][i] = x
				buf.c[1][i] = x
