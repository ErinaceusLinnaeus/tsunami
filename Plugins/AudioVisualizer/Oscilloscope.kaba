# Image = hui:media-play
# Title[Deutsch] = Oszilloskop
use hui
use "../api.kaba"

const int NUM_SAMPLES = 2048

class OscilloscopePanel extends ConfigPanel
	override Oscilloscope* c
	
	override void __init__(Module* _osci)
		from_source("
DrawingArea area '' height=200 expandy")
		
		c.panel = &self
	
		event_x("area", "hui:draw", on_draw)
	
	override void __delete__()
		c.panel = nil
	
	void on_draw(Painter p)
		float w = p.width
		float h = p.height
		
		p.set_color(colors.background)
		p.draw_rect(0, 0, w, h)
		
		AudioBuffer *buf = &c.buffer
		p.set_color(color(0.2,0,1,0))
		#p.set_line_width(0.5)
		float x0, y0, x1, y1
		for i in 0:buf.length
			x1 = w * (buf.l[i] / 2 + 0.5)
			y1 = h * (buf.r[i] / 2 + 0.5)
			p.set_color(color(i2f(i) / NUM_SAMPLES, 0,1,0))
			if i > 0
				p.draw_line(x0, y0, x1, y1)
			x0 = x1
			y0 = y1

class Oscilloscope extends AudioVisualizer
	int n
	AudioBuffer buffer
	OscilloscopePanel *panel
	
	override void __init__()
		panel = nil
	
	override void reset_state()
		n = 0
	
	override void process(AudioBuffer buf)
		buffer = buf
		if panel
			panel.redraw("area")
		
	override ConfigPanel* create_panel()
		return new OscilloscopePanel(&self)

