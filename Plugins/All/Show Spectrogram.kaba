# Image = hui:find
# Title[Deutsch] = Spektrum zeigen
use hui
use image
use "../api.kaba"
use "../helper/grid.kaba"

##show
##disasm

const int N = 4096*2 # fft size
const int NT = 512 # time bins
const int NF = 512*2 # freq bins
const float MIN_FREQ = 60.0
const float MAX_FREQ = 3000.0
const float MIN_VOL = 0.0 - 60.0
const float MAX_VOL = 0.0

Image im0

class SpecGrid extends Grid
	override void on_draw(Painter c)
		rect r = plot_rect
		int w = r.width()
		int h = r.height()
		
		float fy = im0.height / r.height()
		float ya = (log(y_axis.val_max) - log(y_axis.val_min)) / (log(y_axis.val_max_max) - log(y_axis.val_min_min)) * fy
		float yb = (log(y_axis.val_max_max) - log(y_axis.val_max)) / (log(y_axis.val_max_max) - log(y_axis.val_min_min)) * im0.height
		float fx = im0.width / r.width()
		float xa = (x_axis.val_max - x_axis.val_min) / (x_axis.val_max_max - x_axis.val_min_min) * fx
		float xb = (x_axis.val_min - x_axis.val_min_min) / (x_axis.val_max_max - x_axis.val_min_min) * im0.width
		
		Image im
		im.create(w, h, Black)
		for x in 0:w
			for y in 0:h
				im.set_pixel(x, y, im0.get_pixel(x * xa + xb, y * ya + yb))
		c.draw_image(r.x1, r.y1, im)
	
		#Image *im = im0.scale(r.width(), r.height())
#		c.DrawImage(r.x1, r.y1, *im)
#		delete(im)

class SpectrumDialog extends Dialog
	SpecGrid grid
	void __init__(ShowSpectrogram plugin, Song song)
		super.__init__("Spectrum", 600, 460, plugin.session.win, true)
		set_options("", "headerbar,resizable")
		from_source("
Grid ? ''
	DrawingArea drawing_area ''
	---|
	Grid ? '' buttonbar
		Button close 'Close' image=hui:close")
		
		AudioView* view = plugin.session.view
		
		grid.init(self, "drawing_area")
		grid.x_axis.set(view.sel.range().offset / float(song.sample_rate), view.sel.range().end() / float(song.sample_rate), false, "s")
		grid.y_axis.set(MIN_FREQ, MAX_FREQ, true, "Hz")
		#grid.allow_bg = false
	
		event("close", on_close)

	void on_close()
		destroy()

color f2col(float _f)
	let f = sqrt(_f)
	if f < 0.333
		return color(f * 3, 0, 0, f * 3)
	else if f < 0.6666
		return color(1, f * 3 - 1, 0, 2 - f * 3)
	return color(1, 1, f * 3 - 2, 0)

class ShowSpectrogram extends SongPlugin

	SpectrumDialog *dlg
	float[NF][NT] sg

	void show_spec(Song *song)
		dlg = new SpectrumDialog(self, song)
	
		AudioBuffer buf
		SongRenderer* renderer = new SongRenderer(song)
		renderer.render(session.view.sel.range(), buf)
		del renderer
	
		complex[] f
		float[] sub
		sub.resize(N)
	
		float _max = 0
	
		float log_max_min = log(MAX_FREQ / MIN_FREQ)
	
		for t in 0:NT
			# time slice
			int offset = (buf.length - N) * t / NT
		
			for j in 0:N
				sub[j] = buf.r[offset + j]
			fft_r2c(sub, f)
			float ww = float(N) / song.sample_rate
			for i in 0:NF
				# frequency slice
				float fmin = MIN_FREQ * exp( log_max_min / (NF - 1) * i)
				float fmax = MIN_FREQ * exp( log_max_min / (NF - 1) * (i + 1))
				int j0 = fmin * ww
				int j1 = fmax * ww + 1
				j0 = clamp(j0, 0, len(f))
				j1 = clamp(j1, 0, len(f))
				
				# find maximum amplitude in that frequency interval
				float bmaxr = 0
				for j in j0:j1
					float amp = f[j].x * f[j].x + f[j].y * f[j].y
					if bmaxr < amp
						bmaxr = amp
				_max = max(_max, bmaxr)
				sg[t][i] = bmaxr
	
		im0.create(NT, NF, Black)
		for t in 0:NT
			for i in 0:NF
				float ff = sg[t][i] / _max
				im0.set_pixel(t, NF - i - 1, f2col(ff))
	

		dlg.redraw("drawing_area")

		dlg.run()
		del dlg

	override void apply()
	
		if session.view.sel.range().length > 0
			show_spec(song)
		else
			session.e("'Show Spectrogram.kaba': Needs some selection!")
