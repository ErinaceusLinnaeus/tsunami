// Image = hui:find
// Title[Deutsch] = Spektrum zeigen
use "../api.kaba"
use "../grid.kaba"

//#show
//#disasm

const int N = 4096
const int NT = 512
const int NF = 512
const float MIN_FREQ = 80.0
const float MAX_FREQ = 10000.0
const float MIN_VOL = 0.0 - 60.0
const float MAX_VOL = 0.0

SpectrumDialog *dlg

float sg[NT][NF]
Image im0

class SpecGrid : Grid
	overwrite void onDraw(Painter *c)
		float w = c.width
		float h = c.height
	
		rect r = plot_rect
	
		Image *im = im0.scale(r.width(), r.height())
		c.DrawImage(r.x1, r.y1, *im)
		delete(im)

class SpectrumDialog : Dialog
	SpecGrid grid
	void __init__()
		super.__init__("Spectrum", 600, 460, MainWin, true)
		AddGrid("", 0, 0, 1, 2, "table1")
		SetTarget("table1", 0)
		AddDrawingArea("", 0, 0, 0, 0, "drawing_area")
		AddGrid("!noexpandy", 0, 1, 2, 1, "table2")
		SetTarget("table2", 0)
		AddText("", 0, 0, 0, 0, "")
		AddButton("!width=120\\Close", 1, 0, 0, 0, "close")
		SetImage("close", "hui:close")
		
		grid.init(self, "drawing_area")
		grid.x_axis.set(0, 1, false, "")
		grid.y_axis.set(MIN_FREQ, MAX_FREQ, true, "Hz")
		//grid.allow_bg = false
	
		EventM("close", self, &OnClose)

	void OnClose()
		delete self

color f2col(float f)
	f = sqrt(f)
	if f < 0.333
		return color(f * 3, 0, 0, f * 3)
	else if f < 0.6666
		return color(1, f * 3 - 1, 0, 2 - f * 3)
	return color(1, 1, f * 3 - 2, 0)

void show_spec()
	dlg = new SpectrumDialog
	
	BufferBox buf
	renderer.RenderAudioFile(audio, audio.selection, buf)
	
	complex f[]
	float sub[]
	sub.resize(N)
	
	float _max = 0
	
	float log_max_min = log(MAX_FREQ / MIN_FREQ)
	
	for int t, 0, NT
		int offset = (buf.num - N) * t / NT
		
		for int j, 0, N
			sub[j] = buf.r[offset + j]
		fft_r2c(sub, f)
		float ww = i2f(N) / audio.sample_rate
		for int i, 0, NF
			float fmin = MIN_FREQ * exp( log_max_min / (NF - 1) * i)
			float fmax = MIN_FREQ * exp( log_max_min / (NF - 1) * (i + 1))
			int j0 = fmin * ww
			int j1 = fmax * ww + 1
			j0 = clamp(j0, 0, f.num)
			j1 = clamp(j1, 0, f.num)
			float bmaxr = 0
			for int j, j0, j1
				float amp = f[j].x * f[j].x + f[j].y * f[j].y
				if bmaxr < amp
					bmaxr = amp
			_max = max(_max, bmaxr)
			sg[t][i] = bmaxr
	
	im0.create(NT, NF, Black)
	for int t, 0, NT
		for int i, 0, NF
			float ff = sg[t][i] / _max
			im0.setPixel(t, NF - i - 1, f2col(ff))
	

	dlg.Redraw("drawing_area")

	dlg.Run()

void main()
	
	if audio.selection.length > 0
		show_spec()
	else
		logging.Error("'Show Spectrogram.kaba': Needs some selection!")
