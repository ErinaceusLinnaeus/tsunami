// Image = hui:find
// Title[Deutsch] = Spektrum zeigen
use "../api.kaba"
use "../grid.kaba"

const int N = 512
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0
const float MIN_VOL = 0.0 - 60.0
const float MAX_VOL = 0.0

SpectrumDialog *dlg

enum
	TypeLinear
	TypeRoot2
	TypeLogarithm

//float x[], yr[], yl[]
float ffr[], ffl[]

GridData grid

int w, h

float todb(float v)
	return log(v) / log(10) * 10

float peak_max

void CreateLines(int type, float x[], float yr[], float yl[])

	x.clear()
	yr.clear()
	yl.clear()
	for int i, 0, N
		x.add(log2lin(i2f(i) / i2f(N), MIN_FREQ, MAX_FREQ))
		if type == TypeLinear
			grid.y_axis.set(0, peak_max, false, "")
			yr.add(ffr[i])
			yl.add(ffl[i])
		else if type == TypeRoot2
			grid.y_axis.set(0, sqrt(peak_max), false, "")
			yr.add(pow(ffr[i], 0.5))
			yl.add(pow(ffl[i], 0.5))
		else //if type == TypeLogarithm
			float vol_max = todb(peak_max)
			float vol_min = vol_max - 60
			grid.y_axis.set(vol_min, vol_max, false, "db")
			yr.add(clamp(todb(ffr[i]), vol_min, vol_max))
			yl.add(clamp(todb(ffl[i]), vol_min, vol_max))

class SpectrumDialog : Dialog
	void __init__()
		super.__init__("Spectrum", 600, 460, MainWin, true)
		AddControlTable("", 0, 0, 1, 2, "table1")
		SetTarget("table1", 0)
		AddDrawingArea("", 0, 0, 0, 0, "drawing_area")
		AddControlTable("!noexpandy", 0, 1, 2, 1, "table2")
		SetTarget("table2", 0)
		AddComboBox("Linear\\Square Root\\Logarithm", 0, 0, 0, 0, "type")
		AddButton("!width=120\\Close", 1, 0, 0, 0, "close")
		SetImage("close", "hui:close")
	
		EventM("type", self, &OnType)
		EventM("hui:close", self, &OnClose)
		EventM("close", self, &OnClose)
	
		SetInt("type", TypeLogarithm)

	overwrite void OnDraw()
		Painter *c = BeginDraw("drawing_area")
		w = c.width
		h = c.height
	
		float x[], yr[], yl[]
		CreateLines(GetInt("type"), x, yr, yl)
	
		grid.Draw(c, 0, 0, w, h)
		grid.DrawCurveB(c, x, yr, true, Red)
		grid.DrawCurveB(c, x, yl, true, Blue)
	
		c.End()

	void OnType()
		Redraw("drawing_area")

	void OnClose()
		delete self

void show_spec()
	dlg = new SpectrumDialog
	
	BufferBox buf
	renderer.RenderAudioFile(audio, audio.selection, buf)
	
	complex fr[], fl[]
	
	int len2 = buf.r.num / 2 + 1
	fr.resize(len2)
	fl.resize(len2)
	//ff.clear()
	//fft_r2c(buf.r, fr)
	//fft_r2c(buf.l, fl)
	buf.get_spectrum(fr, fl, 4096)
	
	float ww = i2f(buf.r.num) / audio.sample_rate
	for int i, 0, N
		float fmin = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (N - 1) * i)
		float fmax = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (N - 1) * (i + 1))
		int j0 = fmin * ww
		int j1 = fmax * ww + 1
		j0 = clamp(j0, 0, len2)
		j1 = clamp(j1, 0, len2)
		float bmaxr = 0
		float bmaxl = 0
		for int j, j0, j1
			float amp = fr[j].x * fr[j].x + fr[j].y * fr[j].y
			if bmaxr < amp
				bmaxr = amp
			amp = fl[j].x * fl[j].x + fl[j].y * fl[j].y
			if bmaxl < amp
				bmaxl = amp
		ffr.add(bmaxr)
		ffl.add(bmaxl)

	peak_max = max(ffr.max(), ffl.max())
	float mmax = sqr(buf.num) / 4
	if mmax > 0
		ffr /= mmax
		ffl /= mmax
	peak_max /= mmax

	dlg.Redraw("drawing_area")

	dlg.Run()
	
	ffr.clear()
	ffl.clear()

void main()
	//char dummy[3]
	grid.x_axis.set(MIN_FREQ, MAX_FREQ, true, "Hz")
	
	if audio.selection.length > 0
		show_spec()
	else
		logging.Error("'Show Spectrum.kaba': Needs some selection!")
