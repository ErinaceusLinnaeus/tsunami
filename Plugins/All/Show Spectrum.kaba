// Image = hui:find
// Title[Deutsch] = Spektrum zeigen
use "../api.kaba"
use "../grid.kaba"

const int N = 512
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0
const float MIN_VOL = 0.0 - 60.0
const float MAX_VOL = 0.0

SpectrumDialog *dlg

enum
	TypeLinear
	TypeLogarithm

//float[] x, yr, yl
float[] ffr, ffl

//GridData grid

//int w, h

float todb(float v)
	return log(v) / log(10) * 10

float peak_max

class SpecGrid : Grid
	int type
	complex[] pr, pl

	void createLines()
		pr.resize(N)
		pl.resize(N)
		for i in 0:N
			float x = log2lin(i2f(i) / i2f(N), MIN_FREQ, MAX_FREQ)
			pr[i].x = x
			pl[i].x = x
			if type == TypeLinear
				pr[i].y = ffr[i]
				pl[i].y = ffl[i]
			else //if type == TypeLogarithm
				float vol_max = todb(peak_max)
				float vol_min = vol_max - 60
				pr[i].y = clamp(todb(ffr[i]), vol_min, vol_max)
				pl[i].y = clamp(todb(ffl[i]), vol_min, vol_max)
	void setType(int t)
		type = t
		if type == TypeLinear
			y_axis.set(0, peak_max, false, "")
		else //if type == TypeLogarithm
			float vol_max = todb(peak_max)
			float vol_min = vol_max - 60
			y_axis.set(vol_min, vol_max, false, "db")
		createLines()

	overwrite void onDraw(Painter *c)
		drawCurveB(c, pr, true, Red)
		drawCurveB(c, pl, true, Blue)

class SpectrumDialog : Dialog
	SpecGrid grid
	void __init__()
		super.__init__("Spectrum", 600, 460, MainWin, true)
		fromSource("
Grid table1 '' 1 2
	DrawingArea drawing_area ''
	Grid ? 2 1 !noexpandy
		ComboBox type 'Linear\\Logarithm'
		Button close 'Close' width=120 image=hui:close")
		
		grid.init(self, "drawing_area")
		grid.x_axis.set(MIN_FREQ, MAX_FREQ, true, "Hz")
	
		event("type", &onType)
		event("hui:close", &onClose)
		event("close", &onClose)
	
		setInt("type", TypeLogarithm)

	void onType()
		grid.setType(getInt("type"))
		grid.redraw()

	void onClose()
		delete self

void show_spec()
	dlg = new SpectrumDialog
	
	BufferBox buf
	SongRenderer* renderer = new SongRenderer(song, &view.sel)
	renderer.render(view.sel.range, buf)
	delete renderer
	
	complex[] fr, fl
	
	int len2 = buf.r.num / 2 + 1
	fr.resize(len2)
	fl.resize(len2)
	//ff.clear()
	//fft_r2c(buf.r, fr)
	//fft_r2c(buf.l, fl)
	buf.get_spectrum(fr, fl, 4096)
	
	float ww = i2f(buf.r.num) / song.sample_rate
	for i in 0:N
		float fmin = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (N - 1) * i)
		float fmax = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (N - 1) * (i + 1))
		int j0 = fmin * ww
		int j1 = fmax * ww + 1
		j0 = clamp(j0, 0, len2)
		j1 = clamp(j1, 0, len2)
		float bmaxr = 0
		float bmaxl = 0
		for j in j0:j1
			float amp = fr[j].x * fr[j].x + fr[j].y * fr[j].y
			if bmaxr < amp
				bmaxr = amp
			amp = fl[j].x * fl[j].x + fl[j].y * fl[j].y
			if bmaxl < amp
				bmaxl = amp
		ffr.add(bmaxr)
		ffl.add(bmaxl)

	peak_max = max(ffr.max(), ffl.max())
	float mmax = sqr(buf.length) / 4
	if mmax > 0
		ffr /= mmax
		ffl /= mmax
	peak_max /= mmax

	dlg.onType()

	dlg.run()
	
	ffr.clear()
	ffl.clear()

void main()
	if view.sel.range.length > 0
		show_spec()
	else
		logging.error("'Show Spectrum.kaba': Needs some selection!")
