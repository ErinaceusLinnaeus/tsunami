use tsunami

func update_bars(out bars: BarCollection)
	int offset = 0
	for b in bars
		b.offset = offset
		offset += b.length

class XXX extends SongPlugin
	func override apply()
		if !clipboard.has_data()
			session.e("clipboard is empty")
			return
		for t in clipboard.temp.tracks
			if t.type != SignalType.MIDI
				session.e("can only paste midi")
				return
		if len(clipboard.temp.bars) == 0
			session.e("clipboard doesn't contain any bars")
			return
		if len(song.bars) == 0
			session.e("song doesn't contain any bars")
			return
			
		TrackLayer*[] source_layers
		TrackLayer*[] target_layers
		if !clipboard.prepare_layer_map(session.view, source_layers, target_layers)
			return
			
		update_bars(clipboard.temp.bars)

		song.begin_action_group("paste")
		for ls,i in source_layers
			paste(session.view.sel.range().offset, ls, target_layers[i])
		song.end_action_group()
	
	func paste(offset: int, source: TrackLayer, out dest: TrackLayer)
		int b0 = first_bar(offset)
		for b,i in clipboard.temp.bars
			if b0+i < len(song.bars)
				paste_bar(source, dest, b.range(), song.bars[b0+i].range())
	
	func remap(rs: Range, rd: Range, x: int) -> int
		return rd.offset + float(x - rs.offset) * float(rd.length) / float(rs.length)
	
	func paste_bar(source: TrackLayer, out dest: TrackLayer, rs: Range, rd: Range)
		print("paste bar... {{rs}} -> {{rd}}")
		var midi = source.midi.get_notes(rs)
		for n in midi
			var c = n.copy(0)
			c.range = RangeTo(remap(rs, rd, n.range.offset), remap(rs, rd, n.range.end()))
			dest.add_midi_note(c)
		
		
	func first_bar(offset: int) -> int
		for b,i in song.bars
			if b.offset >= offset -100
				return i
		return -1
