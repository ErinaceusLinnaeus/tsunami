use hui
use "../api.kaba"

class VolumeChecker extends SongPlugin

	float vol_max
	bool check()
		SongRenderer *renderer = new SongRenderer(song)
		
		let p = new ProgressX("checking", self.session.win)
		
		Range range = song.range()
		AudioBuffer buf
		renderer.set_range(range)
		
		buf.resize(1024*16)
		
		vol_max = 0
		int done = 0
		while not p.is_cancelled()
			int n = renderer.read(buf)
			if n < 0
				break
			
			float _max = max(buf.r.max(), buf.l.max())
			float _min = min(buf.r.min(), buf.l.min())
			vol_max = max(vol_max, max(abs(_max), abs(_min)))
			done += n
			p.set("checking", float(done) / float(range.length))

		bool cancelled = p.is_cancelled()

		del p
		del renderer
		return !cancelled
		
	void ask()
		string q = "Song reaches a peak volume of {{vol_max * 100.0|.1}}% ({{amp2db(vol_max)|.1}}dB)\n"
		if vol_max > 0.95 and vol_max < 1.0
			hui.info_box(session.win, "Info", "{{q}} seems fine...")
			return
		if vol_max < 1
			q += "increase each track's volume by {{amp2db(1/vol_max)|.1}} dB to reach 100%?"
		else
			q += "decrease each track's volume by {{-amp2db(1/vol_max)|.1}} dB to stay below 100%?"
		string a = hui.question_box(session.win, "Question", q, true)
		if a == "hui:yes"
			session.song.begin_action_group()
			for t in session.song.tracks
				t.set_volume(t.volume / vol_max * 0.999)
			session.song.end_action_group()

	override void apply()
		if check()
			ask()
		