use "../api.kaba"

class VolumeChecker extends SongPlugin
	override void apply()
		SongRenderer *renderer = new SongRenderer(song)
		
		ProgressX *p = new ProgressX("checking", self.session.win)
		
		Range range = song.range()
		AudioBuffer buf
		renderer.prepare(range, false)
		
		buf.resize(1024*16)
		
		float vol_max = 0
		int done = 0
		while !p.is_cancelled()
			int n = renderer.read(buf)
			if n < 0
				break
			
			float _max = max(buf.r.max(), buf.l.max())
			float _min = min(buf.r.min(), buf.l.min())
			vol_max = max(vol_max, max(abs(_max), abs(_min)))
			done += n
			p.set("checking", i2f(done) / i2f(range.length))

		if !p.is_cancelled()
			string q = "Song reaches a peak volume of " + (vol_max * 100.0).str2(1) + "% (" + (amp2db(vol_max)).str2(1) + "dB)\n"
			if vol_max < 0
				q += "increase each track's volume by " + (amp2db(1/vol_max)).str2(1) + "dB to reach 100%?"
			else
				q += "decrease each track's volume by " + (amp2db(1/vol_max)).str2(1) + "dB to stay below 100%?"
			string a = HuiQuestionBox(session.win, "Question", q, true)
			if a == "hui:yes"
				session.song.begin_action_group()
				for t in session.song.tracks
					t.set_volume(t.volume / vol_max * 0.999)
				session.song.end_action_group()

		delete p
		delete renderer
		