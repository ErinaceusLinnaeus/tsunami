use "../api.kaba"
use "../tone.kaba"


class ClickRenderer extends PitchRenderer
	float volume
	float phase
	float scale
	override ClickSynthesizer *synth
	override void __init__(Synthesizer *s, int pitch)
		volume = 0
		phase = 0
		scale = 0.9995

	override void on_event(MidiEvent e)
		volume = max(e.volume, volume)

	override bool render(AudioBuffer buf)
		for i in 0:buf.length
			buf.r[i] += sin(phase) * volume
			phase += delta_phi
			if phase > 2*pi
				phase -= 2*pi
			volume *= scale
		return volume > 0.001

class ClickSynthesizer extends Synthesizer
	
	override void __init__()
		auto_generate_stereo = true

	override PitchRenderer* create_pitch_renderer(int pitch)
		return new ClickRenderer(self, pitch)
