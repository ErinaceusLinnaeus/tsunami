use "../api.kaba"
use "../tone.kaba"

const int NUM_HARMONICS = 10

class HarmonicConfig : PluginData
	float intensity[NUM_HARMONICS]
	float release
	overwrite void reset()
		intensity[0] = 1
		for int i, 1, NUM_HARMONICS
			intensity[i] = 0
		release = 0.02

class ConfigPanel : Panel
	HarmonicConfig *c
	void __init__(HarmonicConfig *_c)
		addGrid("", 0, 0, 5, NUM_HARMONICS, "main_table")
		setTarget("main_table", 0)
		addText("Fundamental", 0, 0, 0, 0, "")
		for int j, 0, NUM_HARMONICS
			if j > 0
				addText("x " + (j + 1), 0, j, 0, 0, "")
			addSpinButton("0\\0\\100\\0.1", 1, j, 0, 0, "intensity_" + j)
			eventM("intensity_" + j, self, &onChange)
		addText("Release", 2, 0, 0, 0, "")
		addSpinButton("0\\0\\100\\0.1", 3, 0, 0, 0, "release")
		addText("ms", 4, 0, 0, 0, "")
		eventM("release", self, &onChange)
		c = _c
		
	void onChange()
		for int j, 0, NUM_HARMONICS
			c.intensity[j] = getFloat("intensity_" + j) / 100
		c.release = getFloat("release") / 1000
		

class HarmonicSynthesizer : Synthesizer
	HarmonicConfig config
	ConfigPanel *panel
	
	overwrite void updateDialog()
		for int j, 0, NUM_HARMONICS
			panel.setFloat("intensity_" + j, config.intensity[j] * 100)
		panel.setFloat("release", config.release * 1000)

	overwrite Panel *createPanel()
		panel = new ConfigPanel(&config)
		updateDialog()
		return panel
		
	overwrite void renderNote(BufferBox buf, Range r, float pitch, float volume)
		float freq = pitch_to_freq(pitch)
		float f_w = 1.0 / sample_rate * freq * 2.0 * pi
		float sm_d = config.release * sample_rate
		keep_notes = sm_d * 8


		int r_end = r.offset + r.length
		
		int i0 = max(r.offset, 0)
		int i1 = min(r_end + sm_d * 8, buf.num)

		for int i, i0, i1
			float tt = (i - r.offset) * f_w
			float d = 0
			for int j, 0, NUM_HARMONICS
				d += sin(tt * (j+1)) * config.intensity[j]
			d *= volume
			if i > r_end
				float fi = (i - r_end) / sm_d
				d *= exp(-fi)
			else if i < r.offset + 1000
				d *= (i - r.offset) * 0.001
			buf.r[i] += d
			buf.l[i] += d
