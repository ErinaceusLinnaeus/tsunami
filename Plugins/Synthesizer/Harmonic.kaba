use "../api.kaba"
use "../tone.kaba"

const int NUM_HARMONICS = 10

class PitchState
	float volume
	float phase
	bool fading
	float lin_step
	int lin_range

class HarminocState : PluginData
	PitchState[128] pitch
	overwrite void reset()
		for p in pitch
			p.volume = 0
			p.phase = 0
			p.fading = false
			p.lin_range = -1

class HarmonicConfig : PluginData
	float[NUM_HARMONICS] intensity
	float release
	overwrite void reset()
		intensity[0] = 1
		for i in 1:NUM_HARMONICS
			intensity[i] = 0
		release = 0.02

class HarmonicPanel : ConfigPanel
	overwrite HarmonicSynthesizer *c
	HarmonicConfig *hc
	void __init__(HarmonicSynthesizer *_s)
		super.__init__(_s)
		addGrid("", 0, 0, 5, NUM_HARMONICS, "main_table")
		setTarget("main_table", 0)
		addText("Fundamental", 0, 0, 0, 0, "")
		for j in 0:NUM_HARMONICS
			if j > 0
				addText("x " + (j + 1), 0, j, 0, 0, "")
			addSpinButton("0\\0\\100\\0.1", 1, j, 0, 0, "intensity_" + j)
			event("intensity_" + j, &onChange)
		addText("Release", 2, 0, 0, 0, "")
		addSpinButton("0\\0\\500\\0.1", 3, 0, 0, 0, "release")
		addText("ms", 4, 0, 0, 0, "")
		event("release", &onChange)
		hc = &c.config
		
	void onChange()
		for j in 0:NUM_HARMONICS
			hc.intensity[j] = getFloat("intensity_" + j) / 100
		hc.release = getFloat("release") / 1000
		notify()
	
	overwrite void update()
		for j in 0:NUM_HARMONICS
			setFloat("intensity_" + j, hc.intensity[j] * 100)
		setFloat("release", hc.release * 1000)
		

class HarmonicSynthesizer : Synthesizer
	HarmonicConfig config
	HarminocState state

	overwrite ConfigPanel *createPanel()
		return new HarmonicPanel(self)
		
	overwrite void render(BufferBox buf)
		float sm_d = 1.0/(config.release * sample_rate)

		for i in 0:buf.num

			// current events?
			for e in events
				if e.pos == i
					int pp = e.pitch+0.4
					PitchState *s = &state.pitch[pp]
					if e.volume == 0
						s.fading = true
						s.lin_range = -1
					else
						s.fading = false
						s.lin_range = 100
						s.lin_step = (e.volume - s.volume) / s.lin_range
						enablePitch(pp, true)

			for p in active_pitch
				PitchState *s = &state.pitch[p]

				if s.fading
					s.volume *= exp(-sm_d)
					if s.volume <= 0.001
						s.volume = 0
						s.fading = false
				else if s.lin_range >= 0
					s.volume += s.lin_step
					s.lin_range --

				if s.volume == 0
					enablePitch(p, false)
					continue

				float d = 0
				for j in 0:NUM_HARMONICS
					d += sin(s.phase * (j+1)) * config.intensity[j]
				buf.r[i] += d * s.volume
				buf.l[i] += d * s.volume

				s.phase += delta_phase[p]
				if s.phase > 2*pi
					s.phase -= 2*pi
