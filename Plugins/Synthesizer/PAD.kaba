use hui
use "../api.kaba"
use "../helper/tone.kaba"
use "../helper/grid.kaba"

const int N = 4096*16


class PADConfig extends Module.Config
	float[] A
	float bw
	float bwscale
	float detune
	override void reset()
		A = [1.0, 0.4, 0.2, 0.1, 0.05]
		bw = 0.5
		bwscale = 0.7
		detune = 0.03
	
	void fill_curve(complex[] c, int n0)
		c.clear()
		float n = n0 + 1
		float nn = n + 0.03 * n * n
		float _bw = (pow(2, bw / 12.0) - 1.0) * pow(nn, bwscale)
		for x in 0.5 : 8.5 : 0.01
			float dx = x - n
			float y = exp(-dx*dx/_bw/_bw) * A[n0]
			c.add(complex(x, y))

float gauss(float f, float bw)
	float x = f / bw
	return exp(- x*x) / bw


class PADRenderer extends PitchRenderer
	int pos
	float[] prebuf
	EnvelopeADSR env
	override PADSynthesizer *synth
	override void __init__(Synthesizer s, int pitch)
		pos = 0
		
	void create()
		float freq = pitch_to_freq(pitch)
	
		complex[] c
		float[] amp
		c.resize(N / 2 + 1)
		amp.resize(N / 2 + 1)
		
		float sample_rate = synth.sample_rate
	
		for a,n0 in synth.config.A
			int n = n0 + 1
			float nn = n + synth.config.detune * n0 * n0
			float bwHz = (pow(2, synth.config.bw / 12.0) - 1.0) * freq * pow(nn, synth.config.bwscale)
			float bwi = bwHz / (2.0 * sample_rate)
			float fi = freq * n / sample_rate
			for i in 0:amp.num
				amp[i] += gauss((float(i) / N) - fi, bwi) * a
		
		for i in 0:c.num
			float phi = rand(2 * pi)
			c[i] = complex(cos(phi), sin(phi)) * amp[i]
		
		prebuf.resize(N)
		fft_c2r_inv(c, prebuf)
	
		float m = 0
		for b in prebuf
			m = max(b, m)
		prebuf /= (m * 1.2)
	
	override void on_config()
		env.set(0.02, 0.02, 0.7, 0.5, synth.sample_rate)
		create()

	override void on_start(float volume)
		env.start(volume)
	override void on_end()
		env.end()

	override bool render(AudioBuffer buf)
		for i in 0:len(buf)
			float d = prebuf[pos] * env.get()
			if env.just_killed
				return false
				
			buf.l[i] += d
			pos ++
			if pos >= N
				pos = 0
		buf.auto_generate_channels()
		return true
	

class PADSynthesizer extends Synthesizer
	PADConfig config
	
	override ConfigPanel *create_panel()
		return new PADPanel(&self)

	override PitchRenderer* create_pitch_renderer(int pitch)
		return new PADRenderer(self, pitch)

class PADGrid extends Grid
	PADConfig *pc
	ConfigPanel *p
	int selected, hover
	
	override void __init__()
		selected = -1
		hover = -1

	override void on_draw(Painter c)
		pc.A.resize(8)
	
		# curve + area below
		complex[] curve
		for a,i in pc.A
			pc.fill_curve(curve, i)
			draw_curve_b(c, curve, false, Red)
	
		# points
		for a,i in pc.A
			draw_point2(c, complex(i+1, a), i == selected, i == hover)
			
	# left button down -> select
	override void on_left_button_down()
		selected = hover
		redraw()

	override void on_mouse_move()
		# move
		if GetEvent().button_l and (selected >= 0)
			pc.A[selected] = clamp(get_mouse2().y, 0, 1)
			p.changed()
		else
		# hover?
			hover = -1
			for a,i in pc.A
				complex p = project2(complex(i+1, a))
				if (mx > p.x - 10) and (mx < p.x + 10)
					hover = i
		redraw()

class PADPanel extends ConfigPanel
	PADConfig *pc
	PADGrid grid
	override PADSynthesizer* c
	override void __init__(Module* _s)
		pc = &c.config
		from_source("
Grid ? ''
	Grid ? ''
		Text ? 'bw'
		SpinButton bw '0' range=0:10:0.01
		---|
		Text ? 'bwscale'
		SpinButton bwscale '0' range=0:10:0.01
		---|
		Text ? 'detune'
		SpinButton detune '0' range=-10:10:0.01
	---|
	DrawingArea drawing '' grabfocus expandy")
		event("bw", on_change)
		event("bwscale", on_change)
		event("detune", on_change)
		
		grid.pc = pc
		grid.p = &self
		grid.init(self, "drawing")
		grid.x_axis.set(0.5, 8.5, false, "")
		grid.y_axis.set(0, 1.05, false, "")
		
	void on_change()
		pc.bw = get_float("bw")
		pc.bwscale = get_float("bwscale")
		pc.detune = get_float("detune")
		grid.redraw()
		changed()
	
	override void update()
		set_float("bw", pc.bw)
		set_float("bwscale", pc.bwscale)
		set_float("detune", pc.detune)
		grid.redraw()
