use "../api.kaba"
use "../tone.kaba"

const int NUM_OSCILLATORS = 6

class OscillatorConfig
	float factor
	float amplitude
	float initial, peak, sustain
	float attack, decay, release

class OscillatorLink
	int source, dest

class FMConfig
	OscillatorConfig osc[NUM_OSCILLATORS]
	OscillatorLink link[]
	void reset()
		link.clear()
		for int j, 0, NUM_OSCILLATORS
			osc[j].factor = 1
			osc[j].amplitude = 0
			osc[j].attack = 0.01
			osc[j].decay = 0.4
			osc[j].release = 0.4
			osc[j].initial = 0.01
			osc[j].peak = 1
			osc[j].sustain = 0.6
		osc[0].factor = 1
		osc[0].amplitude = 0.5
		/*osc[1].factor = 1.9881
		osc[1].amplitude = 0.7
		osc[2].factor = 3.9821 // 1.72
		osc[2].amplitude = 0.3
		osc[3].factor = 1.00621
		osc[3].amplitude = 0.5
		osc[4].factor = 1.9934
		osc[4].amplitude = 0.7*/
		OscillatorLink l
		l.source = 0
		l.dest = -1
		link.add(l)
		/*l.source = 1
		l.dest = 0
		link.add(l)
		l.source = 2
		l.dest = 1
		link.add(l)
		l.source = 3
		l.dest = -1
		link.add(l)
		l.source = 4
		l.dest = 3
		link.add(l)*/
	void __init__()
		reset()
	void __assign__(FMConfig o)

class OscillatorState
	float phi, inp
	float last_before_release

class NoteState
	OscillatorState osc[NUM_OSCILLATORS]

class FMState
	NoteState note[128]
	void reset()
		for int i, 0, 128
			for int j, 0, NUM_OSCILLATORS
				note[i].osc[j].inp = 0
				note[i].osc[j].phi = 0
	void __init__()
		reset()
	void __assign__(FMState o)

Dialog *cur_dlg

class ConfigDialog : Dialog
	void __init__(FMConfig *c, Window *parent)
		super.__init__("config", 640, 260, parent, false)
		cur_dlg = self
		AddControlTable("", 0, 0, 1, 3, "root_table")
		SetTarget("root_table", 0)
		AddControlTable("", 0, 1, 8, NUM_OSCILLATORS + 1, "main_table")
		SetTarget("main_table", 0)
		AddText("Frequency", 0, 0, 0, 0, "")
		AddText("Amplitude", 1, 0, 0, 0, "")
		AddText("Initial", 2, 0, 0, 0, "")
		AddText("Peak", 3, 0, 0, 0, "")
		AddText("Sustain", 4, 0, 0, 0, "")
		AddText("Attack (ms)", 5, 0, 0, 0, "")
		AddText("Decay (ms)", 6, 0, 0, 0, "")
		AddText("Release (ms)", 7, 0, 0, 0, "")
		for int j, 0, NUM_OSCILLATORS
			AddSpinButton("0\\0\\32\\0.001", 0, j + 1, 0, 0, "factor_" + j)
			AddSpinButton("0\\0\\32\\0.001", 1, j + 1, 0, 0, "amplitude_" + j)
			AddSpinButton("0\\0\\1\\0.001", 2, j + 1, 0, 0, "initial_" + j)
			AddSpinButton("0\\0\\1\\0.001", 3, j + 1, 0, 0, "peak_" + j)
			AddSpinButton("0\\0\\1\\0.001", 4, j + 1, 0, 0, "sustain_" + j)
			AddSpinButton("0\\0\\1000\\0.1", 5, j + 1, 0, 0, "attack_" + j)
			AddSpinButton("0\\0\\1000\\0.1", 6, j + 1, 0, 0, "decay_" + j)
			AddSpinButton("0\\0\\1000\\0.1", 7, j + 1, 0, 0, "release_" + j)
		PutFavoriteBarSizable(self, "root_table", 0, 0)
		PutCommandBarSizable(self, "root_table", 0, 2)

class FMSynthesizer : Synthesizer
	FMConfig config
	FMState state
	
	overwrite void ResetConfig()
		config.reset()

	overwrite void Reset()
		reset()
		state.reset()
	
	overwrite void DataToDialog()
		for int j, 0, NUM_OSCILLATORS
			cur_dlg.SetFloat("factor_" + j, config.osc[j].factor)
			cur_dlg.SetFloat("amplitude_" + j, config.osc[j].amplitude)
			cur_dlg.SetFloat("initial_" + j, config.osc[j].initial)
			cur_dlg.SetFloat("peak_" + j, config.osc[j].peak)
			cur_dlg.SetFloat("sustain_" + j, config.osc[j].sustain)
			cur_dlg.SetFloat("attack_" + j, config.osc[j].attack * 1000)
			cur_dlg.SetFloat("decay_" + j, config.osc[j].decay * 1000)
			cur_dlg.SetFloat("release_" + j, config.osc[j].release * 1000)
	
	
	overwrite void DataFromDialog()
		for int j, 0, NUM_OSCILLATORS
			config.osc[j].factor = cur_dlg.GetFloat("factor_" + j)
			config.osc[j].amplitude = cur_dlg.GetFloat("amplitude_" + j)
			config.osc[j].initial = cur_dlg.GetFloat("initial_" + j)
			config.osc[j].peak = cur_dlg.GetFloat("peak_" + j)
			config.osc[j].sustain = cur_dlg.GetFloat("sustain_" + j)
			config.osc[j].attack = cur_dlg.GetFloat("attack_" + j) / 1000
			config.osc[j].decay = cur_dlg.GetFloat("decay_" + j) / 1000
			config.osc[j].release = cur_dlg.GetFloat("release_" + j) / 1000

	overwrite void Configure()
		ConfigDialog *d = new ConfigDialog(&config, MainWin)
		DataToDialog()
		d.Run()

	overwrite void RenderNote(BufferBox buf, Range r, float pitch, float volume)
		int n = pitch + 0.4
		float freq = pitch_to_freq(pitch)
		float w_base = freq * 2.0 * pi / sample_rate
		NoteState *ns = &state.note[n]
		
		float w[NUM_OSCILLATORS]
		float oup[NUM_OSCILLATORS]
		int attack[NUM_OSCILLATORS]
		int decay[NUM_OSCILLATORS]
		int release[NUM_OSCILLATORS]
		int t1[NUM_OSCILLATORS]
		float c_attack[NUM_OSCILLATORS]
		float c_decay[NUM_OSCILLATORS]
		float c_release[NUM_OSCILLATORS]
		
		keep_notes = 0
		
		for int j, 0, NUM_OSCILLATORS
			w[j] = w_base * config.osc[j].factor
			attack[j] = sample_rate * config.osc[j].attack
			decay[j] = sample_rate * config.osc[j].decay
			release[j] = sample_rate * config.osc[j].release
			t1[j] = attack[j] + decay[j]
			c_attack[j] = log(config.osc[j].peak / config.osc[j].initial) / attack[j]
			c_decay[j] = log(config.osc[j].sustain / config.osc[j].peak) / decay[j]
			c_release[j] = log(0.01 / config.osc[j].sustain) / release[j]
			if release[j] > keep_notes
				keep_notes = release[j]
			

		int i0 = max(r.offset, 0)
		int i1 = min(r.offset + r.length + keep_notes, buf.num)

		for int i, i0, i1
			float final = 0
			int dt = i - r.offset
			
			// oscillate
			for int j, 0, NUM_OSCILLATORS
				ns.osc[j].phi += w[j] * (1 + ns.osc[j].inp)
				oup[j] = sin(ns.osc[j].phi) * config.osc[j].amplitude
				ns.osc[j].inp = 0
				
				// hull curve
				float h
				if dt > r.length
					h = ns.osc[j].last_before_release * exp(c_release[j] * (dt - r.length))
				else
					if dt < attack[j]
						h = config.osc[j].initial * exp(c_attack[j] * dt)
					else if dt < t1[j]
						h = config.osc[j].peak * exp(c_decay[j] * (dt - attack[j]))
					else
						h = config.osc[j].sustain
					ns.osc[j].last_before_release = h
				oup[j] *= h
			
			// transport
			forall l in config.link
				if l.dest >= 0
					ns.osc[l.dest].inp += oup[l.source]
				else
					final += oup[l.source]
					
			// output
			float d = final * volume
			buf.r[i] += d
			buf.l[i] += d
