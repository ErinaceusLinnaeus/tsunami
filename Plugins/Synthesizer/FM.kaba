use hui
use tsunami
use helper.tone
use helper.draw

const NUM_OSCILLATORS = 6

const OSCI_RADIUS = 15.0
const LINK_LENGTH = OSCI_RADIUS * 5.0



enum OsciType
	SINE as "sine"
	SQUARE as "square"
	SAW as "saw"

class OscillatorConfig
	var factor: float
	var amplitude: float
	var initial, peak, sustain: float
	var attack, decay, release: float
	var type: OsciType
	var target: bool[NUM_OSCILLATORS]
	var target_out: bool
	func __init__()
		reset()
	func mut reset()
		factor = 1
		amplitude = 0.1
		initial = 0.01
		peak = 1
		sustain = 0.6
		attack = 0.01
		decay = 0.4
		release = 0.4
		type = OsciType.SINE
		for t in target
			t = false
		target_out = false
	func active() -> bool
		if target_out
			return true
		for t in target
			if t
				return true
		return false

class OscillatorLink
	var source, dest: int

class FMConfig extends Module.Config
	var osc: OscillatorConfig[NUM_OSCILLATORS]
	#OscillatorLink[] link
	func override reset()
		#link.clear()
		for o in osc
			o.reset()
		osc[0].factor = 1
		osc[0].amplitude = 0.5
		osc[0].target_out = true
	func mut clear_links()
		for o in osc
			for t in o.target
				t = false
			o.target_out = false

class FMPanel extends ConfigPanel
	var fc: FMConfig*
	var chosen: int
	var menu_top: Menu*
	var menu_osci: Menu*
	
	var width, height: float
	var p_osci: vec2[NUM_OSCILLATORS]
	var p_out: vec2
	
	var moving, hover: int
	
	func mut add_top(label: string, id: string)
		event("top-{{id}}", on_topology)
		menu_top.add(label, "top-{{id}}")
	
	var override c: FMSynthesizer*
	func override __init__(_s: Module)
		fc = &c.config
		
		from_source("
Grid ? ''
	DrawingArea area '' expandx expandy
	---|
	Grid ? ''
		Label ? 'Freq Multiplier' right disabled
		SpinButton factor '' range=0:32:0.001 expandx
		---|
		Label ? 'Amplitude' right disabled
		SpinButton amplitude '' range=0:100:0.001")
		
		event_x("area", "hui:draw", on_draw)
		event_x("area", "hui:left-button-down", on_left_button_down)
		event_x("area", "hui:left-button-up", on_left_button_up)
		event_x("area", "hui:right-button-down", on_right_button_down)
		event_x("area", "hui:mouse-move", on_mouse_move)
		event("factor", on_spin_button)
		event("amplitude", on_spin_button)
		chosen = 0
		hover = -1
		
		menu_top = new Menu(self)
		menu_top.add("Topology", "label-top")
		menu_top.enable("label-top", false)
		add_top("o", "x")
		add_top("o-o", "x0")
		add_top("o   o", "xx")
		add_top("o-o-o", "x01")
		add_top("o-(o   o)", "x00")
		add_top("o-o   o", "x0x")
		add_top("o-o   o-o", "x0x2")
		add_top("o-o-o   o", "x01x")
		add_top("o-(o   o)   o", "x00x")
		add_top("o-o-o   o-o-o", "x01x34")
		add_top("o-o-o   o-o   o", "x01x3x")
		add_top("o-(o   o)   o-(o   o)", "x00x33")

		menu_osci = new Menu(self)
		menu_osci.add("Oscillator", "label")
		menu_osci.enable("label", false)
		menu_osci.add("sine", "type-sine")
		menu_osci.add("square", "type-square")
		menu_osci.add("saw", "type-saw")
		event("type-sine", on_type_sine)
		event("type-square", on_type_square)
		event("type-saw", on_type_saw)
	
	func mut update_osci_pos()
		# initial
		p_out = vec2(40, 60)
		for p,i in p_osci
			p = vec2(100+15*i, i*20)#vec2((i % 3) * 100 + 100, i * 20 + 10 + pow((i%3),2)*3)
			let has_links = fc.osc[i].target_out
			for t in fc.osc[i].target
				has_links = has_links or t
			if !has_links
				p += vec2(1000,1000)
		for i in 0:2000
			iterate_osci_pos()
	func _f_link(a: vec2, b: vec2) -> vec2
		let d = len(b - a)
		let dir = (b - a) * (1/d)
		return dir * (d - LINK_LENGTH) * 0.1 + vec2(0, (b.y-a.y) * 0.05)
	func _f_free(a: vec2, b: vec2) -> vec2
		let d = len(b - a)
		let dir = (b - a) * (1/d)
		return dir * min(d - OSCI_RADIUS*3.5, 0) * 0.3
	func mut iterate_osci_pos()
		vec2[NUM_OSCILLATORS] f
		for o,i in fc.osc
			f[i] = vec2(0,0)
			for o2,j in fc.osc
				if i == j
					continue
				if linked(i,j)
					f[i] += _f_link(p_osci[i], p_osci[j])
				else
					f[i] += _f_free(p_osci[i], p_osci[j])
			if o.target_out
				f[i] += _f_link(p_osci[i], p_out)
		for o,i in fc.osc
			p_osci[i] += f[i]
	func linked(i: int, j: int) -> bool
		return fc.osc[i].target[j] or fc.osc[j].target[i]
	
	func osci_pos(index: int) -> vec2
		if index < 0
			return p_out
		return p_osci[index]

	func draw_type(p: Painter, z: vec2, type: OsciType, _dx: float, amp: float)
		let dx = _dx / pi
		let v0 = 0.0
		let t0 = -pi
		for t in -pi:pi+0.1:0.1
			let v = sin(t)
			if type == OsciType.SQUARE
				if v > 0
					v = 1
				else
					v = -1
			else if type == OsciType.SAW
				v = -1 + 2*loop(t, 0, pi) / pi
			if t > t0
				p.draw_line([z.x + t0*dx, z.y - v0*amp], [z.x + t*dx, z.y - v*amp])
			t0 = t
			v0 = v
	
	func draw_osci(p: Painter, index: int)
		var z = osci_pos(index)
		var bg = colors.blob_bg
		var col = colors.text_soft1
		if index == chosen
			bg = colors.blob_bg_selected
			col = colors.text
		if index == hover
			bg = colors.hoverify(bg)
		p.set_color(bg)
		p.draw_circle([z.x, z.y], 15)
	
		var o = fc.osc[index]
		p.set_color(col)
		draw_type(p, z - vec2(0,2), o.type, 9, 5.5)
		let f = int(o.factor + 0.5)
		p.set_font_size(7)
		#p.set_color(colors.text_soft1)
		p.draw_str([z.x-4, z.y+6], "x{{f}}")
	
	func draw_link(p: Painter, a: int, b: int)
		p.set_color(colors.text_soft1)
		var z1 = osci_pos(a)
		var z2 = osci_pos(b)
		draw_spline(p, [z2.x + 18, z2.y], [50,0], [z1.x - 18, z1.y], [50,0])
	
	func t2x(t: float) -> float
		return width*0.7 * sqrt(t)
	func v2y(v: float) -> float
		return height - 100*v
	func x2t(x: float) -> float
		return sqr(max(x,0) / width / 0.7)
	func y2v(y: float) -> float
		return max((height - y), 0) / 100
	func draw_curve_exp(p: Painter, t0: float, v0: float, t1: float, v1: float, col: color)
		let e = log(v1/v0)
		let cc: vec2[]
		for a in 0:1.01:0.05
			float t = t0+a*(t1-t0)
			float v = v0 * exp(a * e)
			cc.add([t2x(t), v2y(v)])
		p.set_color(col)
		p.set_line_dash([5.0,2.0])
		p.draw_lines(cc)
		p.set_line_dash([2.0,0.0])
		cc.add([t2x(t1), v2y(0)])
		cc.add([t2x(t0), v2y(0)])
		p.set_color(col.with_alpha(0.2))
		p.draw_polygon(cc)
	func draw_curve_lin(p: Painter, t0: float, v0: float, t1: float, v1: float, col: color)
		let cc: vec2[]
		cc.add([t2x(t0), v2y(v0)])
		cc.add([t2x(t1), v2y(v1)])
		p.set_color(col)
		p.set_line_dash([5.0,2.0])
		p.draw_lines(cc)
		p.set_line_dash([2.0,0.0])
		cc.add([t2x(t1), v2y(0)])
		cc.add([t2x(t0), v2y(0)])
		p.set_color(col.with_alpha(0.2))
		p.draw_polygon(cc)
	
	func draw_grabber(p: Painter, t0: float, v0: float, t1: float, v1: float, col: color, hover: bool)
		var col2 = col
		p.set_line_width(2.0)
		if hover
			col2 = colors.hoverify(col2)
			p.set_line_width(3.0)
		p.set_color(col2)
		p.draw_line([t2x(t0), v2y(v0)], [t2x(t1), v2y(v1)])
		p.set_line_width(1)
	
	func draw_envelope(p: Painter, o: OscillatorConfig)
		let t0 = 0.0
		let t1 = t0 + o.attack
		let t2 = t1 + o.decay
		let t3 = t2 + o.release
		draw_curve_lin(p, t0, o.initial, t1, o.peak, cool_color(0))
		draw_curve_lin(p, t1, o.peak, t2, o.sustain, cool_color(1))
		draw_curve_exp(p, t2, o.sustain, t3, 0.01, cool_color(3))
		
		
		draw_grabber(p, t1, 0, t1, 1, cool_color(0), hover == -10)
		draw_grabber(p, t2, 0, t2, 1, cool_color(1), hover == -11)
		draw_grabber(p, t3, 0, t3, 1, cool_color(3), hover == -12)
		draw_grabber(p, t1, o.sustain, t3, o.sustain, cool_color(2), hover == -13)
		p.set_color(colors.text_soft1)
		p.set_font_size(9)
		if hover == -10
			p.draw_str([width - 100, height - 20], "attack {{o.attack * 1000|.1}} ms")
		else if hover == -11
			p.draw_str([width - 100, height - 20], "decay {{o.decay * 1000|.0}} ms")
		else if hover == -12
			p.draw_str([width - 100, height - 20], "release {{o.release * 1000|.0}} ms")
		else if hover == -13
			p.draw_str([width - 100, height - 20], "sustain {{o.sustain * 100|.0}} %")
		
	func mut on_draw(p: Painter)
		width = p.width
		height = p.height
		p.set_color(colors.background)
		p.draw_rect([0,width, 0,height])
		
		p.set_roundness(5)
		p.set_color(colors.blob_bg_hidden)
		p.draw_rect([0, p_out.x+15, p_out.y-20, p_out.y+20])
		p.set_roundness(0)
		p.set_color(colors.text_soft1)
		p.draw_str([p_out.x-32, p_out.y-10], "out")
		
		for j in 0:NUM_OSCILLATORS
			draw_osci(p, j)
		for o,j in fc.osc
			for k in 0:NUM_OSCILLATORS
				if o.target[k]
					draw_link(p, j, k)
			if o.target_out
				draw_link(p, j, -1)
		
		draw_envelope(p, fc.osc[chosen])
	
	func get_hover() -> int
		let e = hui.get_event()
		let mx = e.mouse.x
		let my = e.mouse.y
		for i in 0:NUM_OSCILLATORS
			var z = osci_pos(i)
			if abs(mx - z.x) + abs(my - z.y) < 20
				return i
		if my > v2y(1.1) and chosen >= 0
			var o = &fc.osc[chosen]
			if abs(mx - t2x(o.attack)) < 10
				return -10
			if abs(mx - t2x(o.attack + o.decay)) < 10
				return -11
			if abs(mx - t2x(o.attack + o.decay + o.release)) < 10
				return -12
			if mx > t2x(o.attack) and mx < t2x(o.attack + o.decay + o.release) and abs(my - v2y(o.sustain)) < 10
				return -13
		return -1
	
	func mut on_left_button_down()
		hover = get_hover()
		moving = -1
		if hover >= 0
			set_chosen(hover)
		else if hover < -5
			moving = hover
	func mut on_left_button_up()
		moving = -1
	func mut on_mouse_move()
		let e = hui.get_event()
		let mx = e.mouse.x
		let my = e.mouse.y
		if moving < -5 and chosen >= 0
			var o = &fc.osc[chosen]
			if moving == -10
				o.attack = clamp(x2t(mx), 0.0002, 2)
			if moving == -11
				o.decay = clamp(x2t(mx) - o.attack, 0.001, 2)
			if moving == -12
				o.release = clamp(x2t(mx) - o.attack - o.decay, 0.001, 2)
			if moving == -13
				o.sustain = clamp(y2v(my), 0.001, 1.0)
			update()
			changed()
		else
			hover = get_hover()
		redraw("area")
				
		
	func mut on_right_button_down()
		hover = get_hover()
		if hover >= 0
			set_chosen(hover)
			menu_osci.popup(self)
		else
			menu_top.popup(self)
	
	func mut set_chosen(i: int)
		chosen = i
		redraw("area")
		update()
	
	func mut on_topology()
		let code = hui.get_event().id[4:]
		fc.clear_links()
		for i in 0:len(code)
			if code[i] == 'x'
				fc.osc[i].target_out = true
			else
				let t = int(code[i:i+1])
				fc.osc[i].target[t] = true
		update_osci_pos()
		redraw("area")
		changed()
	
	func mut on_type_sine()
		fc.osc[chosen].type = OsciType.SINE
		redraw("area")
		changed()
	
	func mut on_type_square()
		fc.osc[chosen].type = OsciType.SQUARE
		redraw("area")
		changed()
		
	func mut on_type_saw()
		fc.osc[chosen].type = OsciType.SAW
		redraw("area")
		changed()

	func mut on_spin_button()
		if chosen < 0
			return
		var o = &fc.osc[chosen]
		o.factor = get_float("factor")
		o.amplitude = get_float("amplitude")
		changed()
	
	func override update()
		if chosen >= 0
			var o = &fc.osc[chosen]
			set_float("factor", o.factor)
			set_float("amplitude", o.amplitude)
		update_osci_pos()
		redraw("area")


class OscillatorState
	var input = 0.
	var output = 0.
	var volume = 0.
	var phi = 0.
	var env: EnvelopeADSR
	var conf: OscillatorConfig
	func __init__()
		env.reset()

class FMRenderer extends PitchRenderer
	var osc: OscillatorState[]
	var num_osc_active = 0
	var override synth: FMSynthesizer*

	func override on_end()
		for o,oi in osc
			o.env.end()

	func override on_start(_volume: float)
		num_osc_active = len(osc)
		for o in osc
			# TODO: volume should only effect "output" oscs
			o.env.start(_volume * o.conf.amplitude)

	func override on_config()
		let n = 0
		for c in synth.config.osc
			if !c.active()
				continue
			if len(osc) <= n
				OscillatorState o
				osc.add(o)
			osc[n].conf = c
			osc[n].env.set(c.attack, c.decay, c.sustain, c.release, synth.sample_rate)
			osc[n].env.set2(c.initial, c.peak)
			n ++
		osc.resize(n)

	func override render(out buf: AudioBuffer) -> bool
		for i in 0:len(buf)

			for o in osc
				o.volume = o.env.get()
				if o.env.just_killed
					num_osc_active --
					if num_osc_active == 0
						return false
					
				o.phi += delta_phi * (1 + o.input) * o.conf.factor
				if o.conf.type == OsciType.SINE
					o.output = sin(o.phi) * o.volume
				else if o.conf.type == OsciType.SQUARE
					if sin(o.phi) > 0
						o.output = o.volume
					else
						o.output = -o.volume
				else if o.conf.type == OsciType.SAW
					o.output = (loop(o.phi, 0, 2*pi) / pi - 1) * o.volume
				o.input = 0
				if o.phi > 16*pi
					o.phi = loop(o.phi, 0, 2*pi)
			
			# transport
			for o in osc
				if o.conf.target_out
					buf.c[0][i] += o.output
				for t,j in o.conf.target
					if t
						osc[j].input += o.output
		buf.auto_generate_channels()
		return true

class FMSynthesizer extends Synthesizer
	var config: FMConfig

	func override create_panel() -> ConfigPanel*
		return new FMPanel(self)

	func override create_pitch_renderer(pitch: int) -> PitchRenderer*
		return new FMRenderer(self, pitch)

			
	
