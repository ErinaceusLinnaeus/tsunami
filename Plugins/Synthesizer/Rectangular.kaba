use "../api.kaba"
use "../tone.kaba"

class RectangularState : PluginData
	float[128] volume
	float[128] phase
	overwrite void reset()
		for int p, 0, 128
			volume[p] = 0
			phase[p] = 0


class RectangularSynthesizer : Synthesizer
	RectangularState state

	overwrite void render(BufferBox buf)
		for int i, 0, buf.num
			for e in events
				if e.pos == i
					int pp = e.pitch
					state.volume[pp] = e.volume
					enablePitch(pp, e.volume > 0)
			
			for p in active_pitch
				float d = sin(state.phase[p])
				if d > 0
					buf.r[i] += state.volume[p]
					buf.l[i] += state.volume[p]
				else
					buf.r[i] -= state.volume[p]
					buf.l[i] -= state.volume[p]
				state.phase[p] += delta_phase[p]
				if state.phase[p] > 2*pi
					state.phase[p] -= 2*pi

