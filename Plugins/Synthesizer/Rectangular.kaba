use "../api.kaba"
use "../tone.kaba"

class PitchState
	float volume
	float phase


class RectangularSynthesizer extends Synthesizer
	
	PitchState[MAX_PITCH] pitches
	override void reset_state()
		for p in pitches
			p.volume = 0
			p.phase = 0

	override void render(AudioBuffer buf)
		for i in 0:buf.length
			for e in events
				if e.pos == i
					int pp = e.pitch
					pitches[pp].volume = e.volume
					enable_pitch(pp, e.volume > 0)
			
			for ip in active_pitch
				PitchState *p = &pitches[ip]
				float d = sin(p.phase)
				if d > 0
					buf.r[i] += p.volume
					buf.l[i] += p.volume
				else
					buf.r[i] -= p.volume
					buf.l[i] -= p.volume
				p.phase += delta_phi[ip]
				if p.phase > 2*pi
					p.phase -= 2*pi

