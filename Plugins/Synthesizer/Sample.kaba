use "../api.kaba"
use "../tone.kaba"


class SampleConfig extends PluginData
	SampleRef*[] samples
	override void __delete__()
		reset()
	override void reset()
		for s in samples
			if s
				delete s
		samples.clear()

class SampleSynthesizer extends Synthesizer
	SampleConfig config
	
	MidiEvent[] _events
	
	override void render(AudioBuffer buf)
		for e in events
			if e.volume > 0
				_events.add(e)
		
		for e in _events
			int n = e.pitch + 0.4
			if n < 0 or n >= config.samples.num
				e.pitch = -1
				continue
			SampleRef *s = config.samples[n]
			if !s
				e.pitch = -1
				continue
			buf.add(*s.buf, e.pos, e.volume, 0)
			e.pos -= buf.length
			if e.pos < -s.buf.length
				e.pitch = -1
			
		for e,i in _events
			if e.pitch < 0
				_events.remove(i)
				i --
			
	
	override ConfigPanel *create_panel()
		return new SamplePanel(self)

class SamplePanel extends ConfigPanel
	SampleConfig *sc
	override SampleSynthesizer *c
	override void __init__(SampleSynthesizer *_s)
		super.__init__(_s)
		sc = &c.config
		add_list_view("!width=300\\Note\\Sample", 0, 0, "list")
		event("list", &on_select)

	override void update()
		reset("list")
		for i in 0:MAX_PITCH
			string name = ""
			if i < sc.samples.num
				if sc.samples[i]
					name = sc.samples[i].origin.name
			set_string("list", pitch_name(i) + "\\" + name)
		
	void on_select()
		int n = get_int("")
		if n < 0
			return
		if n >= sc.samples.num
			sc.samples.resize(n + 1)
		Sample *old = nil
		if sc.samples[n]
			old = sc.samples[n].origin
		Sample *s = SelectSample(c.session, self, old)
		if s != old
			if sc.samples[n]
				delete sc.samples[n]
				sc.samples[n] = nil
			if s
				sc.samples[n] = s.create_ref()
			changed()
