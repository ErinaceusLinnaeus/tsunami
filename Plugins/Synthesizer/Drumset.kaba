use "../api.kaba"
use "../tone.kaba"

class Drumset
	string name
	Song *a
	
	void load()
		if a
			return
		a = new Song
		string temp = storage.current_directory
		storage.load(a, AppDirectoryStatic + "Data/Drumsets/" + name + ".nami")
		storage.current_directory = temp

class DrumList
	Drumset[] sets
	
	int find(string name)
		if name == "" and sets.num > 0
			return 0
		for s,i in sets
			if s.name == name
				return i
		return -1
		
	string[] getNames()
		string[] list
		for s in sets
			list.add(s.name)
		return list

	//string[] find_drumsets()
	void update()
		DirEntry[] list = DirSearch(AppDirectoryStatic + "Data/Drumsets", "*.nami", false)
		for e in list
			string name = e.name.substr(0, -6)
			if find(name) >= 0
				continue
			Drumset s
			s.name = name
			s.a = nil
			sets.add(s)
	
	Song *get(string name)
		update()
		int n = find(name)
		if n < 0
			if sets.num > 0
				n = 0
				logging.warn("Drumset not found: '" + name + "', using '" + sets[0].name + "' instead")
			else
				logging.error("Drumset not found: '" + name + "' (none found)")
				return nil
		Drumset *s = &sets[n]
		s.load()
		return s.a

DrumList drumlist

class DrumsetConfig extends PluginData
	string name
	override void reset()
		name = ""

class DrumsetState extends PluginData
	Song *a
	Synthesizer *s
	

class DrumsetSynthesizer extends Synthesizer
	DrumsetConfig config
	DrumsetState state
	override void __init__()
		state.a = nil
	override void onConfig()
		state.a = drumlist.get(config.name)
		state.s = nil
		if state.a
			state.s = state.a.tracks[0].synth
		if state.s
			state.s.setSampleRate(sample_rate)
			state.s.onConfig()
	override void render(AudioBuffer buf)
		Synthesizer *s = state.s
		if !s
			return
		s.events = events
		s.render(buf)

	
	override ConfigPanel *createPanel()
		return new DrumsetPanel(self)

class DrumsetPanel extends ConfigPanel
	DrumsetConfig *sc
	override DrumsetSynthesizer *c
	override void __init__(DrumsetSynthesizer *_s)
		super.__init__(_s)
		sc = &c.config
		addGrid("", 0, 0, 2, 1, "grid")
		setTarget("grid", 0)
		addLabel("Set", 0 ,0, 0, 0, "")
		addComboBox("", 1, 0, 0, 0, "sets")
		event("sets", &onSelect)

	override void update()
		reset("sets")
		string[] sets = drumlist.getNames()
		setString("sets", "  - Default -")
		setInt("sets", 0)
		for s,i in sets
			setString("sets", s)
			if s == sc.name
				setInt("sets", i+1)
		
	void onSelect()
		int n = getInt("")
		sc.name = ""
		if n > 0
			sc.name = drumlist.sets[n-1].name
		notify()
