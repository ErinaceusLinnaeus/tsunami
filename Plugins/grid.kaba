use "tone.kaba"
use "api.kaba"


float log2lin(float f, float f_min, float f_max)
	return exp(f * log(f_max / f_min)) * f_min

float lin2log(float f, float f_min, float f_max)
	return log(f / f_min) / log(f_max / f_min)

class GridTick
	float value, weight
	string label

class GridPartition
	GridTick[] tick

class GridAxis
	float offset, size
	rect rect
	float min_label_dist
	float val_min, val_max
	float val_min_min, val_max_max
	bool as_log
	bool freq_as_pitch
	string unit
	
	void __init__()
		freq_as_pitch = false
		set(0, 1, false, "")
	
	void set(float _val_min, float _val_max, bool _log, string _unit)
		val_min = _val_min
		val_max = _val_max
		val_min_min = _val_min
		val_max_max = _val_max
		as_log = _log
		unit = _unit
	
	float rel2val(float __v)
		if as_log
			return log2lin(__v, val_min_min, val_max_max)
		return __v * (val_max_max - val_min_min) + val_min_min
		
	float project(float __v)
		if as_log
			return lin2log(__v, val_min, val_max) * size + offset
		return ((__v - val_min) / (val_max - val_min)) * size + offset

	float unproject(float __v)
		if as_log
			return log2lin((__v - offset) / size, val_min, val_max)
		return (__v - offset) / size * (val_max - val_min) + val_min
	
	void zoom(float p, float dz)
		float m = unproject(p)
		if as_log
			val_min = clamp(exp(log(m) + (log(val_min) - log(m)) * pow(0.9, dz)), val_min_min, val_max_max)
			val_max = clamp(exp(log(m) + (log(val_max) - log(m)) * pow(0.9, dz)), val_min_min, val_max_max)
		else
			val_min = clamp(m + (val_min - m) * pow(0.9, dz), val_min_min, val_max_max)
			val_max = clamp(m + (val_max - m) * pow(0.9, dz), val_min_min, val_max_max)
		

	GridPartition get_lin_partition()
		float d = (val_max - val_min) / size * 150
		GridPartition r
		float step = 1
		float e = log(abs(abs(d))) / log(10)
		float e_floor = i2f(f2i(e))
		if e < 0
			e_floor -= 1
		step = pow(10, e_floor)
		float factor = d / step
		int decimals = f2i(max(-e_floor, 0))
		float min = i2f(f2i(val_min / step) * step)
		float max = val_max
		
		float vv = min
		while vv <= max and step > 0
			GridTick tt
			tt.value = vv
			tt.weight = 1
			tt.label = vv.str2(decimals)
			tt.label += " "
			tt.label += unit
			r.tick.add(tt)
			if factor < 3
				tt.value = vv + step / 2
				tt.weight = 0.7
				tt.label = ""
				r.tick.add(tt)
			vv += step
		
		return r
		
	GridPartition get_log_partition()
		float pixel_per_decade = abs(size / log(val_max / val_min)) * log(10)
		float size_param = pixel_per_decade / min_label_dist
		GridPartition r
		int e_min = log(abs(abs(val_min))) / log(10)
		int e_max = log(abs(abs(val_max))) / log(10) + 1
		for e in e_min:e_max
			float vv0 = pow(10, e)
			for i in 1:10
				float vv = vv0 * i
				if (vv >= val_min) and (vv <= val_max)
					GridTick tt
					tt.value = vv
					tt.weight = exp(-(i+1)*0.14)
					tt.label = ""
					bool show = false
					if (i == 1)
						show = true
					if (i == 3) and (size_param > 2)
						show = true
					if ((i == 2) or (i == 5)) and (size_param > 4)
						show = true
					if show
						tt.label = vv.str2(0) + " " + unit
					r.tick.add(tt)
				vv = vv0 * 1.5
				if (i == 1) and (vv >= val_min) and (vv <= val_max)
					GridTick tt
					tt.value = vv
					tt.weight = 0.5
					tt.label = ""
					r.tick.add(tt)
		return r
		
	GridPartition get_pitch_partition()
		GridPartition r
		float pitch_min = freq_to_pitch(val_min)
		float pitch_max = freq_to_pitch(val_max)
		int oct_min = pitch_get_octave(pitch_min)
		int oct_max = pitch_get_octave(pitch_max) + 1
		for o in oct_min:oct_max
			int pitch = pitch_from_octave_and_rel(0, o)
			GridTick tt
			tt.value = pitch_to_freq(pitch)
			if (tt.value >= val_min) and (tt.value <= val_max)
				tt.weight = 1
				tt.label = pitch_name(pitch)
				r.tick.add(tt)
			pitch = pitch_from_octave_and_rel(6, o)
			tt.value = pitch_to_freq(pitch)
			if (tt.value >= val_min) and (tt.value <= val_max)
				tt.weight = 0.7
				tt.label = ""
				r.tick.add(tt)
		return r
		
	GridPartition get_partition()
		if as_log
			if freq_as_pitch
				return get_pitch_partition()
			return get_log_partition()
		return get_lin_partition()

class GridData
	bool allow_bg
	float x0, y0
	rect plot_rect
	float x, y, w, h
	
	GridAxis x_axis, y_axis
	
	color col_bg
	color col_grid
	color col_line
	color col_text
	
	void __init__()
		col_grid = view.colors.grid
		col_text = view.colors.text
		col_line = view.colors.text_soft1
		col_bg = view.colors.background
		allow_bg = true
		x_axis.min_label_dist = 60
		y_axis.min_label_dist = 30

	complex project2(complex z)
		return complex(x_axis.project(z.x), y_axis.project(z.y))

	vector project3(vector z)
		return vector(x_axis.project(z.x), y_axis.project(z.y), z.z)

	complex unproject2(complex z)
		return complex(x_axis.unproject(z.x), y_axis.unproject(z.y))

	vector unproject3(vector z)
		return vector(x_axis.unproject(z.x), y_axis.unproject(z.y), z.z)
	
	
	void drawAxisY(Painter *c, GridPartition part)
		for t in part.tick
			c.setColor(ColorInterpolate(col_bg, col_grid, t.weight))
			float yy = y_axis.project(t.value)
			if yy < y_axis.rect.y1 or yy > y_axis.rect.y2
				continue
			c.drawLine(x, yy, x + w, yy)
			if t.label.num > 0
				c.setColor(col_line)
				c.drawLine(x, yy, x + 6, yy)
				c.drawStr(x0 + 2, yy - 6, t.label)
	
	void drawAxisX(Painter *c, GridPartition part)
		for t in part.tick
			c.setColor(ColorInterpolate(col_bg, col_grid, t.weight))
			float xx = x_axis.project(t.value)
			if xx < x_axis.rect.x1 or xx > x_axis.rect.x2
				continue
			c.drawLine(xx, y, xx, y + h)
			if t.label.num > 0
				c.setColor(col_line)
				c.drawLine(xx, y + h - 6, xx, y + h)
				c.drawStr(xx - 8, y + h + 5, t.label)
	
	rect getPlotRegion(float _x0, float _y0, float _w, float _h)
		return rect(_x0 + 40, _x0 + _w - 3, _y0 + 3, _y0 + _h -20)

	void drawBackground(Painter *c, float _x0, float _y0, float _w, float _h)
	
		x0 = _x0
		y0 = _y0 + 3
	
		plot_rect = getPlotRegion(_x0, _y0, _w, _h)
		_y0 += 3
		x = plot_rect.x1
		y = plot_rect.y1
		w = plot_rect.width()
		h = plot_rect.height()
		
		x_axis.offset = x
		x_axis.size = w
		x_axis.rect = rect(x, x+w, y+h, _h)
		y_axis.offset = y + h
		y_axis.size = -h
		y_axis.rect = rect(_x0, x, _y0, y+h)
		
		// background
		if allow_bg
			c.setColor(col_bg)
			c.drawRect(x, y, w, h)
	
		c.setLineWidth(0.9)
		c.setFontSize(7)
		GridPartition part = x_axis.get_partition()
		drawAxisX(c, part)
		part = y_axis.get_partition()
		drawAxisY(c, part)
	
	void drawCurve(Painter *c, FloatInterpolator fi, bool fill_below)
		float[] tt, yy
		complex[] pp
		tt = rangef(0, 1, 0.003)
		yy = fi.getList(tt)
		for t,i in tt
			complex zz = project2(complex(x_axis.rel2val(t), yy[i]))
			pp.add(zz)
		if fill_below
			pp.add(complex(x + w, y + h))
			pp.add(complex(x, y + h))
			c.setColor(color(0.3, 0.3, 0.3, 1))
			c.drawPolygon(pp)
			pp.resize(pp.num - 2)
		c.setLineWidth(2)
		c.setColor(col_line)
		c.drawLines(pp)
	
	void drawCurveB(Painter *c, complex[] _p, bool fill_below, color col)
		complex[] pp
		for p in _p
			pp.add(project2(p))
		if fill_below
			pp.add(complex(x + w, y + h))
			pp.add(complex(x, y + h))
			color col2 = col
			col2.a = 0.3
			c.setColor(col2)
			c.drawPolygon(pp)
			pp.resize(pp.num - 2)
		c.setLineWidth(0.9)
		c.setColor(col)
		c.drawLines(pp)
	
	void drawPoint2(Painter *c, complex p, bool selected, bool hover)
		complex pp = project2(p)
		c.setColor(col_text)
		float radius = 3
		if hover
			c.setColor(Red)
			radius = 4
		else if selected
			c.setColor(color(1, 0.7, 0, 0))
		c.drawCircle(pp.x, pp.y, radius)
	
	void drawPoint3(Painter *c, vector p, bool selected, bool hover)
		vector pp = project3(p)
		c.setColor(col_text)
		float radius = 3
		if hover
			c.setColor(Red)
			c.drawStr(pp.x, pp.y - 20, pp.z.str2(1))
			radius = 4
		else if selected
			c.setColor(color(1, 0.7, 0, 0))
		c.drawCircle(pp.x, pp.y, radius)
	
	void drawLine2(Painter *c, complex p0, complex p1, bool selected, bool hover)
		complex pp0 = project2(p0)
		complex pp1 = project2(p1)
		c.setColor(col_line)
		float radius = 3
		if hover
			c.setColor(Red)
			radius = 4
		else if selected
			c.setColor(color(1, 0.7, 0, 0))
		c.drawLine(pp0.x, pp0.y, pp1.x, pp1.y)
	
	complex getMouse2()
		float mx = HuiGetEvent().mouse_x
		float my = HuiGetEvent().mouse_y
		return unproject2(complex(mx, my))
	
	vector getMouse3(float z)
		float mx = HuiGetEvent().mouse_x
		float my = HuiGetEvent().mouse_y
		return unproject3(vector(mx, my, z))
		
	bool mouseOverPoint2(complex q)
		float mx = HuiGetEvent().mouse_x
		float my = HuiGetEvent().mouse_y
		complex p = project2(q)
		return (mx > p.x - 5) and (mx < p.x + 5) and (my > p.y - 5) and (my < p.y + 5)
		
	bool mouseOverPoint3(vector q)
		return mouseOverPoint2(complex(q.x, q.y))
		
	virtual void dummyVirtual()
		nil
		

class Grid : GridData
	Panel *panel
	string id
	float mx, my
	
	void onDrawAll(Painter *c)
		float w = c.width
		float h = c.height
	
		drawBackground(c, 0, 0, w, h)
		c.clip(plot_rect)
		onDraw(c)
	
	void redraw()
		panel.redraw(id)
	
	void onLeftButtonDownAll()
		mx = HuiGetEvent().mouse_x
		my = HuiGetEvent().mouse_y
		if plot_rect.inside(mx, my)
			onLeftButtonDown()
		if x_axis.rect.inside(mx, my)
			x_axis.freq_as_pitch = !x_axis.freq_as_pitch
			redraw()
		if y_axis.rect.inside(mx, my)
			y_axis.freq_as_pitch = !y_axis.freq_as_pitch
			redraw()
	
	void onLeftButtonUpAll()
		mx = HuiGetEvent().mouse_x
		my = HuiGetEvent().mouse_y
		if plot_rect.inside(mx, my)
			onLeftButtonUp()
	
	void onRightButtonDownAll()
		mx = HuiGetEvent().mouse_x
		my = HuiGetEvent().mouse_y
		if plot_rect.inside(mx, my)
			onRightButtonDown()
	
	void onRightButtonUpAll()
		mx = HuiGetEvent().mouse_x
		my = HuiGetEvent().mouse_y
		if plot_rect.inside(mx, my)
			onRightButtonUp()
	
	void onMouseMoveAll()
		mx = HuiGetEvent().mouse_x
		my = HuiGetEvent().mouse_y
		//if plot_rect.inside(mx, my)
		onMouseMove()
	
	void onMouseWheelAll()
		mx = HuiGetEvent().mouse_x
		my = HuiGetEvent().mouse_y
		if plot_rect.inside(mx, my)
			onMouseWheel()
		if x_axis.rect.inside(mx, my)
			x_axis.zoom(mx, HuiGetEvent().wheel)
			redraw()
		if y_axis.rect.inside(mx, my)
			y_axis.zoom(my, HuiGetEvent().wheel)
			redraw()
	
	void onKeyDownAll()
		onKeyDown()

	void init(Panel *_panel, string _id)
		panel = _panel
		id = _id
		panel.eventOX(id, "hui:draw", self, &onDrawAll)
		panel.eventOX(id, "hui:left-button-down", self, &onLeftButtonDownAll)
		panel.eventOX(id, "hui:left-button-up", self, &onLeftButtonUpAll)
		panel.eventOX(id, "hui:right-button-down", self, &onRightButtonDownAll)
		panel.eventOX(id, "hui:right-button-up", self, &onRightButtonUpAll)
		panel.eventOX(id, "hui:mouse-move", self, &onMouseMoveAll)
		panel.eventOX(id, "hui:mouse-wheel", self, &onMouseWheelAll)
		panel.eventOX(id, "hui:key-down", self, &onKeyDownAll)
	
	virtual void onDraw(Painter *c)
	virtual void onLeftButtonDown()
	virtual void onLeftButtonUp()
	virtual void onRightButtonDown()
	virtual void onRightButtonUp()
	virtual void onMouseMove()
	virtual void onMouseWheel()
	virtual void onKeyDown()
