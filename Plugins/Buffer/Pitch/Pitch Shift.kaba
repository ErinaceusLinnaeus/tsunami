// Image = hui:properties
// Title[Deutsch] = Pitch Shift

use "../../api.kaba"

class Config : PluginData
	float Pitch

	overwrite void reset()
		Pitch = 1


class PitchShift : AudioEffect
	Config config

	ConfigPanel *panel

	complex buf1[]
	complex buf2[]

	overwrite void updateDialog()
		panel.fill()

	void shift_buf(float b[])
		int i, j
		fft_r2c(b, buf1)
		float f = 1.0 / b.num
		int ll = b.num / 2
		buf2.resize(ll)
		for i, 0, ll
			float i_s = i2f(i) / config.Pitch
			int ii_s = f2i(i_s)
			float di_s = i_s - i2f(ii_s)
			if i < ll and ii_s < ll
				buf2[i] = f*(buf1[ii_s])// * (1 - di_s) + buf[ii_s + 1] * di_s)
			else
				buf2[i] = complex(0, 0)
		fft_c2r_inv(buf2, b)

	overwrite Panel *createPanel()
		panel = new ConfigPanel(self)
		return panel

	overwrite void processTrack(BufferBox buf)
		shift_buf(buf.r)
		shift_buf(buf.l)
		buf1.clear()
		buf2.clear()


class ConfigPanel : Panel
	PitchShift *p
	void __init__(PitchShift *_p)
		addGrid("", 0, 0, 3, 1, "table2")
		setTarget("table2", 0)
		addSlider("!expandx", 0, 0, 0, 0, "pitch_slider")
		addSpinButton("\\0\\1000\\0.1", 1, 0, 0, 0, "pitch")
		addText("%", 2, 0, 0, 0, "")
		p = _p
		CreateSliderM(self, "pitch_slider", "pitch", 0, 2, 100, &onPitch, p.config.Pitch)
		
		fill()
	
	void fill()
		SliderSet(self, "pitch", p.config.Pitch)

	void onPitch()
		p.config.Pitch = SliderGet(self, "pitch")
		p.notify()

