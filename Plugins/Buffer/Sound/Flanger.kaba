// Image = hui:copy
// Title[Deutsch] = Flanger

use "../../api.kaba"

const string AutoConfigDelay = "0:0.02:0.1:1000:ms"
const string AutoConfigSpeed = "0:2:0.01:1:Hz"
const string AutoConfigDepth = "0:1:0.1:100:%"


class PluginConfig : PluginData
	float Delay
	float Speed
	float Depth
	overwrite void reset()
		Depth = 0.5
		Speed = 0.3
		Delay = 0.01

class PluginState : PluginData
	float[] temp_r
	float[] temp_l
	int offset
	overwrite void reset()
		temp_r.clear()
		temp_l.clear()
		offset = 0

class Flanger : AudioEffect
	PluginConfig config
	PluginState state

	void doFlanger(float[] b, float[] temp, int sample_rate)
		int p = state.offset % temp.num
	
		float f_a = (1 - 0.5 * config.Depth) * config.Delay
		float f_b = 0.5 * config.Depth * config.Delay
		float f_c = 1.0 / sample_rate * 2 * pi * config.Speed
	
		for bb,i in b
	
			// old value
			temp[p] = bb
		
			// shift
			float dt = f_a - cos((i + state.offset) * f_c) * f_b
			int dp = dt * sample_rate
			int p2 = p - dp
			if p2 < 0
				p2 += temp.num
		
			// add echo
			bb += temp[p2]
		
			p ++
			if p >= temp.num
				p = 0

	
	overwrite void process(BufferBox buf)
		Song *a = song
		int delay_samples = a.sample_rate * config.Delay + 1
		state.temp_r.resize(delay_samples)
		state.temp_l.resize(delay_samples)
	
		doFlanger(buf.r, state.temp_r, a.sample_rate)
		doFlanger(buf.l, state.temp_l, a.sample_rate)
	
		state.offset += buf.num
