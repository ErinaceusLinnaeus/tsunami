// Image = hui:properties
// Title[Deutsch] = Equalizer (31)

use "../../api.kaba"




const int NUM_BANDS = 31
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0

class PluginData
	float Factor[NUM_BANDS]

PluginData data


Window *EqualizerDialog
float FreqMin[NUM_BANDS], FreqMax[NUM_BANDS]

complex f[]

void equalize(float b[])
	int _STACK_ALIGNMENT_
	
/*	// normalize parameters...
	float max_factor = 0
	for i, 0, NUM_BANDS
		if data.Factor[i] > max_factor
			max_factor = data.Factor[i]
	if max_factor > 1
		for i, 0, NUM_BANDS
			data.Factor[i] /= max_factor*/
	
	
	// transform
	int len2 = b.num / 2 + 1
	f.resize(len2)
	fft_r2c(b, f)
	
	// equalize
	float w = i2f(b.num) / audio.sample_rate
	for int i, 0, NUM_BANDS
		int j0 = clamp(FreqMin[i] * w, 0, len2)
		int j1 = clamp(FreqMax[i] * w, 0, len2)
		float ff = data.Factor[i] / b.num
		for int j, j0, j1
			f[j] = f[j] * ff
		
	// transform back
	fft_c2r_inv(f, b)

void ProcessTrack(BufferBox buf)
	equalize(buf.r)
	equalize(buf.l)
	f.clear()

void ResetData()
	for int i, 0, NUM_BANDS
		data.Factor[i] = 1

void DataToDialog()
	for int i, 0, NUM_BANDS
		EqualizerDialog.SetFloat("slider_" + i, data.Factor[i] / 2)

void OnEvent()
	Event *e = HuiGetEvent()
	for int i, 0, NUM_BANDS
		if e.id == ("slider_" + i)
			data.Factor[i] = EqualizerDialog.GetFloat("") * 2


void Configure()
	// dialog
	EqualizerDialog = new FixedDialog("Equalizer",630,325,MainWin,false)
	for int i, 0, NUM_BANDS
		EqualizerDialog.AddSlider("",5 + i*20,35,20,210,"slider_" + i)
	EqualizerDialog.AddText("20",5,250,40,25,"")
	EqualizerDialog.AddText("40",65,250,40,25,"")
	EqualizerDialog.AddText("80",125,250,40,25,"")
	EqualizerDialog.AddText("160",185,250,40,25,"")
	EqualizerDialog.AddText("320",245,250,40,25,"")
	EqualizerDialog.AddText("640",305,250,40,25,"")
	EqualizerDialog.AddText("1280",365,250,40,25,"")
	EqualizerDialog.AddText("2k5",425,250,40,25,"")
	EqualizerDialog.AddText("5k",485,250,40,25,"")
	EqualizerDialog.AddText("10k",545,250,40,25,"")
	EqualizerDialog.AddText("20k",600,250,30,25,"")
	
	EqualizerDialog.Event("*", &OnEvent)

	PutCommandBarFixed(EqualizerDialog, 5, 285, 630)
	PutFavoriteBarFixed(EqualizerDialog, 5, 5, 630)


	for int i, 0, NUM_BANDS
		FreqMin[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i - 0.5))
		FreqMax[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i + 0.5))
	FreqMin[0] = 0
	FreqMax[NUM_BANDS - 1] = 1000000
	
	DataToDialog()

	EqualizerDialog.Run()

