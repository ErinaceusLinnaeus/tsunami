// Image = hui:properties
// Title[Deutsch] = Equalizer 31

use "../../api.kaba"




const int NUM_BANDS = 31
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0

class Config : PluginData
	float[NUM_BANDS] Factor

	override void reset()
		for f in Factor
			f = 1

class Equalizer31 : AudioEffect

	Config config

	ConfigDialog *dlg
	float[NUM_BANDS] FreqMin, FreqMax

	complex[] f

	override void __init__()
		for i in 0:NUM_BANDS
			FreqMin[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i - 0.5))
			FreqMax[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i + 0.5))
		FreqMin[0] = 0
		FreqMax[NUM_BANDS - 1] = 1000000

	void equalize(float[] b)
		int _STACK_ALIGNMENT_
	
		// transform
		int len2 = b.num / 2 + 1
		f.resize(len2)
		fft_r2c(b, f)

		// equalize
		float w = i2f(b.num) / song.sample_rate
		for i in 0:NUM_BANDS
			int j0 = clamp(FreqMin[i] * w, 0, len2)
			int j1 = clamp(FreqMax[i] * w, 0, len2)
			float ff = config.Factor[i] / b.num
			for j in j0:j1
				f[j] = f[j] * ff
		
		// transform back
		fft_c2r_inv(f, b)

	override void process(BufferBox buf)
		equalize(buf.r)
		equalize(buf.l)
		f.clear()

	void updateDialog()
		for i in 0:NUM_BANDS
			dlg.setFloat("slider_" + i, config.Factor[i] / 2)
	
	void configure()
		dlg = new ConfigDialog(self)
		dlg.run()

const int dx = 30

class ConfigDialog : FixedDialog
	Equalizer31 *eq


	void __init__(Equalizer31 *_eq)
		super.__init__("Equalizer",10 + dx * NUM_BANDS,325,MainWin,false)
		
		for int i, 0, NUM_BANDS
			addSlider("",5 + i*dx,35,25,210,"slider_" + i)
		addLabel("20",5,250,25,25,"")
		addLabel("40",5+3*dx,250,25,25,"")
		addLabel("80",5+6*dx,250,25,25,"")
		addLabel("160",5+9*dx,250,25,25,"")
		addLabel("320",5+12*dx,250,25,25,"")
		addLabel("640",5+15*dx,250,25,25,"")
		addLabel("1280",5+18*dx,250,25,25,"")
		addLabel("2k5",5+21*dx,250,25,25,"")
		addLabel("5k",5+24*dx,250,25,25,"")
		addLabel("10k",5+27*dx,250,25,25,"")
		addLabel("20k",5+30*dx,250,25,25,"")
		PutCommandBarFixed(self, 5, 285, dx * NUM_BANDS)
		PutFavoriteBarFixed(self, 5, 5, dx * NUM_BANDS)
	
		eq = _eq
		event("*", &onEvent)
	
	
		fill()

	void onEvent()
		Event *e = HuiGetEvent()
		for int i, 0, NUM_BANDS
			if e.id == ("slider_" + i)
				eq.config.Factor[i] = getFloat("") * 2
		
	void fill()
		for int i, 0, NUM_BANDS
			setFloat("slider_" + i, eq.config.Factor[i] / 2)
	