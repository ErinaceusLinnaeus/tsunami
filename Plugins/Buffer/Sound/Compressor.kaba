// Image = hui:properties
// Title[Deutsch] = Kompressor

use "../../api.kaba"
use "../../grid.kaba"

class Config : PluginData
	complex curve[]
	float Delay

	void fill_inter(FloatInterpolator fi)
		fi.setType("lerp")
		fi.add(curve[0].y, 0)
		for int i, 1, curve.num
			fi.add(curve[i].y, curve[i].x - curve[i - 1].x)

	overwrite void reset()
		curve.clear()
		curve.add(complex(0, 0))
		curve.add(complex(1, 1))
		Delay = 0.1
		//MouseOver = -1
		//Selected = -1


class Compressor : AudioEffect
	Config config

	ConfigDialog *win

	float level_scale

	void compress(float b[])
		float in_level = 0
		for int i, 0, b.num
			// in level
			float in_level_cur = abs(b[i])
			in_level *= level_scale
			if in_level_cur > in_level
				in_level = in_level_cur
		
			// out level
			float out_level = in_level
			for int j, 1, config.curve.num
				if (in_level >= config.curve[j - 1].x) and (in_level <= config.curve[j].x)
					out_level = config.curve[j - 1].y + (config.curve[j].y - config.curve[j - 1].y) * (in_level - config.curve[j - 1].x) / (config.curve[j].x - config.curve[j - 1].x)
					break
		
			// scale
			float factor = 1
			if in_level > 0
				factor = out_level / in_level
			b[i] = b[i] * factor

	overwrite void UpdateDialog()
		win.fill()

	overwrite void Configure()
		win = new ConfigDialog(self)
		win.Run()

	overwrite void ProcessTrack(BufferBox buf)
		AudioFile *a = audio
	
		float n_steps = config.Delay * a.sample_rate
		if n_steps > 0
			level_scale = exp(- 1 / n_steps)
		else
			level_scale = 0
		compress(buf.r)
		compress(buf.l)

class CompressorGrid : Grid
	int MouseOver, Selected
	Config *config

	overwrite void onDraw(Painter *c)
		FloatInterpolator fi
		config.fill_inter(fi)
	
		// curve
		drawCurve(c, fi, true)
		for int i, 1, config.curve.num
			drawPoint2(c, config.curve[i], i == Selected, i == MouseOver)

	// left button down -> select
	overwrite void onLeftButtonDown()
		Selected = MouseOver
		redraw()

	// right button down -> new point
	overwrite void onRightButtonDown()
		complex m = getMouse2()
		for int i, 1, config.curve.num
			complex q = config.curve[i - 1]
			complex p = config.curve[i]
			if (m.x > q.x) and (m.x < p.x) and (m.y > q.y) and (m.y < p.y)
				config.curve.insert(m, i)
				Selected = i
				redraw()
				break

	overwrite void onMouseMove()
		// move
		if HuiGetEvent().button_l and (Selected > 0)
			config.curve[Selected] = getMouse2()
			float xmin = config.curve[Selected - 1].x
			float xmax = 1
			float ymin = config.curve[Selected - 1].y
			float ymax = 1
			if Selected < config.curve.num - 1
				xmax = config.curve[Selected + 1].x
				ymax = config.curve[Selected + 1].y
			config.curve[Selected].x = clamp(config.curve[Selected].x, xmin, xmax)
			config.curve[Selected].y = clamp(config.curve[Selected].y, ymin, ymax)
			if Selected == config.curve.num - 1
				config.curve[Selected].x = 1
		else
		// mouse over?
			MouseOver = -1
			for int i, 1, config.curve.num
				if mouseOverPoint2(config.curve[i])
					MouseOver = i
		redraw()

	overwrite void onKeyDown()
		int k = HuiGetEvent().key

		// [Delete] -> remove point
		if (k == KeyDelete) and (Selected > 0) and (Selected < config.curve.num - 1)
			config.curve.remove(Selected)
			Selected = -1
			redraw()

class ConfigDialog : Dialog
	CompressorGrid grid
	void __init__(Compressor *_fx)
		super.__init__("Compressor", 500, 600, MainWin, false)
		AddGrid("", 0, 0, 1, 4, "table1")
		SetTarget("table1", 0)
		AddDrawingArea("!grabfocus", 0, 1, 0, 0, "drawing_area")
		AddGrid("!noexpandy", 0, 2, 3, 1, "table2")
		SetTarget("table2", 0)
		AddText("Delay", 0, 0, 0, 0, "")
		AddSpinButton("\\0\\10000\\0.1", 1, 0, 0, 0, "edit")
		AddText("ms", 2, 0, 0, 0, "")
		
		PutFavoriteBar(self, "table1", 0, 0)
		PutCommandBar(self, "table1", 0, 3)
		grid.config = &_fx.config
		
		EventM("edit", self, &OnEdit)
	
		grid.init(self, "drawing_area")
		grid.x_axis.set(0, 1, false, "")
		grid.y_axis.set(0, 1, false, "")
	
		fill()
	
	void fill()
		grid.redraw()
		SetFloat("edit", grid.config.Delay * 1000)

	void OnEdit()
		grid.config.Delay = GetFloat("") / 1000.0
		