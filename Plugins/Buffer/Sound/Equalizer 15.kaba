// Image = hui:properties
// Title[Deutsch] = Equalizer 15

use "../../api.kaba"




const int NUM_BANDS = 15
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0

class Config : PluginData
	float[NUM_BANDS] Factor

	overwrite void reset()
		for int i, 0, NUM_BANDS
			Factor[i] = 1

class Equalizer15 : AudioEffect

	Config config

	float[NUM_BANDS] FreqMin, FreqMax

	complex[] f

	void __init__()

		for int i, 0, NUM_BANDS
			FreqMin[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i - 0.5))
			FreqMax[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i + 0.5))
		FreqMin[0] = 0
		FreqMax[NUM_BANDS - 1] = 1000000

	void equalize(float[] b)
		int _STACK_ALIGNMENT_
	
		// transform
		int len2 = b.num / 2 + 1
		f.resize(len2)
		fft_r2c(b, f)

		// equalize
		float w = i2f(b.num) / audio.sample_rate
		for int i, 0, NUM_BANDS
			int j0 = clamp(FreqMin[i] * w, 0, len2)
			int j1 = clamp(FreqMax[i] * w, 0, len2)
			float ff = config.Factor[i] / b.num
			for int j, j0, j1
				f[j] = f[j] * ff
		
		// transform back
		fft_c2r_inv(f, b)

	overwrite void process(BufferBox buf)
		equalize(buf.r)
		equalize(buf.l)
		f.clear()
	
	overwrite ConfigPanel *createPanel()
		return new ConfigPanel(self)

class Equalizer15Panel : ConfigPanel
	overwrite Equalizer15 *c

	void __init__(Equalizer15 *_eq)
		super.__init__(_eq)
		addGrid("", 0, 1, NUM_BANDS, 2, "grid")
		setTarget("grid", 0)
		for int i, 0, NUM_BANDS
			addSlider("!expandy,vertical", i, 0, 0, 0, "slider_" + i)
			//addString("slider_" + i, "0.5:0")
		addText("20", 0, 1, 0, 0, "")
		addText("54", 2, 1, 0, 0, "")
		addText("140", 4, 1, 0, 0, "")
		addText("390", 6, 1, 0, 0, "")
		addText("1k", 8, 1, 0, 0, "")
		addText("2k8", 10, 1, 0, 0, "")
		addText("7k5", 12, 1, 0, 0, "")
		addText("20k", 14, 1, 0, 0, "")
	
		event("*", &onEvent)
	
		update()

	void onEvent()
		Event *e = HuiGetEvent()
		for int i, 0, NUM_BANDS
			if e.id == ("slider_" + i)
				c.config.Factor[i] = getFloat("") * 2
				notify()
		
	overwrite void update()
		for int i, 0, NUM_BANDS
			setFloat("slider_" + i, c.config.Factor[i] / 2)
	