// Image = hui:properties
// Title[Deutsch] = Equalizer

use "../../api.kaba"
use "../../grid.kaba"



const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0
const int MIN_VOL = 0-45
const int MAX_VOL = 45

class Config : PluginData
	vector curve[]

	void fill_inter(FloatInterpolator fi)
		fi.setType("cubic-spline-notang")
		fi.add3(curve[0].y, 0.0, exp(curve[0].z), 0.0)
		for int i, 1, curve.num
			fi.add3(curve[i].y, 0.0, exp(curve[i].z), log(curve[i].x / curve[i - 1].x))
		fi.normalize()

	overwrite void reset()
		curve.clear()
		curve.add(vector(MIN_FREQ, 0, 0))
		curve.add(vector(MAX_FREQ, 0, 0))
		//MouseOver = -1
		//Selected = -1

class Equalizer : AudioEffect

	Config config
	ConfigPanel *panel


	void equalize(float b[])
		complex f[]
	
	
		// transform
		int len2 = b.num / 2 + 1
		f.resize(len2)
		fft_r2c(b, f)
	
		FloatInterpolator fi
		config.fill_inter(fi)
	
		// equalize
		f *= (1.0 / b.num)
		float w = i2f(b.num) / audio.sample_rate
		for int i, 1, f.num
			float freq = i2f(i) / w
			float param = lin2log(freq, MIN_FREQ, MAX_FREQ)
			float db = fi.get(param)
			f[i] = f[i] * pow(10, db / 20)
		
		// transform back
		fft_c2r_inv(f, b)

	overwrite void processTrack(BufferBox buf)
		equalize(buf.r)
		equalize(buf.l)

	overwrite void updateDialog()
		panel.grid.redraw()

	overwrite Panel *createPanel()
		panel = new ConfigPanel(&config)
		return panel


class EqualizerGrid : Grid
	Config *e
	int MouseOver, Selected
	overwrite void onDraw(Painter *c)
		// curve + area below
		FloatInterpolator fi
		e.fill_inter(fi)
		drawCurve(c, fi, true)
	
		// points
		for int i, 0, e.curve.num
			drawPoint3(c, e.curve[i], i == Selected, i == MouseOver)
			

	// left button down -> select
	overwrite void onLeftButtonDown()
		Selected = MouseOver
		redraw()

	overwrite void onMouseWheel()
		if MouseOver >= 0
			if HuiGetEvent().wheel > 0
				e.curve[MouseOver].z += 0.2
			else
				e.curve[MouseOver].z -= 0.2
			redraw()

	// right button down -> new point
	overwrite void onRightButtonDown()
		vector m = getMouse3(0)
		for int i, 1, e.curve.num
			if (m.x > e.curve[i - 1].x) and (m.x < e.curve[i].x)
				e.curve.insert(m, i)
				Selected = i
				MouseOver = i
				redraw()
				break

	overwrite void onMouseMove()
		// move
		if HuiGetEvent().button_l and (Selected >= 0)
			e.curve[Selected] = getMouse3(e.curve[Selected].z)
			float xmin = MIN_FREQ
			float xmax = MAX_FREQ
			if Selected > 0
				xmin = e.curve[Selected - 1].x
			if Selected < e.curve.num - 1
				xmax = e.curve[Selected + 1].x
			e.curve[Selected].x = clamp(e.curve[Selected].x, xmin, xmax)
			e.curve[Selected].y = clamp(e.curve[Selected].y, MIN_VOL, MAX_VOL)
			if Selected == e.curve.num - 1
				e.curve[Selected].x = MAX_FREQ
			if Selected == 0
				e.curve[Selected].x = MIN_FREQ
		else
		// mouse over?
			MouseOver = -1
			for int i, 0, e.curve.num
				if mouseOverPoint3(e.curve[i])
					MouseOver = i
		redraw()

	// [Delete] -> remove point
	overwrite void onKeyDown()
		if HuiGetEvent().key_code == KeyDelete
			if Selected > 0 and Selected < e.curve.num-1
				e.curve.remove(Selected)
				Selected = -1
				redraw()

class ConfigPanel : Panel
	EqualizerGrid grid
	void __init__(Config *_e)
		addDrawingArea("!grabfocus,width=500,height=300", 0, 0, 0, 0, "drawing")
	
		grid.init(self, "drawing")
		grid.x_axis.set(MIN_FREQ, MAX_FREQ, true, "Hz")
		grid.y_axis.set(MIN_VOL, MAX_VOL, false, "db")
		grid.e = _e
	
		grid.redraw()

