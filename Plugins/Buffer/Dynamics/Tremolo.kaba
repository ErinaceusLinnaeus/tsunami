// Title[Deutsch] = Tremolo

use "../../api.kaba"


class Config : PluginData
	float Speed
	float Depth

	overwrite void reset()
		Depth = 0.5
		Speed = 6

class Tremolo : AudioEffect

	Config config
	TremoloPanel *panel

	void doTremolo(float b[])
	
		float f_c = 1.0 / audio.sample_rate * 2 * pi * config.Speed
	
		for int i, 0, b.num
			float f = 1 - (1 + cos(i * f_c)) * config.Depth * 0.5
	
			// old value
			b[i] *= f

	overwrite void updateDialog()
		panel.fill()

	overwrite Panel *createPanel()
		panel = new TremoloPanel(self)
		return panel
	
	overwrite void process(BufferBox buf)
		
		doTremolo(buf.r)
		doTremolo(buf.l)

class TremoloPanel : Panel
	Tremolo *t
	void __init__(Tremolo *_t)
		addGrid("", 0, 0, 4, 2, "table2")
		setTarget("table2", 0)
		addSlider("!expandx", 1, 0, 0, 0, "speed_slider")
		addSpinButton("\\0\\100\\0.1", 2, 0, 0, 0, "speed")
		addText("Hz", 3, 0, 0, 0, "")
		addSlider("", 1, 1, 0, 0, "depth_slider")
		addSpinButton("\\0\\100\\0.1", 2, 1, 0, 0, "depth")
		addText("%", 3, 1, 0, 0, "")
		addText("Geschwindigkeit", 0, 0, 0, 0, "")
		addText("Tiefe", 0, 1, 0, 0, "")
		
		t = _t
	
		CreateSliderM(self, "speed_slider", "speed", 0, 20,   1,  &OnSpeed, t.config.Speed)
		CreateSliderM(self, "depth_slider", "depth", 0, 1,  100,  &OnDepth, t.config.Depth)
	
		fill()

	void OnSpeed()
		t.config.Speed = SliderGet(self, "speed")
		t.notify()

	void OnDepth()
		t.config.Depth = SliderGet(self, "depth")
		t.notify()

	void fill()
		SliderSet(self, "speed", t.config.Speed)
		SliderSet(self, "depth", t.config.Depth)
	

