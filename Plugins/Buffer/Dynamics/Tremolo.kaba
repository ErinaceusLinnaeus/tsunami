// Title[Deutsch] = Tremolo

use "../../api.kaba"

bool not_first_time

TremoloDialog *dlg

class PluginData
	float Speed
	float Depth

PluginData data

void DoTremolo(float b[])
	
	float f_c = 1.0 / audio.sample_rate * 2 * pi * data.Speed
	
	for int i, 0, b.num
		float f = 1 - (1 + cos(i * f_c)) * data.Depth * 0.5
	
		// old value
		b[i] *= f

class TremoloDialog : Dialog
	void __init__()
		super.__init__("Tremolo", 460, 145, MainWin, false)
		AddGrid("", 0, 0, 1, 3, "table1")
		SetTarget("table1", 0)
		AddGrid("", 0, 1, 4, 2, "table2")
		SetTarget("table2", 0)
		AddSlider("!expandx", 1, 0, 0, 0, "speed_slider")
		AddSpinButton("\\0\\100\\0.1", 2, 0, 0, 0, "speed")
		AddText("Hz", 3, 0, 0, 0, "")
		AddSlider("", 1, 1, 0, 0, "depth_slider")
		AddSpinButton("\\0\\100\\0.1", 2, 1, 0, 0, "depth")
		AddText("%", 3, 1, 0, 0, "")
		AddText("Geschwindigkeit", 0, 0, 0, 0, "")
		AddText("Tiefe", 0, 1, 0, 0, "")
		
		PutFavoriteBar(self, "table1", 0, 0)
		PutCommandBar(self, "table1", 0, 2)
	
		CreateSliderM(self, "speed_slider", "speed", 0, 20,   1,  &OnSpeed, data.Speed)
		CreateSliderM(self, "depth_slider", "depth", 0, 1,  100,  &OnDepth, data.Depth)
	
		fill()

	void OnSpeed()
		data.Speed = SliderGet(self, "speed")

	void OnDepth()
		data.Depth = SliderGet(self, "depth")

	void fill()
		SliderSet(self, "speed", data.Speed)
		SliderSet(self, "depth", data.Depth)

void DataToDialog()
	dlg.fill()

void ResetData()
	data.Depth = 0.5
	data.Speed = 6

void Configure()
	dlg = new TremoloDialog
	dlg.Run()
	
void ProcessTrack(BufferBox buf)
		
	DoTremolo(buf.r)
	DoTremolo(buf.l)
	

