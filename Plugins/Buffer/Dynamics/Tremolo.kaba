// Title[Deutsch] = Tremolo

use "../../api.kaba"


class PluginData
	float Speed
	float Depth

class Tremolo : AudioEffect

	PluginData config
	TremoloDialog *dlg

	void DoTremolo(float b[])
	
		float f_c = 1.0 / audio.sample_rate * 2 * pi * config.Speed
	
		for int i, 0, b.num
			float f = 1 - (1 + cos(i * f_c)) * config.Depth * 0.5
	
			// old value
			b[i] *= f

	overwrite void UpdateDialog()
		dlg.fill()

	overwrite void ResetConfig()
		config.Depth = 0.5
		config.Speed = 6

	overwrite void Configure()
		dlg = new TremoloDialog(self)
		dlg.Run()
	
	overwrite void ProcessTrack(BufferBox buf)
		
		DoTremolo(buf.r)
		DoTremolo(buf.l)

class TremoloDialog : Dialog
	Tremolo *t
	void __init__(Tremolo *_t)
		super.__init__("Tremolo", 460, 145, MainWin, false)
		AddGrid("", 0, 0, 1, 3, "table1")
		SetTarget("table1", 0)
		AddGrid("", 0, 1, 4, 2, "table2")
		SetTarget("table2", 0)
		AddSlider("!expandx", 1, 0, 0, 0, "speed_slider")
		AddSpinButton("\\0\\100\\0.1", 2, 0, 0, 0, "speed")
		AddText("Hz", 3, 0, 0, 0, "")
		AddSlider("", 1, 1, 0, 0, "depth_slider")
		AddSpinButton("\\0\\100\\0.1", 2, 1, 0, 0, "depth")
		AddText("%", 3, 1, 0, 0, "")
		AddText("Geschwindigkeit", 0, 0, 0, 0, "")
		AddText("Tiefe", 0, 1, 0, 0, "")
		
		PutFavoriteBar(self, "table1", 0, 0)
		PutCommandBar(self, "table1", 0, 2)
		
		t = _t
	
		CreateSliderM(self, "speed_slider", "speed", 0, 20,   1,  &OnSpeed, t.config.Speed)
		CreateSliderM(self, "depth_slider", "depth", 0, 1,  100,  &OnDepth, t.config.Depth)
	
		fill()

	void OnSpeed()
		t.config.Speed = SliderGet(self, "speed")

	void OnDepth()
		t.config.Depth = SliderGet(self, "depth")

	void fill()
		SliderSet(self, "speed", t.config.Speed)
		SliderSet(self, "depth", t.config.Depth)
	

