// Image = hui:zoom-in
// Title[Deutsch] = LautstÃ¤rke

use "../../api.kaba"


class Config : PluginData
	float Volume
	bool Maximize
	
	overwrite void reset()
		self.Volume = 1

class Volume : AudioEffect
	VolumePanel *panel
	Config config

	overwrite void updateDialog()
		panel.fill()

	overwrite Panel *createPanel()
		panel = new VolumePanel(self)
		return panel

	overwrite void process(BufferBox buf)
		float factor = config.Volume
		if config.Maximize
			float max = 0
			for int i, 0, buf.num
				if abs(buf.r[i]) > max
					max = abs(buf.r[i])
				if abs(buf.l[i]) > max
					max = abs(buf.l[i])
			factor = 1 / max
		for int i, 0, buf.num
			buf.r[i] = clamp(buf.r[i] * factor, -1, 1)
			buf.l[i] = clamp(buf.l[i] * factor, -1, 1)
	
class VolumePanel : Panel
	Volume *v
	void __init__(Volume *_v)
		addGrid("!width=400", 0, 0, 1, 2, "table1")
		setTarget("table1", 0)
		addCheckBox("Maximieren", 0, 1, 0, 0, "max")
		addGrid("", 0, 0, 3, 1, "table2")
		setTarget("table2", 0)
		addSlider("!expandx", 0, 0, 0, 0, "volume_slider")
		addSpinButton("\\0\\1000\\0.1", 1, 0, 0, 0, "volume")
		addText("%", 2, 0, 0, 0, "")
		
		v = _v
		
		CreateSliderM(self, "volume_slider", "volume", 0, 2, 100, &onVolume, v.config.Volume)
	
		eventM("max", self, &onMax)
		
		fill()

	void fill()
		SliderSet(self, "volume", v.config.Volume)
		check("max", v.config.Maximize)
		enable("volume", !v.config.Maximize)
		enable("volume_slider", !v.config.Maximize)

	void onMax()
		v.config.Maximize = isChecked("max")
		enable("volume", !v.config.Maximize)
		enable("volume_slider", !v.config.Maximize)
		v.notify()

	void onVolume()
		v.config.Volume = SliderGet(self, "volume")
		v.notify()

