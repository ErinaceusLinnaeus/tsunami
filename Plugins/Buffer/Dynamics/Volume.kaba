// Image = hui:zoom-in
// Title[Deutsch] = LautstÃ¤rke

use "../../api.kaba"


class Config : PluginData
	float Volume
	bool Maximize
	
	overwrite void reset()
		self.Volume = 1

class Volume : AudioEffect
	Config config

	overwrite ConfigPanel *createPanel()
		return new VolumePanel(self)

	overwrite void process(BufferBox buf)
		float factor = config.Volume
		if config.Maximize
			float max = 0
			for int i, 0, buf.num
				if abs(buf.r[i]) > max
					max = abs(buf.r[i])
				if abs(buf.l[i]) > max
					max = abs(buf.l[i])
			factor = 1 / max
		for int i, 0, buf.num
			buf.r[i] = clamp(buf.r[i] * factor, -1, 1)
			buf.l[i] = clamp(buf.l[i] * factor, -1, 1)
	
class VolumePanel : ConfigPanel
	overwrite Volume *c
	void __init__(Volume *_v)
		super.__init__(_v)
		addGrid("!width=400", 0, 0, 1, 2, "table1")
		setTarget("table1", 0)
		addCheckBox("Maximieren", 0, 1, 0, 0, "max")
		addGrid("", 0, 0, 3, 1, "table2")
		setTarget("table2", 0)
		addSlider("!expandx", 0, 0, 0, 0, "volume_slider")
		addSpinButton("\\0\\1000\\0.1", 1, 0, 0, 0, "volume")
		addText("%", 2, 0, 0, 0, "")
		
		CreateSliderM(self, "volume_slider", "volume", 0, 2, 100, &onVolume, c.config.Volume)
	
		eventM("max", self, &onMax)

	overwrite void update()
		SliderSet(self, "volume", c.config.Volume)
		check("max", c.config.Maximize)
		enable("volume", !c.config.Maximize)
		enable("volume_slider", !c.config.Maximize)

	void onMax()
		c.config.Maximize = isChecked("max")
		enable("volume", !c.config.Maximize)
		enable("volume_slider", !c.config.Maximize)
		notify()

	void onVolume()
		c.config.Volume = SliderGet(self, "volume")
		notify()

