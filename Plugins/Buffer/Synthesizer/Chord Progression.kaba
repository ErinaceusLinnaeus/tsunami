// Image = hui:add
// Title[Deutsch] = Akkordfolge

use "../../api.kaba"
use "../../tone.kaba"


class PluginData
	string Chords
	float Volume
	bool DurationByBeats
	float Duration
	int Beats

PluginData data


window dlg

void ProcessTrack(BufferBox buf)
	Chord chord[]
	AudioFile *a = audio
	ParseChords(data.Chords, chord)
	
	int p0 = a.GetNextBeat(buf.offset)
	
	for int i, 0, chord.num
		int pos0 = f2i(a.sample_rate * data.Duration * i2f(i))
		int pos1 = f2i(a.sample_rate * data.Duration * i2f(i + 1))
		if data.DurationByBeats
			pos0 = p0 - buf.offset
			for int j, 0, data.Beats
				p0 = a.GetNextBeat(p0)
			pos1 = p0 - buf.offset
		AddChord(a.sample_rate, buf, pos0, pos1, chord[i], data.Volume)

void OnChords()
	data.Chords = dlg.GetString("")

void OnVolume()
	data.Volume = SliderGet(dlg, "volume")

void OnDurationTypeBeats()
	data.DurationByBeats = true
	dlg.Enable("duration", false)
	dlg.Enable("beats", true)

void OnDurationTypeTime()
	data.DurationByBeats = false
	dlg.Enable("duration", true)
	dlg.Enable("beats", false)

void OnDuration()
	data.Duration = dlg.GetFloat("")

void OnBeats()
	data.Beats = dlg.GetInt("")

void DataToDialog()
	dlg.SetString("chords", data.Chords)
	SliderSet(dlg, "volume", data.Volume)
	dlg.Check("duration_by_beats", data.DurationByBeats)
	dlg.SetFloat("duration", data.Duration)
	dlg.SetInt("beats", data.Beats)
	dlg.Enable("duration", !data.DurationByBeats)
	dlg.Enable("beats", data.DurationByBeats)

void ResetData()
	data.Volume = 0.2
	data.Chords = "aeFa"
	data.DurationByBeats = false
	data.Duration = 2.0
	data.Beats = 1

void Configure()
	// dialog
	dlg = new FixedDialog("Akkord Generator",415,185,MainWin,false)
	dlg.AddText("Akkorde:",5,35,100,25,"")
	dlg.AddEdit("",110,35,300,25,"chords")
	dlg.AddText("Lautstärke:",5,65,100,25,"")
	dlg.AddSlider("",110,65,145,25,"volume_slider")
	dlg.AddSpinButton("\\0\\100\\0.1",260,65,150,25,"volume")
	dlg.AddText("%",415,65,20,25,"")
	dlg.AddText("Dauer:",5,95,100,25,"")
	dlg.AddRadioButton("Sekunden",110,95,150,25,"duration_type:time")
	dlg.AddSpinButton("\\0\\100\\0.01",260,95,150,25,"duration")
	dlg.AddRadioButton("Schläge",110,125,150,25,"duration_type:beats")
	dlg.AddSpinButton("\\1\\100\\1",260,125,150,25,"beats")
	PutCommandBarFixed(dlg, 5, 155, 415)
	PutFavoriteBarFixed(dlg, 5, 5, 415)
	
	
	AddSlider(dlg, "volume_slider", "volume", 0, 1, 100, &OnVolume, data.Volume)
	dlg.Event("chords", &OnChords)
	dlg.Event("duration_type:beats", &OnDurationTypeBeats)
	dlg.Event("duration_type:time", &OnDurationTypeTime)
	dlg.Event("duration", &OnDuration)
	dlg.Event("beats", &OnBeats)

	dlg.SetDecimals(1)
	DataToDialog()

	dlg.Run()

