// Image = hui:add
// Title[Deutsch] = Akkordfolge

use "../../api.kaba"
use "../../tone.kaba"


class PluginData
	string Chords
	float Volume
	bool DurationByBeats
	float Duration
	int Beats

PluginData data

DummySynthesizer synth


ConfigDialog *dlg

void ProcessTrack(BufferBox buf)
	Chord chord[]
	AudioFile *a = audio
	ParseChords(data.Chords, chord)
	
	int p0 = a.GetNextBeat(buf.offset)
	synth.sample_rate = a.sample_rate
	
	for int i, 0, chord.num
		int pos0 = f2i(a.sample_rate * data.Duration * i2f(i))
		int pos1 = f2i(a.sample_rate * data.Duration * i2f(i + 1))
		if data.DurationByBeats
			pos0 = p0 - buf.offset
			for int j, 0, data.Beats
				p0 = a.GetNextBeat(p0)
			pos1 = p0 - buf.offset
		AddChord(synth, buf, pos0, pos1, chord[i], data.Volume)

class ConfigDialog : Dialog
	void __init__()
		super.__init__("Akkord Generator", 415, 185, MainWin, false)
		AddGrid("", 0, 0, 1, 3, "table1")
		SetTarget("table1", 0)
		AddGrid("", 0, 1, 4, 4, "table2")
		SetTarget("table2", 0)
		AddText("Akkorde:", 0, 0, 0, 0, "")
		AddEdit("", 1, 0, 0, 0, "chords")
		AddText("Lautstärke:", 0, 1, 0, 0, "")
		AddSlider("", 1, 1, 0, 0, "volume_slider")
		AddSpinButton("\\0\\100\\0.1", 2, 1, 0, 0, "volume")
		AddText("%", 3, 1, 0, 0, "")
		AddText("Dauer:", 0, 2, 0, 0, "")
		AddRadioButton("Sekunden", 1, 2, 0, 0, "duration_type:time")
		AddSpinButton("\\0\\100\\0.01", 2, 2, 0, 0, "duration")
		AddRadioButton("Schläge", 1, 3, 0, 0, "duration_type:beats")
		AddSpinButton("\\1\\100\\1", 2, 3, 0, 0, "beats")
		
		PutCommandBar(self, "table1", 0, 2)
		PutFavoriteBar(self, "table1", 0, 0)
	
	
		CreateSliderM(self, "volume_slider", "volume", 0, 1, 100, &OnVolume, data.Volume)
		EventM("chords", self, &OnChords)
		EventM("duration_type:beats", self, &OnDurationTypeBeats)
		EventM("duration_type:time", self, &OnDurationTypeTime)
		EventM("duration", self, &OnDuration)
		EventM("beats", self, &OnBeats)

		fill()
	
	void fill()
		SetString("chords", data.Chords)
		SliderSet(self, "volume", data.Volume)
		Check("duration_by_beats", data.DurationByBeats)
		SetFloat("duration", data.Duration)
		SetInt("beats", data.Beats)
		Enable("duration", !data.DurationByBeats)
		Enable("beats", data.DurationByBeats)

	void OnChords()
		data.Chords = GetString("")

	void OnVolume()
		data.Volume = SliderGet(self, "volume")

	void OnDurationTypeBeats()
		data.DurationByBeats = true
		Enable("duration", false)
		Enable("beats", true)

	void OnDurationTypeTime()
		data.DurationByBeats = false
		Enable("duration", true)
		Enable("beats", false)

	void OnDuration()
		data.Duration = GetFloat("")

	void OnBeats()
		data.Beats = GetInt("")

void DataToDialog()
	dlg.fill()

void ResetData()
	data.Volume = 0.2
	data.Chords = "aeFa"
	data.DurationByBeats = false
	data.Duration = 2.0
	data.Beats = 1

void Configure()
	dlg = new ConfigDialog
	dlg.Run()

