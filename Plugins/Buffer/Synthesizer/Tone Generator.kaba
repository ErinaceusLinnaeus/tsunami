// Image = hui:add
// Title[Deutsch] = Sinus Generator

use "../../api.kaba"
use "../../tone.kaba"


class PluginData
	int Pitch
	float Frequency
	float Volume

class ToneGenerator : AudioEffect
	PluginData config
	ConfigDialog *dlg
	DummySynthesizer synth

	overwrite void ProcessTrack(BufferBox buf)
		AudioFile *a = audio
		synth.sample_rate = a.sample_rate
		Range r
		r.offset = 0
		r.length = buf.num
		synth.RenderNote(buf, r, freq_to_pitch(config.Frequency), config.Volume)

		/*float f = config.Frequency

		float w_f = 1.0 / a.sample_rate * f * 2.0 * pi

		for int i, 0, buf.num
			float tt = i * w_f
			float d = sin(tt) * config.Volume
			buf.r[i] = clamp(buf.r[i] + d, -1, 1)
			buf.l[i] = clamp(buf.l[i] + d, -1, 1)*/
	
	
	overwrite void UpdateDialog()
		dlg.fill()

	overwrite void ResetConfig()
		config.Pitch = 81
		config.Volume = 0.3
		config.Frequency = pitch_to_freq(config.Pitch)

	overwrite void Configure()
		dlg = new ConfigDialog(self)
		dlg.Run()

class ConfigDialog : Dialog
	ToneGenerator *t
	void __init__(ToneGenerator *_t)
		super.__init__("Ton Generator", 420, 135, MainWin, false)
		AddGrid("", 0, 0, 1, 2, "table1")
		SetTarget("table1", 0)
		AddGrid("", 0, 0, 3, 3, "table2")
		SetTarget("table2", 0)
		AddRadioButton("Ton", 0, 0, 0, 0, "type:pitch")
		AddRadioButton("Frequenz", 0, 1, 0, 0, "type:freq")
		AddSpinButton("\\0\\100000\\0.1", 1, 1, 0, 0, "frequency")
		AddText("Hz", 2, 1, 0, 0, "")
		AddText("LautstÃ¤rke:", 0, 2, 0, 0, "")
		AddText("%", 2, 2, 0, 0, "")
		AddGrid("", 1, 0, 2, 1, "table3")
		AddGrid("", 1, 2, 2, 1, "table4")
		SetTarget("table3", 0)
		AddComboBox("", 0, 0, 0, 0, "pitch_rel")
		AddSpinButton("\\0\\8", 1, 0, 0, 0, "octave")
		SetTarget("table4", 0)
		AddSlider("", 0, 0, 0, 0, "volume_slider")
		AddSpinButton("\\0\\100\\0.1", 1, 0, 0, 0, "volume")
		
		PutCommandBar(self, "table1", 0, 1)
		
		t = _t
	
		Reset("pitch_rel")
		for int i, 0, 12
			SetString("pitch_rel", rel_pitch_name(i))
	
		CreateSliderM(self, "volume_slider", "volume", 0, 1, 100, &OnVolume, t.config.Volume)
		EventM("pitch_rel", &OnPitch)
		EventM("octave", &OnPitch)
		EventM("frequency",  &OnFrequency)
		EventM("type:freq",  &OnTypeFreq)
		EventM("type:pitch", &OnTypePitch)

		fill()

	void OnFrequency()
		t.config.Pitch = -1
		t.config.Frequency = GetFloat("")

	void ReadPitchFromDialog()
		t.config.Pitch = pitch_from_octave_and_rel(GetInt("pitch_rel"),
		                                           GetInt("octave"))

	void OnPitch()
		ReadPitchFromDialog()
		t.config.Frequency = pitch_to_freq(t.config.Pitch)
		SetFloat("frequency", t.config.Frequency)

	void OnVolume()
		t.config.Volume = SliderGet(self, "volume")

	void fill()
		bool by_pitch = t.config.Pitch >= 0
		if by_pitch
			SetInt("pitch_rel", t.config.Pitch % 12)
			SetInt("octave", pitch_get_octave(t.config.Pitch))
			Check("type:pitch", true)
		else
			Check("type:freq", true)
		SetFloat("frequency", t.config.Frequency)
		SliderSet(self, "volume", t.config.Volume)

		Enable("frequency", !by_pitch)
		Enable("octave", by_pitch)
		Enable("pitch_rel", by_pitch)

	void OnTypeFreq()
		t.config.Pitch = -1
		fill()

	void OnTypePitch()
		ReadPitchFromDialog()
		t.config.Frequency = pitch_to_freq(t.config.Pitch)
		fill()

