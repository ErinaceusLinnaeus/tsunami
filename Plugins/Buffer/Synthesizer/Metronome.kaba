// Image = hui:add
// Title[Deutsch] = Metronom

use "../../api.kaba"


class PluginData
	float BeatsPerMinute
	float Volume
	int BeatsPerMeasure

PluginData data

class PluginState
	int beat
	int dpos

PluginState state

DummySynthesizer synth

ConfigDialog *dlg

void ProcessTrack(BufferBox buf)
	AudioFile *a = audio

	float dt_m = 60.0 / data.BeatsPerMinute
	int sm_m = dt_m * a.sample_rate
	
	synth.sample_rate = a.sample_rate

	while true
		if state.beat == 0
			synth.RenderMetronomeClick(buf, state.dpos, 0, data.Volume)
		else
			synth.RenderMetronomeClick(buf, state.dpos, 1, data.Volume)
		
		state.beat ++
		if state.beat >= data.BeatsPerMeasure
			state.beat = 0
		state.dpos += sm_m
		if state.dpos >= buf.num
			state.dpos -= buf.num
			break

void ResetState()
	state.beat = 0
	state.dpos = 0

void ResetData()
	data.Volume = 0.3
	data.BeatsPerMinute = 90
	data.BeatsPerMeasure = 4

void DataToDialog()
	dlg.fill()
	
class ConfigDialog : Dialog
	void __init__()
		super.__init__("Metronom", 305, 135, MainWin, false)
		AddControlTable("", 0, 0, 1, 2, "table1")
		SetTarget("table1", 0)
		AddControlTable("", 0, 0, 2, 3, "table2")
		SetTarget("table2", 0)
		AddText("Geschwindigkeit:", 0, 0, 0, 0, "")
		AddSpinButton("!expandx\\\\0\\1000\\0.1", 1, 0, 0, 0, "beats_per_minute")
		AddText("/min", 2, 0, 0, 0, "")
		AddText("Schläge pro Takt:", 0, 1, 0, 0, "")
		AddSpinButton("\\1\\32\\1", 1, 1, 0, 0, "beats_per_measure")
		AddText("Lautstärke:", 0, 2, 0, 0, "")
		AddSpinButton("\\0\\100\\0.1", 1, 2, 0, 0, "volume")
		AddText("%", 2, 2, 0, 0, "")
		PutCommandBar(self, "table1", 0, 1)
	
		EventM("beats_per_minute", self, &OnMetroBeats)
		EventM("beats_per_measure", self, &OnMetroMeasure)
		EventM("volume", self, &OnMetroVolume)
	
		fill()

	void fill()
		SetFloat("beats_per_minute", data.BeatsPerMinute)
		SetInt("beats_per_measure", data.BeatsPerMeasure)
		SetFloat("volume", data.Volume * 100)
		//SliderSet(self, "volume", data.Volume)

	void OnMetroBeats()
		data.BeatsPerMinute = GetFloat("")

	void OnMetroMeasure()
		data.BeatsPerMeasure = GetInt("")

	void OnMetroVolume()
		data.Volume = GetFloat("") / 100
		//data.Volume = SliderGet(self, "volume")

void Configure()
	dlg = new ConfigDialog
	dlg.Run()

