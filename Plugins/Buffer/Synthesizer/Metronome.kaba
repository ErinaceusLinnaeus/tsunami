// Image = hui:add
// Title[Deutsch] = Metronom

use "../../api.kaba"
use "../../Midi/Source/Metronome.kaba"

class Config extends PluginData
	float BeatsPerMinute
	float Volume
	int BeatsPerMeasure
	override void reset()
		Volume = 0.3
		BeatsPerMinute = 90
		BeatsPerMeasure = 4



class Metronome extends AudioEffect
	Config config
	Synthesizer *synth
	MetronomeSource *source
	MidiRenderer *renderer
	
	override void __init__()
		synth = new DummySynthesizer
		source = new MetronomeSource(90, 4, DEFAULT_SAMPLE_RATE)
		renderer = new MidiRenderer(synth, source)
	
	override void __delete__()
		delete renderer
		delete source
		delete synth

	override void process(BufferBox buf)
		source.sample_rate = song.sample_rate
		source.volume = config.Volume
		source.num_beats = config.BeatsPerMeasure
		source.setBpm(config.BeatsPerMinute)
		renderer.read(buf)
		
	override ConfigPanel *createPanel()
		return new MetronomePanel(self)

	
class MetronomePanel extends ConfigPanel
	override Metronome *c
	override void __init__(Metronome *_m)
		super.__init__(_m)
		addGrid("!width=300", 0, 0, 2, 3, "table2")
		setTarget("table2", 0)
		addLabel("Geschwindigkeit:", 0, 0, 0, 0, "")
		addSpinButton("!expandx\\\\0\\1000\\0.1", 1, 0, 0, 0, "beats_per_minute")
		addLabel("/min", 2, 0, 0, 0, "")
		addLabel("Schläge pro Takt:", 0, 1, 0, 0, "")
		addSpinButton("\\1\\32\\1", 1, 1, 0, 0, "beats_per_measure")
		addLabel("Lautstärke:", 0, 2, 0, 0, "")
		addSpinButton("\\0\\100\\0.1", 1, 2, 0, 0, "volume")
		addLabel("%", 2, 2, 0, 0, "")
	
		event("beats_per_minute", &onMetroBeats)
		event("beats_per_measure", &onMetroMeasure)
		event("volume", &onMetroVolume)
	
		update()

	override void update()
		setFloat("beats_per_minute", c.config.BeatsPerMinute)
		setInt("beats_per_measure", c.config.BeatsPerMeasure)
		setFloat("volume", c.config.Volume * 100)
		//SliderSet(self, "volume", c.config.Volume)

	void onMetroBeats()
		c.config.BeatsPerMinute = getFloat("")
		notify()

	void onMetroMeasure()
		c.config.BeatsPerMeasure = getInt("")
		notify()

	void onMetroVolume()
		c.config.Volume = getFloat("") / 100
		//m.config.Volume = SliderGet(self, "volume")
		notify()

