// Image = hui:add
// Title[Deutsch] = Metronom

use "../../api.kaba"


class PluginData
	float BeatsPerMinute
	float Volume
	int BeatsPerMeasure

PluginData data

class PluginState
	int beat
	int dpos

PluginState state

window dlg

void ProcessTrack(BufferBox buf)
	AudioFile *a = audio

	float dt_m = 60.0 / data.BeatsPerMinute
	int sm_m = dt_m * a.sample_rate

	while true
		if state.beat == 0
			buf.add_click(state.dpos, data.Volume, 660.0, a.sample_rate)
		else
			buf.add_click(state.dpos, data.Volume * 0.5, 880.0, a.sample_rate)
		
		state.beat ++
		if state.beat >= data.BeatsPerMeasure
			state.beat = 0
		state.dpos += sm_m
		if state.dpos >= buf.num
			state.dpos -= buf.num
			break

void ResetState()
	state.beat = 0
	state.dpos = 0

void ResetData()
	data.Volume = 0.3
	data.BeatsPerMinute = 90
	data.BeatsPerMeasure = 4

void DataToDialog()
	dlg.SetFloat("beats_per_minute", data.BeatsPerMinute)
	dlg.SetInt("beats_per_measure", data.BeatsPerMeasure)
	dlg.SetFloat("volume", data.Volume * 100)
	//SliderSet(dlg, "volume", data.Volume)

void OnMetroBeats()
	data.BeatsPerMinute = dlg.GetFloat("")

void OnMetroMeasure()
	data.BeatsPerMeasure = dlg.GetInt("")

void OnMetroVolume()
	data.Volume = dlg.GetFloat("") / 100
	//data.Volume = SliderGet(dlg, "volume")

void Configure()
	// dialog
	dlg = new FixedDialog("Metronom",305,135,MainWin,false)
	dlg.AddText("Geschwindigkeit:",5,5,120,25,"")
	dlg.AddSpinButton("\\0\\1000\\0.1",130,5,140,25,"beats_per_minute")
	dlg.AddText("/min",275,5,30,25,"")
	dlg.AddText("Schläge pro Takt:",5,35,130,25,"")
	dlg.AddSpinButton("\\1\\32\\1",130,35,140,25,"beats_per_measure")
	dlg.AddText("Lautstärke:",5,65,120,25,"")
	dlg.AddSpinButton("\\0\\100\\0.1",130,65,140,25,"volume")
	dlg.AddText("%",275,65,30,25,"")
	PutCommandBarFixed(dlg, 5, 100, 305)

	dlg.SetDecimals(1)
	
	dlg.Event("beats_per_minute", &OnMetroBeats)
	dlg.Event("beats_per_measure", &OnMetroMeasure)
	dlg.Event("volume", &OnMetroVolume)
	
	DataToDialog()

	dlg.Run()

