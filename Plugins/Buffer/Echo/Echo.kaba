// Image = hui:copy
// Title[Deutsch] = Echo

use "../../api.kaba"

const string AUTO_CONFIG_DELAY = "0:1:1:1000:ms"
const string AUTO_CONFIG_VOLUME = "0:2:0.1:100:%"
const string AUTO_CONFIG_FEEDBACK = "0:1:0.1:100:%"


class Config extends PluginData
	float delay
	float volume
	float feedback

	override void reset()
		volume = 0.2
		feedback = 0.5
		delay = 0.15


class PluginState extends PluginData
	float[] buf_r, buf_l
	int offset

	override void reset()
		buf_r.clear()
		buf_l.clear()
		offset = 0

class Echo extends AudioEffect
	Config config
	PluginState state


	void doDelay(float[] b, float[] d)
		int p = state.offset % d.num
		for bb in b
	
			// old value
			float b0 = bb
		
			// add echo
			bb = bb + d[p] * config.volume
			//clampf(b[i], -1, 1)
		
			// add to echo
			d[p] = b0 + d[p] * config.feedback
		
			p ++
			if p >= d.num
				p = 0


	override void process(AudioBuffer buf)

		Song *a = song
		int delay_samples = a.sample_rate * config.delay
		state.buf_r.resize(delay_samples)
		state.buf_l.resize(delay_samples)
	
		doDelay(buf.r, state.buf_r)
		doDelay(buf.l, state.buf_l)
	
		state.offset += buf.length
