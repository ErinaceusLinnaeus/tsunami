// Image = hui:copy
// Title[Deutsch] = Echo

use "../../api.kaba"

bool not_first_time


class Config : PluginData
	float Delay
	float Volume
	float Feedback

	overwrite void reset()
		self.Volume = 0.2
		Feedback = 0.5
		Delay = 0.15


class PluginState : PluginData
	float buf_r[], buf_l[]
	int offset

	overwrite void reset()
		buf_r.clear()
		buf_l.clear()
		offset = 0

class Echo : AudioEffect
	Config config
	PluginState state
	DelayDialog *dlg


	void doDelay(float b[], float d[])
		int p = state.offset % d.num
		for int i, 0, b.num
	
			// old value
			float b0 = b[i]
		
			// add echo
			b[i] = b[i] + d[p] * config.Volume
			//clampf(b[i], -1, 1)
		
			// add to echo
			d[p] = b0 + d[p] * config.Feedback
		
			p ++
			if p >= d.num
				p = 0



	overwrite void updateDialog()
		dlg.fill()

	overwrite void configure()
		// dialog
		dlg = new DelayDialog(self)
		dlg.run()

	overwrite void processTrack(BufferBox buf)

		AudioFile *a = audio
		int delay_samples = a.sample_rate * config.Delay
		state.buf_r.resize(delay_samples)
		state.buf_l.resize(delay_samples)
	
		doDelay(buf.r, state.buf_r)
		doDelay(buf.l, state.buf_l)
	
		state.offset += buf.num
					

	
class DelayDialog : Dialog
	Echo *e
	void __init__(Echo *_e)
		super.__init__("Echo", 395, 175, MainWin, false)
		addGrid("", 0, 0, 1, 3, "table1")
		setTarget("table1", 0)
		addGrid("", 0, 1, 4, 3, "table2")
		setTarget("table2", 0)
		addSlider("!expandx", 1, 0, 0, 0, "volume_slider")
		addSpinButton("\\0\\200\\0.1", 2, 0, 0, 0, "volume")
		addText("%", 3, 0, 0, 0, "")
		addSlider("", 1, 1, 0, 0, "delay_slider")
		addSpinButton("\\0\\1000", 2, 1, 0, 0, "delay")
		addText("ms", 3, 1, 0, 0, "")
		addSlider("", 1, 2, 0, 0, "feedback_slider")
		addSpinButton("\\0\\100\\0.1", 2, 2, 0, 0, "feedback")
		addText("%", 3, 2, 0, 0, "")
		addText("Lautstärke", 0, 0, 0, 0, "")
		addText("Verzögerung", 0, 1, 0, 0, "")
		addText("Feedback", 0, 2, 0, 0, "")
		PutFavoriteBar(self, "table1", 0, 0)
		PutCommandBar(self, "table1", 0, 2)
		
		e = _e
	
		CreateSliderM(self, "volume_slider", "volume", 0, 2, 100, &onVolume, e.config.Volume)
		CreateSliderM(self, "feedback_slider", "feedback", 0, 1, 100, &onFeedback, e.config.Feedback)
		CreateSliderM(self, "delay_slider", "delay", 0, 1, 1000, &onDelay, e.config.Delay)
	
		fill()
		
	void fill()
		SliderSet(self, "volume", e.config.Volume)
		SliderSet(self, "feedback", e.config.Feedback)
		SliderSet(self, "delay", e.config.Delay)
		
	void onVolume()
		e.config.Volume = SliderGet(self, "volume")

	void onFeedback()
		e.config.Feedback = SliderGet(self, "feedback")

	void onDelay()
		e.config.Delay = SliderGet(self, "delay")

