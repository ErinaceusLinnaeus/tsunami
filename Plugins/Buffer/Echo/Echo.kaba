// Image = hui:copy
// Title[Deutsch] = Echo

use "../../api.kaba"

const string AutoConfigDelay = "0:1:1:1000:ms"
const string AutoConfigVolume = "0:2:0.1:100:%"
const string AutoConfigFeedback = "0:1:0.1:100:%"


class Config : PluginData
	float Delay
	float Volume
	float Feedback

	overwrite void reset()
		self.Volume = 0.2
		Feedback = 0.5
		Delay = 0.15


class PluginState : PluginData
	float buf_r[], buf_l[]
	int offset

	overwrite void reset()
		buf_r.clear()
		buf_l.clear()
		offset = 0

class Echo : AudioEffect
	Config config
	PluginState state


	void doDelay(float b[], float d[])
		int p = state.offset % d.num
		for int i, 0, b.num
	
			// old value
			float b0 = b[i]
		
			// add echo
			b[i] = b[i] + d[p] * config.Volume
			//clampf(b[i], -1, 1)
		
			// add to echo
			d[p] = b0 + d[p] * config.Feedback
		
			p ++
			if p >= d.num
				p = 0


	overwrite void process(BufferBox buf)

		AudioFile *a = audio
		int delay_samples = a.sample_rate * config.Delay
		state.buf_r.resize(delay_samples)
		state.buf_l.resize(delay_samples)
	
		doDelay(buf.r, state.buf_r)
		doDelay(buf.l, state.buf_l)
	
		state.offset += buf.num
