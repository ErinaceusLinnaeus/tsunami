// Image = hui:copy
// Title[Deutsch] = Flanger

#include "api.kaba"

bool not_first_time


window FlangerDialog

class PluginData
	float Delay
	float Speed
	float Depth

PluginData data

void ResetData()
	data.Depth = 0.5
	data.Speed = 0.3
	data.Delay = 0.01

class PluginState
	float temp_r[]
	float temp_l[]
	int delay_samples

PluginState state

void InitState(Track t)
	AudioFile *a = t.root
	state.delay_samples = a.sample_rate * data.Delay + 1
	state.temp_r.resize(state.delay_samples)
	state.temp_l.resize(state.delay_samples)

void ClearState()
	state.temp_r.clear()
	state.temp_l.clear()

void OnSpeed()
	data.Speed = SliderGet(FlangerDialog, "speed")

void OnDepth()
	data.Depth = SliderGet(FlangerDialog, "depth")

void OnDelay()
	data.Delay = SliderGet(FlangerDialog, "delay")

void DoFlanger(float b[], float temp[], int sample_rate)
	temp = 0
	int p = 0
	
	float f_a = (1 - 0.5 * data.Depth) * data.Delay
	float f_b = 0.5 * data.Depth * data.Delay
	float f_c = 1.0 / sample_rate * 2 * pi * data.Speed
	
	for int i, 0, b.num
	
		// old value
		temp[p] = b[i]
		
		// shift
		float dt = f_a - cos(i * f_c) * f_b
		int dp = dt * sample_rate
		int p2 = p - dp
		if p2 < 0
			p2 += state.delay_samples
		
		// add echo
		b[i] += temp[p2]
		//clampf(b[i], -1, 1)
		
		p ++
		if p >= state.delay_samples
			p = 0

void DataToDialog()
	SliderSet(FlangerDialog, "speed", data.Speed)
	SliderSet(FlangerDialog, "depth", data.Depth)
	SliderSet(FlangerDialog, "delay", data.Delay)
	/*SetSlider(FlangerDialog, HMM_SPEED_SLIDER, HMM_SPEED, 0, 2,    data.Speed, 1)
	SetSlider(FlangerDialog, HMM_DEPTH_SLIDER, HMM_DEPTH, 0, 1,    data.Depth, 100)
	SetSlider(FlangerDialog, HMM_DELAY_SLIDER, HMM_DELAY, 0, 0.02, data.Delay, 1000)*/

void Configure()
	// dialog
	FlangerDialog=HuiCreateDialog("Flanger",415,175,MainWin,false)
	FlangerDialog.AddSlider("",115,35,205,25,"speed_slider")
	FlangerDialog.AddSpinButton("\\0\\10000\\0.1",325,35,50,25,"speed")
	FlangerDialog.AddText("Hz",380,35,30,25,"")
	FlangerDialog.AddSlider("",115,65,205,25,"delay_slider")
	FlangerDialog.AddEdit("",325,65,50,25,"delay")
	FlangerDialog.AddText("ms",380,65,30,25,"")
	FlangerDialog.AddSlider("",115,95,205,25,"depth_slider")
	FlangerDialog.AddEdit("",325,95,50,25,"depth")
	FlangerDialog.AddText("%",380,95,30,25,"")
	FlangerDialog.AddText("Geschwindigkeit",5,35,110,25,"")
	FlangerDialog.AddText("Verz√∂gerung",5,65,100,25,"")
	FlangerDialog.AddText("Tiefe",5,95,100,25,"")
	PutCommandBarFixed(FlangerDialog, 5, 135, 415)
	FlangerDialog.Update()
	
	AddSlider(FlangerDialog, "speed_slider", "speed", 0, 2,    1,    &OnSpeed, data.Speed)
	AddSlider(FlangerDialog, "depth_slider", "depth", 0, 1,    100,  &OnDepth, data.Depth)
	AddSlider(FlangerDialog, "delay_slider", "delay", 0, 0.02, 1000, &OnDelay, data.Delay)
	
	PutFavoriteBarFixed(FlangerDialog, 5, 5, 405)
	
	FlangerDialog.SetDecimals(1)
	DataToDialog()
	
	HuiWaitTillWindowClosed(FlangerDialog)
	
void ProcessTrack(BufferBox buf, Track t, int level)
	InitState(t)
	
	AudioFile *a = t.root
	DoFlanger(buf.r, state.temp_r, a.sample_rate)
	DoFlanger(buf.l, state.temp_l, a.sample_rate)
	
	ClearState()

