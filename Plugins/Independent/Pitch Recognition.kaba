// Image = hui:find
// Title[Deutsch] = Tonerkennung
use "../api.kaba"
use "../tone.kaba"

const int freq = 44100
const int f_min = 50
const int f_max = 5000

const float threshold = 0.1


const int buf_size = 32768
float[] buf

Window *win

float get_freq(float i)
	return i / (buf_size / 2) * freq / 2

float Frequency, Volume, Error
bool LoudEnough

float error
string get_note(float f)
	float pitch = log(f / 440.0) / log(2.0) * 12 + 69
	int rel = f2i(pitch + 0.5) % 12
	error = loop(pitch, -0.5, 0.5)
	return rel_pitch_name(rel)
	//loopf(fl, 0, 1)
	//note = i2s(n)

void DrawVolume(Painter *c)
	c.setColor(color(1, 0.8, 0.8, 0.8))
	float y0 = 30
	float y1 = c.height - 5
	float h = y1 - y0
	c.drawRect(5, y0 + h * (1 - Volume), 5, h)
	c.setColor(color(1, 0.6, 0.6, 0.6))
	c.drawLine(2, (y0 + y1) / 2, 13, (y0 + y1) / 2)

void Draw()
	Painter *c = win.beginDraw("drawing")
	c.setColor(White)
	c.setFontSize(12)
	c.drawRect(0, 0, c.width, c.height)
	
	DrawVolume(c)
	
	float r = 95
	float r1 = 90
	float r2 = 100
	float x = c.width / 2
	float y = c.height - 10
	
	c.setColor(color(1, 0.4, 0.4, 0.4))
	for int i, 0, 9
		float w = i * 0.125 * pi - pi / 2
		c.drawLine(x + r1 * sin(w), y - r1 * cos(w), x + r2 * sin(w), y - r2 * cos(w))
	
	c.setColor(Black)
	if LoudEnough
		c.drawStr(x - 8, y - r2 - 22, get_note(Frequency))
		c.drawStr(x + r2 * 0.7 + 6, y - r2 * 0.7 - 22, "#")
		c.drawStr(x - r2 * 0.7 - 10, y - r2 * 0.7 - 22, "b")
		c.drawStr(5, 5, Frequency.str2(1) + " Hz")
		c.drawLine(x, y, x + r * sin(Error * pi), y - r * cos(Error * pi))
	else
		c.drawStr(x - 30, y - 20, "zu leise")
	c.end()

void OnClose()
	delete win
	win = nil

void UpdateInput()
	int[1] stack_corrector // needed for stack alignment (T_T)
	
	int num = clampi(input.cur_buf.num, 0, buf_size)
	buf.resize(buf_size)
	
	// alten Pufferinhalt verschieben
	for int i, 0, buf_size - num
		buf[i] = buf[i + num]
	
	// neuen Inhalt anfügen
	for int i, 0, num
		buf[buf_size - num + i] = (input.cur_buf.r[i] + input.cur_buf.l[i]) / 2
	
	/*float w_f = 1.0 / freq * 440.0 * 2.0 * pi
	for int i, 0, buf_size
		float tt = i * w_f
		buf[i] = f2i(val_max * sin(tt) * 0.1)*/
	
	complex[] bufc
	bufc.resize(buf.num / 2 + 1)
	fft_r2c(buf, bufc)
	float max = 0
	float fmax = 0, fmax2 = 0
	for int i, 0, bufc.num
		float f = get_freq(i)
		if f > f_min and f < f_max
			float amp = abs(bufc[i].x)
			//floatout f
			if (amp > max * 2.5) or (amp > max and f < fmax2)
				max = amp
				fmax = f
				fmax2 = f * 1.5
	max /= sqrt(buf.num) * 2 * pi
	Volume = clamp(max / threshold / 2, 0, 1)
	if max > threshold
		LoudEnough = true
		if abs(log(fmax / Frequency)) < 0.1
			Frequency *= 1 + log(fmax / Frequency) * 0.1
		else
			Frequency = fmax
		//Frequency = fmax
		Error = error
	else
		LoudEnough = false
	win.redraw("drawing")


complex[] bufc

void main()
	win = new FixedDialog("Tonerkennung", 300, 175, MainWin, false)
	win.addDrawingArea("", 5, 5, 290, 135, "drawing")
	win.addButton("Schließen", 5, 145, 290, 25, "close")
	win.setImage("close", "hui:close")
	
	win.eventSX("drawing", "hui:draw", &Draw)
	win.eventS("hui:close", &OnClose)
	win.eventS("close", &OnClose)
	


	buf.resize(1000)
	bufc.resize(1000)
	fft_r2c(buf, bufc)
	
	input.start(TRACK_TYPE_AUDIO, freq)
	input.addObserver(nil, &UpdateInput)
	
	win.run()
	
	input.removeObserver(nil)
	input.stop()
