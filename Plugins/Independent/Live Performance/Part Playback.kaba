use hui
use tsunami

enum Mode
	FORWARD
	LOOP

func get_part(s: Song, offset: int) -> TrackMarker*
	var t = s.time_track()
	if !t
		return nil
	for m in weak(t.layers[0].markers)
		if m.range.inside(offset)
			return m
	return nil



class PPPanel extends ConfigPanel
	var looper: Looper*
	
	func __init__(l: Looper)
		super.__init__(l)
		from_source("
Button forward 'Forward' expandx expandy")
		looper = &l

		event("forward", on_forward)
	
	func mut on_forward()
		print("forward")
		looper.go_forward()
		

class Looper extends TsunamiPlugin
	var runner: int
	var mode: Mode
	var chain: SignalChain*
	var renderer: SongRenderer*
	var song: Song*
	var cur_part: TrackMarker*
	
	func override on_start()
		cur_part = nil
		song = session.song
		chain = session.view.signal_chain
		renderer = session.view.renderer
		runner = hui.run_repeated(0.05, on_update)
		mode = Mode.FORWARD
		
	func override on_stop()
		hui.cancel_runner(runner)
			
	func override create_panel() -> ConfigPanel*
		return new PPPanel(self)
	
	func on_leave_part()
		print("leaving " + cur_part.text)
		#go_forward
	func mut on_enter_part()
		print("entering " + cur_part.text)
		renderer.set_range(cur_part.range)
		renderer.set_loop(true)
		mode = Mode.LOOP
	func mut go_forward()
		renderer.set_range(song.range())
		renderer.set_loop(false)
		mode = Mode.FORWARD
	
	func mut on_update()
		int pos = renderer.get_pos(0)
		var p = get_part(song, pos)
		if p != cur_part
			if cur_part
				on_leave_part()
			cur_part = p
			if cur_part
				on_enter_part()
