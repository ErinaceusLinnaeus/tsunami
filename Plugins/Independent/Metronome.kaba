// Image = hui:media-play
// Title[Deutsch] = Metronom
use "../api.kaba"

Window *dlg

const int SAMPLE_RATE = 44100
float length
float freq
float bpm
float volume

DummySynthesizer synth

int fill(BufferBox buf)
	float dt_m = 60.0 / bpm
	int sm_m = dt_m * SAMPLE_RATE
	
	buf.r = 0
	buf.l = 0
	
	int num_clicks = buf.num / sm_m + 2
	
	for int i, -1, num_clicks
		int n = buf.offset / sm_m + i
		if (n % 4) == 0
			synth.RenderMetronomeClick(buf, i * sm_m - (buf.offset % sm_m), 0, volume)
		else
			synth.RenderMetronomeClick(buf, i * sm_m - (buf.offset % sm_m), 1, volume)
	return buf.num

void OnBpm()
	bpm = dlg.GetFloat("")

void OnClose()
	output.Stop()
	delete dlg

void main()
	dlg = new Dialog("Metronome", 200, 80, MainWin, false)
	dlg.AddGrid("", 0, 0, 1, 2, "table")
	dlg.SetTarget("table", 0)
	dlg.AddSpinButton("", 0, 0, 0, 0, "bpm")
	dlg.AddButton("Close", 0, 1, 0, 0, "close")
	dlg.SetImage("close", "hui:close")
	
	freq = 880.0
	length = 0.02
	volume = 0.8
	bpm = 90.0
	dlg.SetFloat("bpm", bpm)
	
	dlg.Event("hui:close", &OnClose)
	dlg.Event("close", &OnClose)
	dlg.Event("bpm", &OnBpm)
	
	synth.sample_rate = SAMPLE_RATE
	
	output.PlayGenerated(&fill, SAMPLE_RATE)
	
	dlg.Run()
