// Image = hui:media-play
// Title[Deutsch] = Metronom
use "../api.kaba"

class MetronomeSource : MidiSource
	float length
	float freq
	float bpm
	int num_beats
	float volume

	int offset
	int cur_beat
	Window* win
	float fraction
	int sm_m
	float sample_rate
	
	void __init__(float _bpm, int _beats, int _sample_rate)
		win = nil
		sample_rate = _sample_rate
		
		freq = 880.0
		length = 0.02
		volume = 1
		bpm = _bpm
		num_beats = _beats

		offset = 0
		cur_beat = 0
		fraction = 0
		float dt_m = 60.0 / bpm
//		sm_m = dt_m * sample_rate
		
	override int read(MidiRawData midi)
		float dt_m = 60.0 / bpm
		sm_m = dt_m * sample_rate
	
		// render clicks
		while offset < midi.samples
			if cur_beat == 0
				midi.addMetronomeClick(offset, 0, volume)
			else
				midi.addMetronomeClick(offset, 1, volume)
			cur_beat = loopi(cur_beat + 1, 0, num_beats - 1)
			offset += sm_m
		offset -= midi.samples
		
		fraction = 1 - i2f(offset) / i2f(sm_m)
		
		if win
			win.redraw("area")
		return midi.samples
	
	void setBpm(float bpm_new)
		bpm = bpm_new
		
		float dt_m = 60.0 / bpm
		sm_m = dt_m * sample_rate
		
		offset = (1 - fraction) * sm_m


class MetronomeDialog : Dialog
	Metronome *metro
	
	void __init__(Metronome *_metro)
		super.__init__("Metronome", 300, 250, _metro.win, false)
		fromSource("
Grid ? '' 1 3
	DrawingArea area ''
	Grid ? '' 3 2
		Text ? 'Speed'
		SpinButton bpm '0\\1' expandx
		Text ? '/min'
		Text ? 'Beats'
		SpinButton num_beats '4\\1\\99'
		.
	Grid ? '' 2 1 buttonbar
		Button close 'Close' image=hui:close")
		
		metro = _metro
	
		setFloat("bpm", metro.source.bpm)
	
		event("hui:close", &onClose)
		event("close", &onClose)
		event("bpm", &onBpm)
		event("num_beats", &onNumBeats)
	
	override void onDraw(Painter *p)
		float w = p.width
		float h = p.height
		float x0 = w / 2
		float y0 = h / 2
		float r = min(x0, y0) * 0.9
		
		// background
		p.setColor(colors.background)
		p.drawRect(0, 0, w, h)
		
		// circle
		p.setColor(ColorInterpolate(colors.grid, colors.background, 0.5))
		p.setFill(false)
		p.drawCircle(x0, y0, r)
		
		// top point
		p.setFill(true)
		p.setColor(colors.text_soft3)
		p.drawCircle(x0, y0 - r, 3)
		
		// moving point
		p.setColor(colors.text)
		float t = metro.source.fraction - 0.5
		float phi = 2 *pi * (2*t*t*t + t/2)
		p.drawCircle(x0 - sin(phi) * r, y0 + cos(phi) * r, 5)
		
		// beat number
		int n = loopi(metro.source.cur_beat - 1, 0, metro.source.num_beats - 1) + 1
		p.drawStr(x0-7, y0-11, n)

	void onBpm()
		metro.source.setBpm(getFloat(""))

	void onNumBeats()
		metro.source.num_beats = getInt("")
		//metro.source.cur_beat = 0
		//metro.source.offset = 0

	void onClose()
		metro.end()

class Metronome : TsunamiPlugin
	OutputStream *stream
	MidiRenderer *renderer
	MetronomeSource *source
	DummySynthesizer synth
	Dialog *dlg
	
	override void onStart()
		source = new MetronomeSource(90, 4, synth.sample_rate)
		renderer = new MidiRenderer(&synth, source)
		
		dlg = new MetronomeDialog(self)
		dlg.show()
		source.win = dlg
	
		stream = new OutputStream(renderer)
		stream.setBufferSize(1024)
		stream.play()
		
	override void onEnd()
		source.win = nil
		delete dlg
		delete stream
		delete renderer
		delete source
