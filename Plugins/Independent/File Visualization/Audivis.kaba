use hui
use tsunami
use AudioVisualizer.Shader

enum Mode
	FORWARD
	LOOP

func get_part(s: Song*, offset: int) -> TrackMarker*
	var t = s.time_track()
	if !t
		return nil
	for m in weak(t.layers[0].markers)
		if m.range.inside(offset)
			return m
	return nil



class PPPanel extends ConfigPanel
	var looper: Looper*
	
	func __init__(l: Looper)
		super.__init__(l)
		from_source("
Button forward 'Forward' expandx expandy")
		looper = &l

		event("forward", on_forward)
	
	func on_forward()
		looper.go_forward()
		

class Looper extends TsunamiPlugin
	var runner: int
	var mode: Mode
	var chain: SignalChain*
	var renderer: SongRenderer*
	var song: Song*
	var cur_part: TrackMarker*
	var shader_vis: ShaderVis*
	var shader_panel: ShaderPanel*
	var shader_win: Dialog*
	
	func override on_start()
		cur_part = nil
		song = session.song
		chain = session.view.signal_chain
		renderer = session.view.renderer
		runner = hui.run_repeated(0.05, on_update)
		mode = Mode.FORWARD
		
		shader_vis = chain.add(Module.Type.AUDIO_VISUALIZER, "Shader")
		chain.connect(renderer, 0, shader_vis, 0)
		chain.connect(shader_vis, 0, session.view.output_stream, 0) # almost ...
		
		shader_win = new Dialog("Shader", 800, 800, session.win, true)
		shader_panel = shader_vis.create_panel()
		shader_win.embed(shader_panel, "", 0,0)
		shader_win.show()
		
		
	func override on_stop()
		print("stop 1")
		del shader_panel
		print("stop 2")
		del shader_win
		print("stop 3")
		del shader_vis
		hui.cancel_runner(runner)
			
	func override create_panel() -> ConfigPanel*
		return new PPPanel(self)
	
	func on_leave_part()
		print("leaving " + cur_part.text)
		#go_forward
	func on_enter_part()
		print("entering " + cur_part.text)
		renderer.set_range(cur_part.range)
		renderer.set_loop(true)
		mode = Mode.LOOP
	func go_forward()
		renderer.set_range(song.range())
		renderer.set_loop(false)
		mode = Mode.FORWARD
	
	func on_update()
		int pos = renderer.get_pos(0)
		var p = get_part(song, pos)
		if p != cur_part
			if cur_part
				on_leave_part()
			cur_part = p
			if cur_part
				on_enter_part()
