// Image = hui:find
// Title[Deutsch] = Supernova
use "../api.kaba"
use "../tone.kaba"

//const int freq = 44100
const int f_min = 50
const int f_max = 5000

const float threshold = 5.0


const int BUFFER_SIZE = 4096
BufferBox buf

SuperNovaWindow *win

class PlayedData
	int note
	int length
	int offset
	bool correct

PlayedData[] played
int Offset

float get_freq(float i)
	return i / (BUFFER_SIZE / 2) * song.sample_rate / 2

float Frequency, Volume, Error
bool LoudEnough

int[6] StringPitch
int HandPosition

Track *MidiTrack
int LessonLength

float Score
float ScoreFactor
float TotalScore

int Mode
enum
	ModeConfig
	ModeLesson
string[] LessonName

float error
string get_note(float f)
	float pitch = log(f / 440.0) / log(2.0) * 12 + 69
	int rel = f2i(pitch + 0.5) % 12
	error = loop(pitch, -0.5, 0.5)
	return rel_pitch_name(rel)
	//loopf(fl, 0, 1)
	//note = i2s(n)

class SuperNovaWindow : Window
	int hover
	float w, h
	Painter *c
	void __init__()
		super.__init__("Supernova", -1, -1, 1024, 600)
		addGrid("", 0, 0, 1, 2, "table1")
		setTarget("table1", 0)
		addDrawingArea("", 0, 0, 0, 0, "drawing")
		addGrid("", 0, 1, 4, 1, "table2")
		setTarget("table2", 0)
		addLabel("Geschwindigkeit", 0, 0, 0, 0, "")
		addComboBox("Langsam\\Normal\\Schnell", 1, 0, 0, 0, "speed")
		addButton("Lektion abbrechen", 2, 0, 0, 0, "cancel")
		addButton("SchlieÃŸen", 3, 0, 0, 0, "close")
		hover = -1
		setInt("speed", 1)
		enable("cancel", false)
		
		event("close", &onClose)
		event("cancel", &onCancel)
		setFullscreen(true)
	
	void __delete__()
		if Mode == ModeLesson
			StopLesson()
	
	void onClose()
		delete self
	
	void onCancel()
		StopLesson()
		redraw("drawing")

	void drawVolume()
		c.setColor(color(1, 0.8, 0.8, 0.8))
		float y0 = 30
		float y1 = c.height - 5
		float h = y1 - y0
		c.drawRect(5, y0 + h * (1 - Volume), 5, h)
		c.setColor(color(1, 0.6, 0.6, 0.6))
		c.drawLine(2, (y0 + y1) / 2, 13, (y0 + y1) / 2)
	
	float sample2x(float s)
		return (s - Offset) / 700.0 + 500
	
	void drawSheet(float y0)
		c.setColor(color(1, 0.6, 0.6, 0.6))
		for i in 0:6
			float ly = h - (i + 1) * 30
			c.drawLine(0, ly, w, ly)
		c.setColor(Black)
		c.setFontSize(18)
		MidiNote[] notes = MidiTrack.midi.getNotes(song.getRange())
		for n in notes
			int s, f
			for i in 0:6
				if n.pitch >= StringPitch[i] + HandPosition
					s = i
					f = n.pitch - StringPitch[i]
			float ly = h - (s + 1) * 30
			c.setColor(ColorSetHSB(0.3, (n.pitch % 12) / 12.0, 1, 1))
			c.drawRect(sample2x(n.range.offset), ly - 12, n.range.length / 700.0, 24)
			c.setColor(Black)
			c.drawStr(sample2x(n.range.offset), ly-12, f)
			
	void drawScore()
		c.setColor(Black)
		c.setFontSize(24)
		c.drawStr(w - 220, 50, Score.str2(0) + " Punkte")
		
	void drawLesson()
	
		drawVolume()
	
		if MidiTrack
			drawSheet(0)
	
		c.setColor(Gray)
		c.drawStr(20, 20, Frequency.str2(1) + "Hz")
		c.drawStr(20, 40, Volume.str())
		for p in played
			if p.correct
				c.setColor(Blue)
			else
				c.setColor(Red)
			float x = sample2x(p.offset)
			float y = h - h * (p.note - 40.0) / 60.0
			c.drawLine(x, y, x + 2, y)
		
		c.setColor(color(1, 1, 0.5, 0.5))
		c.drawLine(500, 0, 500, h)
		
		drawScore()
	
		c.setColor(Black)
		if LoudEnough
			c.drawStr(120, 20, rel_pitch_name(played[played.num - 1].note % 12))
		else
			c.drawStr(w / 2 - 30, h / 2 - 20, "zu leise")
	
	void drawConfig()
		c.setFontSize(20)
		for l,i in LessonName
			if i == hover
				c.setColor(color(1, 0.8, 0.8, 1))
				c.drawRect(100, 35 + i * 40, 300, 35)
			c.setColor(Black)
			c.drawStr(100, 38 + i * 40, l)
		c.setColor(Black)
		c.setFontSize(24)
		c.drawStr(w - 300, h - 50, TotalScore.str2(0) + " Punkte")

	overwrite void onDraw(Painter *p)
		c = p
		c.setColor(White)
		c.setFontSize(12)
		w = c.width
		h = c.height
		c.drawRect(0, 0, w, h)
		
		if Mode == ModeConfig
			drawConfig()
		else if Mode == ModeLesson
			drawLesson()
		
	overwrite void onMouseMove()
		if Mode != ModeConfig
			return
		hover = -1
		float mx = HuiGetEvent().mouse_x
		float my = HuiGetEvent().mouse_y
		for l,i in LessonName
			if mx >= 100 and mx < 400 and my >= 35 + i * 40 and my <= 70 + i * 40
				hover = i
		redraw("drawing")
	overwrite void onLeftButtonDown()
		if Mode != ModeConfig
			return
		if hover >= 0
			StartLesson(LessonName[hover])

void UpdateInput()
	int available = input.current_buffer.available()
	
	if available > BUFFER_SIZE
		input.current_buffer.moveReadPos(available - BUFFER_SIZE)
		Offset += available - BUFFER_SIZE//input.cur_buf.num
	
	int num = clampi(available, 0, BUFFER_SIZE)
	input.current_buffer.peekRef(buf, num)
	
	/*float w_f = 1.0 / freq * 440.0 * 2.0 * pi
	for int i, 0, buf_size
		float tt = i * w_f
		buf[i] = f2i(val_max * sin(tt) * 0.1)*/
	
	float[] temp = buf.r + buf.l
	
	complex[] bufc
	bufc.resize(buf.num / 2 + 1)
	fft_r2c(temp, bufc)
	float max = 0
	float fmax = 0, fmax2 = 0
	for b,i in bufc
		float f = get_freq(i)
		if f > f_min and f < f_max
			float amp = (b.x*b.x + b.y*b.y) / f
			//floatout f
			/*if (amp > max * 2.5) or (amp > max and f < fmax2)
				max = amp
				fmax = f
				fmax2 = f * 1.5*/
			if (amp > max)
				max = amp
				fmax = f
	max *= fmax
	max /= sqrt(buf.num) * 2 * pi
	Volume = clamp(max / threshold / 2, 0, 1)
	if max > threshold
		PlayedData p
		p.offset = Offset
		p.length = buf.num
		p.correct = false
		LoudEnough = true
		Frequency = fmax
		p.note = f2i(freq_to_pitch(Frequency) + 0.5)
		//Frequency = fmax
		Error = error
	
		MidiNote[] notes = MidiTrack.midi.getNotes(audio.getRange())
		for n in notes
			if n.range.offset <= Offset and n.range.offset + n.range.length > Offset
				if n.pitch == p.note
					Score += p.length * ScoreFactor
					p.correct = true
		played.add(p)
	else
		LoudEnough = false
	if played.num > 500
		played.remove(0)
		
	if Offset > LessonLength
		StopLesson()

	win.redraw("drawing")

void StartLesson(string name)
	Offset = 0
	Score = 0
	played.clear()

	storage.load(audio, AppDirectoryStatic + "Lessons/" + name + ".nami")
	for t in audio.tracks
		if t.type == TRACK_TYPE_MIDI
			MidiTrack = t
			
	float num_samples = 0
	MidiNote[] notes = MidiTrack.midi.getNotes(audio.getRange())
	for n in notes
		num_samples += n.range.length
	ScoreFactor = 100 / num_samples
	for t in audio.tags
		if t.key == "position"
			HandPosition = t.value.int()
		if t.key == "score"
			ScoreFactor = t.value.float() / num_samples * 1.2
	
	int speed = win.getInt("speed")
	if speed == 0
		audio.sample_rate = audio.sample_rate * 0.7
		ScoreFactor *= 0.6
	else if speed == 2
		audio.sample_rate = audio.sample_rate * 1.4
		ScoreFactor *= 1.5
	
	LessonLength = MidiTrack.midi.getRange().length + audio.sample_rate * 4

	bufc.resize(1000)
	
	input.start(TRACK_TYPE_AUDIO, audio.sample_rate)
	input.addObserver(nil, &UpdateInput)
	
	view.renderer.prepare(audio, audio.getRange(), false)
	view.stream.play()
	win.enable("speed", false)
	win.enable("cancel", true)
	Mode = ModeLesson

void StopLesson()
	view.stream.stop()
	input.removeObserver(nil)
	input.stop()
	TotalScore += Score
	HuiConfig.setFloat("Supernova.Score", TotalScore)
	Mode = ModeConfig
	win.enable("speed", true)
	win.enable("cancel", false)

complex[] bufc

void main()
	MidiTrack = nil
	Mode = ModeConfig
			
	win = new SuperNovaWindow
		
	StringPitch[0] = 40
	StringPitch[1] = 45 // A = 110
	StringPitch[2] = 50
	StringPitch[3] = 55
	StringPitch[4] = 59
	StringPitch[5] = 64
	HandPosition = 5
	
	LessonName.clear()
	DirEntry[] list = DirSearch(AppDirectoryStatic + "Lessons/", "*.nami", false)
	for e in list
		if e.name[0] != '0'
			LessonName.add(e.name.substr(0, -6))
		
		
	TotalScore = HuiConfig.getFloat("Supernova.Score", 0)
	
	MainWin.hide()
	win.run()
	MainWin.show()
