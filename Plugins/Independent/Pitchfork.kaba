// Image = hui:media-play
// Title[Deutsch] = Stimmgabel
use "../api.kaba"


class Renderer : AudioRendererInterface
	int offset
	float freq
	float volume
	void __init__()
		offset = 0
		freq = 880
		volume = 0.8
	overwrite int read(BufferBox buf)
		float w_f = 1.0 / i2f(sample_rate) * freq * 2.0 * pi
		for int i, 0, buf.num
			buf.r[i] = sin((i + offset) * w_f) * volume
			buf.l[i] = sin((i + offset) * w_f) * volume
		offset += buf.num
		return buf.num

class PitchFork : Dialog
	AudioStream *stream
	Renderer *renderer
	
	void __init__(Window *parent)
		super.__init__("Pitchfork", 200, 80, parent, false)
		addGrid("", 0, 0, 1, 2, "table")
		setTarget("table", 0)
		addSpinButton("", 0, 0, 0, 0, "frequency")
		addButton("Close", 0, 1, 0, 0, "close")
		setImage("close", "hui:close")
	
		event("hui:close", &onClose)
		event("close", &onClose)
		event("frequency", &onFrequency)
	
		renderer = new Renderer
		stream = new AudioStream(renderer)
		stream.play()
	
		setFloat("frequency", renderer.freq)

	void onFrequency()
		renderer.freq = getFloat("")

	void onClose()
		delete stream
		delete renderer
		delete self

void main()
	PitchFork *p = new PitchFork(MainWin)
	p.run()
