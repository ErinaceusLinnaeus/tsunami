// Image = hui:media-play
// Title[Deutsch] = Stimmgabel
use "../api.kaba"


class ForkRenderer extends AudioSource
	int offset
	float freq
	float volume
	override void __init__()
		offset = 0
		freq = 880
		volume = 0.8
	override int read(AudioBuffer buf)
		float w_f = 1.0 / i2f(DEFAULT_SAMPLE_RATE) * freq * 2.0 * pi
		for i in 0:buf.length
			buf.r[i] = sin((i + offset) * w_f) * volume
			buf.l[i] = sin((i + offset) * w_f) * volume
		offset += buf.length
		return buf.length

class PitchForkDialog extends ConfigPanel
	PitchFork *fork
	
	override void __init__(PitchFork *pf)
		add_grid("", 0, 0, "table")
		set_target("table")
		add_spin_button("!expandx", 0, 0, "frequency")
		add_label("Hz", 1, 0, "")
		
		fork = pf
	
		event("frequency", &onFrequency)
	
		set_float("frequency", fork.renderer.freq)

	void onFrequency()
		fork.renderer.freq = get_float("")

class PitchFork extends TsunamiPlugin
	OutputStream *stream
	ForkRenderer *renderer
	override void on_start()
	
		renderer = new ForkRenderer
		stream = new OutputStream(session)
		stream.plug(0, renderer, 0)
		
		stream.play()
	
	override ConfigPanel* create_panel()
		return new PitchForkDialog(self)

	override void on_stop()
		delete stream
		delete renderer
