// Image = hui:media-play
// Title[Deutsch] = Stimmgabel
use "../api.kaba"
use "../AudioSource/Sine.kaba"



class PitchForkDialog extends ConfigPanel
	override PitchFork* c
	
	override void __init__(Module *pf)
		add_grid("", 0, 0, "table")
		set_target("table")
		add_spin_button("!expandx", 0, 0, "frequency")
		add_label("Hz", 1, 0, "")
	
		event("frequency", &on_frequency)
	
		set_float("frequency", c.source.config.freq)

	void on_frequency()
		c.source.config.freq = get_float("")

class PitchFork extends TsunamiPlugin
	SignalChain* chain
	SineSource* source
	override void on_start()
	
		chain = new SignalChain(session, "pitch-fork")
		source = chain.add(MODULE_AUDIO_SOURCE, "Sine")
		Module* out = chain.add(MODULE_STREAM, "AudioOutput")
		chain.connect(source, 0, out, 0)
		
		chain.start()
	
	override ConfigPanel* create_panel()
		return new PitchForkDialog(self)

	override void on_stop()
		delete chain
