// Image = hui:media-play
// Title[Deutsch] = Stimmgabel
use "../api.kaba"


class ForkRenderer extends AudioSource
	int offset
	float freq
	float volume
	override void __init__()
		offset = 0
		freq = 880
		volume = 0.8
	override int read(AudioBuffer buf)
		float w_f = 1.0 / i2f(DEFAULT_SAMPLE_RATE) * freq * 2.0 * pi
		for i in 0:buf.length
			buf.r[i] = sin((i + offset) * w_f) * volume
			buf.l[i] = sin((i + offset) * w_f) * volume
		offset += buf.length
		return buf.length

class PitchForkDialog extends Dialog
	PitchFork *fork
	
	void __init__(PitchFork *pf)
		super.__init__("Pitchfork", 200, 80, pf.session.win, false)
		addGrid("", 0, 0, "table")
		setTarget("table")
		addSpinButton("", 0, 0, "frequency")
		addButton("Close", 0, 1, "close")
		setImage("close", "hui:close")
		
		fork = pf
	
		event("hui:close", &onClose)
		event("close", &onClose)
		event("frequency", &onFrequency)
	
		setFloat("frequency", fork.renderer.freq)

	void onFrequency()
		fork.renderer.freq = getFloat("")

	void onClose()
		fork.stop()

class PitchFork extends TsunamiPlugin
	OutputStream *stream
	ForkRenderer *renderer
	PitchForkDialog *dlg
	override void onStart()
	
		renderer = new ForkRenderer
		stream = new OutputStream(session, renderer)
		
		dlg = new PitchForkDialog(self)
		dlg.show()
		
		stream.play()
	override void onStop()
		delete dlg
		delete stream
		delete renderer
