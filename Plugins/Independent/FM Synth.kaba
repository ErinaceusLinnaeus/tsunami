// Image = hui:media-play
// Title[Deutsch] = Stimmgabel
use "../api.kaba"

Window *dlg

const int SAMPLE_RATE = 44100
float freq
float volume
float phi

float factor2, volume2, phi2

int fill(BufferBox buf)
	float freq2 = freq * factor2
	float w_f2 = freq2 * 2.0 * pi / SAMPLE_RATE
	
	for int i, 0, buf.num
		phi2 += w_f2
		float w_f = (freq + sin(phi2) * volume2) * 2.0 * pi / SAMPLE_RATE
		phi += w_f
		buf.r[i] = sin(phi) * volume
		buf.l[i] = sin(phi) * volume
	phi = loop(phi, 0, 2 * pi)
	phi2 = loop(phi2, 0, 2 * pi)
	return buf.num

void OnFrequency()
	freq = dlg.GetFloat("")

void OnFactor2()
	factor2 = dlg.GetFloat("")

void OnVolume2()
	volume2 = dlg.GetFloat("") * 200

void OnClose()
	output.Stop()
	delete dlg

void main()
	dlg = new Dialog("FM Synth", 200, 80, MainWin, false)
	dlg.AddControlTable("", 0, 0, 1, 4, "table")
	dlg.SetTarget("table", 0)
	dlg.AddSpinButton("", 0, 0, 0, 0, "frequency")
	dlg.AddSpinButton("0\\0\\100\\0.01", 0, 1, 0, 0, "factor2")
	dlg.AddSlider("", 0, 2, 0, 0, "volume2")
	dlg.AddButton("Close", 0, 3, 0, 0, "close")
	dlg.SetImage("close", "hui:close")
	
	volume = 0.8
	freq = 880.0
	phi = 0
	volume2 = 0
	factor2 = 0
	phi2 = 0
	dlg.SetFloat("frequency", freq)
	
	dlg.Event("hui:close", &OnClose)
	dlg.Event("close", &OnClose)
	dlg.Event("frequency", &OnFrequency)
	dlg.Event("factor2", &OnFactor2)
	dlg.Event("volume2", &OnVolume2)
	
	output.PlayGenerated(&fill, SAMPLE_RATE)
	output.SetBufferSize(4096)//8192)
	
	dlg.Run()
