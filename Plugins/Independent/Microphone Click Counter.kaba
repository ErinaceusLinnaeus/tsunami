// Image = hui:find
// Title[Deutsch] = Tonerkennung
use "../api.kaba"
use "../tone.kaba"
use "../MidiSource/Click.kaba"

const float THRESHOLD1 = 0.20
const float THRESHOLD2 = 0.15
const float RECOVER_TIME = 0.40

class ClickCounterDialog extends Dialog
	MicrophoneClickCounter *counter
	void __init__(MicrophoneClickCounter *_counter)
		super.__init__("Click Counter", 300, 175, _counter.session.win, false)
		add_grid("", 0, 0, "grid")
		set_target("grid")
		add_drawing_area("", 0, 0, "drawing")
		add_grid("", 0, 1, "grid2")
		set_target("grid2")
		add_button("+", 0, 0, "inc")
		add_button("-", 1, 0, "dec")
		add_button("Close", 2, 0, "close")
		set_image("close", "hui:close")
		
		counter = _counter
	
		//eventX("drawing", "hui:draw", &onDraw)
		event("inc", &on_increase)
		event("dec", &on_decrease)
		event("hui:close", &on_close)
		event("close", &on_close)


	void draw_volume(Painter *c)
		c.set_color(color(1, 0.8, 0.8, 0.8))
		float y0 = 30
		float y1 = c.height - 5
		float h = y1 - y0
		c.draw_rect(5, y0 + h * (1 - counter.Volume), 5, h)
		c.set_color(color(1, 0.6, 0.6, 0.6))
		c.draw_line(2, (y0 + y1) / 2, 13, (y0 + y1) / 2)

	override void on_draw(Painter *c)
		c.set_color(White)
		c.set_font_size(12)
		c.draw_rect(0, 0, c.width, c.height)
	
		draw_volume(c)
	
		c.set_color(Black)
		c.draw_str(100, 100, counter.counter.str())

	void on_close()
		counter.stop()

	void on_increase()
		counter.counter ++
		redraw("drawing")

	void on_decrease()
		counter.counter --
		redraw("drawing")

class MicrophoneClickCounter extends TsunamiPlugin

	InputStreamAudio *input
	OutputStream *stream
	Synthesizer *synth
	ClickSource *source

	float Volume
	int counter

	bool active
	int active_ttl
	Dialog *dlg
	
	override void on_start()

		counter = 2265
		active = false
		active_ttl = -1
	
		source = new ClickSource
		synth = CreateSynthesizer(session, "")
		synth.set_source(source.out)
		stream = new OutputStream(session, synth.out)
		stream.set_buffer_size(2048)
		stream.play()
		
		dlg = new ClickCounterDialog(self)
		dlg.show()
		
		input = new InputStreamAudio(session)
		input.start()
		input.subscribe(self, &on_input, "")
		
	override void on_stop()
		input.unsubscribe(self)
		input.stop()
		
		delete dlg
		delete input
	
		delete stream
		delete synth

	void on_input()
		int available = input.current_buffer.available()
	
		AudioBuffer buf
		input.current_buffer.read_ref(buf, available)
	
		float[] temp
		temp = buf.r + buf.l
		
		input.current_buffer.read_ref_done(buf)
	
		int recover_samples = DEFAULT_SAMPLE_RATE * RECOVER_TIME
	
		Volume = 0
		for f,i in temp
			if f > Volume
				Volume = f
			if f > THRESHOLD1
				if !active
					print("start")
					active = true
					source.click()
					stream.play()
					counter ++
					print(counter)
					active_ttl = recover_samples
			if active and (active_ttl > 0)
				if f > THRESHOLD2
					active_ttl = recover_samples
				else
					active_ttl --
			if active and (active_ttl <= 0)
				print("end")
				active = false
				active_ttl = -1
	
		dlg.redraw("drawing")
