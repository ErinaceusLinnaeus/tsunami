// Image = HuiImageZoomIn
// Deutsch = Equalizer

#include "api.kaba"




#define NUM_BANDS		15
#define MIN_FREQ		20.0
#define MAX_FREQ		20000.0

class PluginData
	float Factor[NUM_BANDS]

PluginData data



HuiWindow EqualizerDialog
float FreqMin[NUM_BANDS], FreqMax[NUM_BANDS]

complex f[]

void equalize(float b[])
	int i, j
	
	// transform
	int len2 = b.num / 2 + 1
	f.resize(len2)
	fft_r2c(b, f)

	// equalize
	float w = i2f(b.num) / cur_audio.sample_rate
	for i, 0, NUM_BANDS
		int j0 = FreqMin[i] * w
		int j1 = FreqMax[i] * w
		clamp(j0, 0, len2)
		clamp(j1, 0, len2)
		float ff = data.Factor[i] / b.num
		for j, j0, j1
			f[j] = f[j] * ff
		
	// transform back
	fft_c2r_inv(f, b)

void ProcessTrack(BufferBox buf, Track t)
	equalize(buf.r)
	equalize(buf.l)
	f.clear()

void ResetData()
	int i
	for i, 0, NUM_BANDS
		data.Factor[i] = 1

	for i, 0, NUM_BANDS
		FreqMin[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i - 0.5))
		FreqMax[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i + 0.5))
	FreqMin[0] = 0
	FreqMax[NUM_BANDS - 1] = 1000000

void DataToDialog()
	int i
	for i, 0, NUM_BANDS
		EqualizerDialog.SetFloat("slider_" + i, data.Factor[i] / 2)

void OnEvent()
	HuiEvent *e = HuiGetEvent()
	int i
	for i, 0, NUM_BANDS
		if e.id == ("slider_" + i)
			data.Factor[i] = EqualizerDialog.GetFloat("") * 2


void Configure()
	// dialog
	EqualizerDialog=HuiCreateDialog("Equalizer",455,325,MainWin,false)
	int i
	for i, 0, NUM_BANDS
		EqualizerDialog.AddSlider("",5 + i*30,35,25,210,"slider_" + i)
	EqualizerDialog.AddText("20",5,250,25,25,"")
	EqualizerDialog.AddText("54",65,250,25,25,"")
	EqualizerDialog.AddText("140",125,250,25,25,"")
	EqualizerDialog.AddText("390",185,250,25,25,"")
	EqualizerDialog.AddText("1k",245,250,25,25,"")
	EqualizerDialog.AddText("2k8",305,250,25,25,"")
	EqualizerDialog.AddText("7k5",365,250,25,25,"")
	EqualizerDialog.AddText("20k",425,250,25,25,"")
	PutCommandBarFixed(EqualizerDialog, 5, 285, 455)
	PutFavoriteBarFixed(EqualizerDialog, 5, 5, 455)
	EqualizerDialog.Update()
	
	EqualizerDialog.Event("*", &OnEvent)
	
	
	DataToDialog()

	HuiWaitTillWindowClosed(EqualizerDialog)
	