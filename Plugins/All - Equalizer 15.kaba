// Image = HuiImageZoomIn
// Deutsch = Equalizer

#include "api.kaba"




const int NUM_BANDS = 15
const float MIN_FREQ = 20.0
const float MAX_FREQ = 20000.0

class PluginData
	float Factor[NUM_BANDS]

PluginData data



window EqualizerDialog
float FreqMin[NUM_BANDS], FreqMax[NUM_BANDS]

complex f[]

void equalize(float b[])
	
	// transform
	int len2 = b.num / 2 + 1
	f.resize(len2)
	fft_r2c(b, f)

	// equalize
	float w = i2f(b.num) / cur_audio.sample_rate
	for int i, 0, NUM_BANDS
		int j0 = clamp(FreqMin[i] * w, 0, len2)
		int j1 = clamp(FreqMax[i] * w, 0, len2)
		float ff = data.Factor[i] / b.num
		for int j, j0, j1
			f[j] = f[j] * ff
		
	// transform back
	fft_c2r_inv(f, b)

void ProcessTrack(BufferBox buf, Track t)
	equalize(buf.r)
	equalize(buf.l)
	f.clear()

void ResetData()
	for int i, 0, NUM_BANDS
		data.Factor[i] = 1

	for int i, 0, NUM_BANDS
		FreqMin[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i - 0.5))
		FreqMax[i] = MIN_FREQ * exp( log(MAX_FREQ / MIN_FREQ) / (NUM_BANDS - 1) * (i + 0.5))
	FreqMin[0] = 0
	FreqMax[NUM_BANDS - 1] = 1000000

void DataToDialog()
	for int i, 0, NUM_BANDS
		EqualizerDialog.SetFloat("slider_" + i, data.Factor[i] / 2)

void OnEvent()
	event e = HuiGetEvent()
	for int i, 0, NUM_BANDS
		if e.id == ("slider_" + i)
			data.Factor[i] = EqualizerDialog.GetFloat("") * 2


void Configure()
	// dialog
	EqualizerDialog=HuiCreateDialog("Equalizer",455,325,MainWin,false)
	for int i, 0, NUM_BANDS
		EqualizerDialog.AddSlider("",5 + i*30,35,25,210,"slider_" + i)
	EqualizerDialog.AddText("20",5,250,25,25,"")
	EqualizerDialog.AddText("54",65,250,25,25,"")
	EqualizerDialog.AddText("140",125,250,25,25,"")
	EqualizerDialog.AddText("390",185,250,25,25,"")
	EqualizerDialog.AddText("1k",245,250,25,25,"")
	EqualizerDialog.AddText("2k8",305,250,25,25,"")
	EqualizerDialog.AddText("7k5",365,250,25,25,"")
	EqualizerDialog.AddText("20k",425,250,25,25,"")
	PutCommandBarFixed(EqualizerDialog, 5, 285, 455)
	PutFavoriteBarFixed(EqualizerDialog, 5, 5, 455)
	EqualizerDialog.Update()
	
	EqualizerDialog.Event("*", &OnEvent)
	
	
	DataToDialog()

	HuiWaitTillWindowClosed(EqualizerDialog)
	