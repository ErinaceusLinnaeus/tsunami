use tsunami
use helper.tone

let AUTO_CONFIG_VOLUME = "range=0:1:0.001,scale=100,unit=%"
let AUTO_CONFIG_FREQMIN = "range=1:40000,unit=Hz,square"
let AUTO_CONFIG_FREQMAX = "range=1:40000,unit=Hz,square"
let AUTO_CONFIG_PERIOD = "range=0:10:0.1,unit=s,square"


class SweepSource extends AudioSource
	class Config extends Module.Config
		var volume: float
		var freq_min, freq_max: float
		var period: float
		func override reset()
			volume = 0.8
			freq_min = 20
			freq_max = 20000
			period = 5

	var config: Config
	var phase: float
	#float freq
	var t: float
	
	func override reset_state()
		phase = 0
		#freq = config.freq_min
		t = 0

	func mut create_sweep(out buf: float[])
		var B = log(config.freq_max / config.freq_min) / config.period
		
		for i in 0:len(buf)
			buf[i] = sin(phase) * config.volume
			var freq = config.freq_min * exp(B * t)
			var w = freq * 2.0 * pi
			phase += w / float(DEFAULT_SAMPLE_RATE)
			t += 1.0 / float(DEFAULT_SAMPLE_RATE)
			if t > config.period
				t = 0
				phase = 0

	func override read(out buf: AudioBuffer) -> int
		create_sweep(buf.c[0])
		if len(buf.c) > 1
			buf.c[1] = buf.c[0]
		return len(buf)
