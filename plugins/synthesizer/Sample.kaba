use tsunami.*
use helper.tone.*


class SampleConfig extends Module.Config
	var samples: shared[SampleRef][]
	func override reset()
		samples.clear()


class SampleRenderer extends PitchRenderer
	var sample: shared[SampleRef]
	var events: MidiEventBuffer
	var override synth: SampleSynthesizer&
	func override on_config()
		sample = nil
		if pitch < len(synth.config.samples)
			sample = synth.config.samples[pitch]
	func override on_start(volume: f32)
		events.add(MidiEvent(0, 0, volume))
	func override render(out buf: AudioBuffer) -> bool
		for s in sample
			var to_del: i32[]
			for mut i=>e in events
				buf.add(s.origin.buf, e.pos, e.volume)
				e.pos -= buf.length
				if e.pos + len(*s.origin.buf) < 0
					to_del.add(i)
			for i in 0:len(to_del)
				events.remove(to_del[len(to_del)-1-i])
			return len(events) > 0
		return false
	

class SampleSynthesizer extends Synthesizer
	var config: SampleConfig
	
	func override create_pitch_renderer(pitch: i32) -> xfer[PitchRenderer]
		return new SampleRenderer(self, pitch)
	
	func override create_panel() -> xfer[ConfigPanel]
		return new SamplePanel(self)

class SamplePanel extends ConfigPanel
	var sc: SampleConfig&
	var override c: SampleSynthesizer&
	func override __init__(_s: Module)
		sc := &c.config
		add_list_view("!expandx,format=ttti\\Index\\Note\\Sample\\Preview", 0, 0, "list")
		event("list", on_select)

	func override update()
		reset("list")
		for i in 0:MAX_PITCH
			var name = ""
			var preview = ""
			if i < len(sc.samples)
				for s in sc.samples[i]
					name = s.origin.name
					preview = get_sample_preview(s.origin, c.session.view)
			set_string("list", "{{i}}\\{{pitch_name_pretty(i)}}\\{{name}}\\{{preview}}")
		
	func mut on_select()
		var n = get_int("")
		if n < 0
			return
		if n >= len(sc.samples)
			sc.samples.resize(n + 1)
		var old: Sample* = nil
		for s in sc.samples[n]
			old = &s.origin
		let f = func(ss: Sample*)
			for _s in ss
				sc.samples[n] = _s.create_ref()
			else
				sc.samples[n] = nil
			changed()
			update()
		SelectSample(c.session, self, old).then(f)
