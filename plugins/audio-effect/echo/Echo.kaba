# Image = hui:copy
# Title[Deutsch] = Echo

use tsunami.*
use helper.grid.*

let AUTO_CONFIG_DELAY = "range=0:1:0.001,scale=1000,unit=ms"
let AUTO_CONFIG_VOLUME = "volume,range=0:2"
let AUTO_CONFIG_FEEDBACK = "range=0:1:0.001,scale=100,unit=%"




class Echo extends AudioEffect
	class Config extends Module.Config
		var delay: float
		var volume: float
		var feedback: float

		func override reset()
			volume = 0.2
			feedback = 0.5
			delay = 0.15
	var config: Config
	var buf_temp: AudioBuffer
	var offset: int


	func do_delay(out b: float[], out d: float[])
		var p = offset % len(d)
		for mut bb in b
	
			# old value
			let b0 = bb
		
			# add echo
			bb = bb + d[p] * config.volume
			#clampf(b[i], -1, 1)
		
			# add to echo
			d[p] = b0 + d[p] * config.feedback
		
			p ++
			if p >= len(d)
				p = 0


	func override process(out buf: AudioBuffer)

		let delay_samples = int(session.sample_rate() * config.delay)
		buf_temp.set_channels(buf.channels)
		buf_temp.resize(delay_samples)
	
		for mut i=>c in buf.c
			do_delay(c, buf_temp.c[i])
	
		offset += len(buf)
	
	func override reset_state()
		buf_temp.clear()
		offset = 0
		on_config()

