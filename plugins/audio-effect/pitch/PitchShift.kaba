# Image = hui:properties
# Title[Deutsch] = Pitch Shift

use tsunami

let AutoConfigFactor = "range=0:2:0.001,scale=100,unit=%"

let CHUNK_SIZE = 1024
let OVERLAP = 512


class PitchShift extends AudioEffect
	class Config extends Module.Config
		var factor: float

		func override reset()
			factor = 1

	var config: Config
	
	var prev_chunk: AudioBuffer[2]
	var prev_no: int
	
	func override reset_state()
		prev_chunk[0].clear()
		prev_chunk[1].clear()
		prev_no = 0
		
	func shift_buf(out b: float[], factor: float)
		var buf1, buf2: complex[]
		fft.r2c(b, buf1)
		let N = len(b)
		let NN = len(buf1)
		buf2.resize(NN)
		for i in 0:NN
			buf2[i] = complex(0, 0)

		let D = int(max(100, sqrt(N)*2))
		for i in 0:NN
			let kk = int(factor * i)
			let x0 = pi * kk
			let j0 = max(kk - D, 0)
			let j1 = min(kk + D, NN)
			let M0 = complex(cos(x0), sin(x0)) * sin(x0)
			for j in j0:j1
				let dk = float(kk - j)
				let x = pi * dk
				if x == 0
					buf2[j] += buf1[i]
				else
					let M = M0 * (1/x)
					buf2[j] += M * buf1[i]

		fft.c2r_inv(buf2, b)
		b /= N

	func mut process_chunk(out buf: AudioBuffer)
		for c in buf.c
			shift_buf(c, config.factor)
			
		let other = 1 - prev_no
			
		prev_chunk[prev_no] = buf
		let n = min(OVERLAP, len(prev_chunk[other]))
		print("{{len(buf)}}  {{n}}")
		for i in 0:n
			let t = float(i) / n
			for k=>c in buf.c
				buf.c[k][i] = t * buf.c[k][i] + (1-t) * prev_chunk[other].c[k][i]
		
		prev_no = other
		

	func override process(out buf: AudioBuffer)
		for offset in 0:len(buf):CHUNK_SIZE
			let end = min(offset + CHUNK_SIZE, len(buf))
			process_chunk(buf[offset:end])

