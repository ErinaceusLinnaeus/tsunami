use tsunami.*

class MLPPanel extends ConfigPanel
	var override c: MidiLivePlayback&
	var session: Session&
	var synth_panel: shared[ConfigPanel]
	func __init__(_session: Session, _mlp: MidiLivePlayback)
		super.__init__(_mlp)
		session := &_session
		from_source("Grid grid ''
	Button select-synth 'select synthesizer...' expandx image=hui:open")
		event("select-synth", on_select_synth)
		
		synth_panel = nil
		
	
	func mut on_select_synth()
		del synth_panel

		let f = func(name: string)
			c.set_synth(name)
			set_string("select-synth", name)
		
			synth_panel = c.synth.create_panel()
			embed(synth_panel, "grid", 0, 1)
			for p in synth_panel
				p.update()
		
		ChooseModule(self, session, Module.Type.SYNTHESIZER, c.synth.name).then(f)

class MidiLivePlayback extends TsunamiPlugin
	var m_in, m_out: shared![Module]
	var synth: shared![Synthesizer]
	var chain: shared![SignalChain]
	func override on_start()
		chain = session.create_signal_chain("midi-live")
		m_in = chain.add_basic(Module.Type.STREAM, "MidiInput")
		m_out = chain.add_basic(Module.Type.STREAM, "AudioOutput")
		synth = chain.add_synth("")
		chain.connect(m_in, 0, synth, 0)
		chain.connect(synth, 0, m_out, 0)
		chain.start()
	
	func mut set_synth(name: string)
		chain.stop()
		chain.reset_state()
		chain.delete(synth)
		
		synth = chain.add_synth(name)
		chain.connect(m_in, 0, synth, 0)
		chain.connect(synth, 0, m_out, 0)
		
		chain.start()

	func override on_stop()
		chain = nil
	
	func override create_panel() -> xfer[ConfigPanel]
		return new MLPPanel(session, self)
